Tiny Mulberry Tapir

medium

# An attacker can borrow 0 in many markets, allowing any user to increase the number of the user's markets in Auditor, which may lead to a DOS attack

## Summary
In the Market contract, the `borrow()` function still allows anyone to borrow an asset amount of 0 for a user. However, this action will still add the market to the list of the user's markets in Auditor. This vulnerability may lead to a permanent DOS if there are enough markets for an attacker to exploit.
## Vulnerability Detail
In the Market contract, the `borrow()` function still allows borrowing of an asset amount of 0. In this case, it bypasses the `spendAllowance` check for 0 assets, so anyone can call borrow with 0 assets on behalf of a user.
```solidity=
function borrow(
    uint256 assets,
    address receiver,
    address borrower
  ) external whenNotPaused whenNotFrozen returns (uint256 borrowShares) {
    spendAllowance(borrower, assets);

    ...

    auditor.checkBorrow(this, borrower);
    asset.safeTransfer(receiver, assets);
  }
```

The `Market.borrow()` function will trigger the `Auditor.checkBorrow()` function, which will add this market to the list of the user's markets by turning on the corresponding bit in that market mask.
```solidity=
function checkBorrow(Market market, address borrower) external {
    MarketData storage m = markets[market];
    if (!m.isListed) revert MarketNotListed();

    uint256 marketMap = accountMarkets[borrower];
    uint256 marketMask = 1 << m.index;

    // validate borrow state
    if ((marketMap & marketMask) == 0) {
      // only markets may call checkBorrow if borrower not in market
      if (msg.sender != address(market)) revert NotMarket();

      accountMarkets[borrower] = marketMap | marketMask;
      emit MarketEntered(market, borrower);
    }
    ...
  }
```
Therefore, an attacker can call borrow with 0 assets to increase the number of markets in a user's market list. The Auditor contract is designed to handle up to 256 markets. If the number of markets in a user's list becomes excessive, they can suffer a DOS for every operation due to an out-of-gas revert when looping through all markets in `accountLiquidity()` function.

A significant issue is that the `exitMarket()` function in the Auditor contract still triggers the `checkShortfall()` function, which triggers `accountLiquidity()` and loops through all the markets in the user's market list to check their collateral and debt. Even when the debt and collateral are 0 (empty position), this function still loops through all the markets unnecessarily and probably runs out of gas. When this function runs out of gas due to too many markets, it can no longer be mitigated.
```solidity=
function exitMarket(Market market) external {
    MarketData storage m = markets[market];
    if (!m.isListed) revert MarketNotListed();

    (uint256 assets, uint256 debt) = market.accountSnapshot(msg.sender);

    // fail if the sender has a borrow balance
    if (debt != 0) revert RemainingDebt();

    // fail if the sender is not permitted to redeem all of their assets
    checkShortfall(market, msg.sender, assets);

    uint256 marketMap = accountMarkets[msg.sender];
    uint256 marketMask = 1 << m.index;

    if ((marketMap & marketMask) == 0) return;
    accountMarkets[msg.sender] = marketMap & ~marketMask;

    emit MarketExited(market, msg.sender);
  }
```

## Impact
Permanent DOS can occur in the operation of a user in certain cases.
## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L140-L169
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Auditor.sol#L154-L169

## Tool used

Manual Review

## Recommendation
There are several ways to mitigate this issue that you can choose from:

- Do not allow borrowing of 0 assets in the Market contract.
- Do not add a market in the `checkBorrow()` function if the debt is still 0.
- Do not trigger `checkShortfall()` when exiting a market with 0 collateral (assets)
