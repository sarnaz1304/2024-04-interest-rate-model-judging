Restless Lemon Swallow

medium

# Unassigned earnings in matured pools will not be included in `totalAssets()` calculation

## Summary

`Market.totalAssets()` includes backupEarnings from each active pool in the totalAssets calculation. If a pool is matured, all unassigned earnings are included.

[Market.sol#L925-L949](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L925-L949)
```solidity
 function totalAssets() public view override returns (uint256) {
    unchecked {
      uint256 backupEarnings = 0;

      // @audit-medium What if long matured pools have unassignedEarnings? Number could be off by a bit
      uint256 latestMaturity = block.timestamp - (block.timestamp % FixedLib.INTERVAL);
      uint256 maxMaturity = latestMaturity + maxFuturePools * FixedLib.INTERVAL;

      for (uint256 maturity = latestMaturity; maturity <= maxMaturity; maturity += FixedLib.INTERVAL) {
        FixedLib.Pool storage pool = fixedPools[maturity];
        uint256 lastAccrual = pool.lastAccrual;

        if (maturity > lastAccrual) {
          backupEarnings += block.timestamp < maturity
            ? pool.unassignedEarnings.mulDivDown(block.timestamp - lastAccrual, maturity - lastAccrual)
            : pool.unassignedEarnings;
        }
      }

      return
        floatingAssets +
        backupEarnings +
        accumulatedEarnings() +
        (totalFloatingBorrowAssets() - floatingDebt).mulWadDown(1e18 - treasuryFeeRate);
    }
  }
```

The function starts with the latest pool and loops through the rest of the active pools, including all backupEarnings. In rare cases, a matured pool could still contain unassignedEarnings, but these will not be included in the totalAssets calculation.

## Vulnerability Detail

Consider the following scenario:

1. Alice deposits 1 DAI into a fixed pool. 
2. Bob has WETH collateral and decides to borrow 5 DAI from the fixed pool.
3. 1 week before the maturity date, Alice decides that she needs to withdraw her 1 DAI which adds unassigned earnings to the pool
4. Bob is a crypto noob and loses the private key to his wallet
5. At maturity, there are still unassigned earnings in the pool which will never be added to floatingAssets because Bob can never call `repayAtMaturity()`

## Impact

Unassigned earnings in the pool are never converted to backupEarnings and added to floatingAssets. Any function that uses 'market.totalAssets()` will receive a slightly incorrect number.

## Code Snippet

```solidity
  function testEarningsStuckInPool() external {
    weth.mint(BOB, 10 ether);
    market.deposit(10 ether, ALICE);
    market.depositAtMaturity(4 weeks, 1 ether, 1 ether, ALICE);

    vm.startPrank(BOB);
    marketWETH.deposit(1.5 ether, BOB);
    auditor.enterMarket(marketWETH);
    market.borrowAtMaturity(4 weeks, 1 ether, 1.1 ether, BOB, BOB);

    vm.warp(3 weeks);
    vm.startPrank(ALICE);
    market.withdrawAtMaturity(4 weeks, .1 ether, 1, ALICE, ALICE);

    vm.warp(4 weeks);
    (,,uint256 unassignedEarnings,) = market.fixedPools(4 weeks);
    assert(unassignedEarnings > 0);
  }
```

## Tool used

Manual Review

## Recommendation

Include a function to accrue earnings on pools after maturity.
