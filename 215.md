Agreeable Lemonade Ostrich

high

# Precision Loss in `repayAtMaturity` can lead to system insolvency and loss of funds

## Summary

- A **precision loss** in the `repayAtMaturity()` function can lead to a gradual **insolvency** in the protocol, resulting in potential **losses for depositors**.

## Vulnerability Detail

- The `floatingBackupBorrowed` variable is critical for maintaining the protocol's **solvency** as it tracks the amount of assets borrowed from the floating pool by fixed pools. Accurate tracking is vital since it influences key financial operations such as **debt calculations**, **withdrawal limits**, and **share pricing**.
- each time a borrower repaying at maturity causes a decrease in `floatingBackupBorrowed`. However, due to a rounding down issue in the `scaleProportionally` function, there's a consistent precision loss of **1 wei**. This discrepancy means that the borrower's principal is reduced by 1 wei more than the amount subtracted from `floatingBackupBorrowed` and `pool.borrowed`.
- Over time, this small error can accumulate, leading to a scenario where the protocol believes there is debt remaining when, in fact, all borrowers have repaid their loans. This "phantom" debt affects the protocol's calculations, leading to incorrect **interest rates**, **share prices** ...ect .
- The precision loss originates from two functions, `scaleProportionally` and `reduceProportionally`, both using `mulDivDown` for calculations, which rounds down the results. When repaying, the `principalCovered` is calculated with `scaleProportionally`, which underestimates by 1 wei. Then, `reduceProportionally` is used to adjust the borrower's position, further reducing the principal by an additional wei due to rounding down.

```js
 function scaleProportionally(Position memory position, uint256 amount) internal pure returns (Position memory) {
   uint256 principal = amount.mulDivDown(position.principal, position.principal + position.fee);
   position.principal = principal;
   position.fee = amount - principal;
   return position;
 }
```

and then we reduce the amount `floatingBackupBorrowed` and `pool.borrowed` by principal that we get from scaleProportionally function :

```js
floatingBackupBorrowed -= pool.repay(principalCovered);
```

- when it comes to user position , we call `reduceProportionally` which also rounds down :

```js
function reduceProportionally(Position memory position, uint256 amount) internal pure returns (Position memory) {
    uint256 positionAssets = position.principal + position.fee;
    uint256 newPositionAssets = positionAssets - amount;
    position.principal = newPositionAssets.mulDivDown(position.principal, positionAssets);
    position.fee = newPositionAssets - position.principal;
    return position;
  }
```

- in this case we can alway reduce the user position by `1wei` more , Let's illustrate the precision loss issue with a step-by-step example:

- Initial state:
  - `totalFloatingBorrowed = 1000 wei`.
  - `pool[1].borrowed = 1000 wei`, `pool[1].supplied = 0 wei`.
  - User A's borrow position: `userA[1].principal = 1000 wei`, `userA[1].fee = 100 wei`.

User A decides to repay `100 wei`:

1. **Calculate `principalCovered`:**

   - Using `scaleProportionally`:
     - `principalCovered = (100 wei * 1000 wei) / (1000 wei + 100 wei) = 90 wei` (rounded down).
   - Update `floatingBackupBorrowed` and `pool[1].borrowed`:
     - `floatingBackupBorrowed = 1000 wei - 90 wei = 910 wei`.
     - `pool[1].borrowed = 910 wei`.

2. **Calculate User A's new position:**
   - Using `reduceProportionally`:
     - New total position assets after repayment: `1100 wei - 100 wei = 1000 wei`.
     - `userPrincipal = (1000 wei * 1000 wei) / 1100 wei = 909 wei` (rounded down).
     - `userA[1].fee = 1000 wei - 909 wei = 91 wei`.

**Discrepancy:**

- User A's `principal` is reduced by `91 wei`, but the `floatingBackupBorrowed` and `pool[1].borrowed` are only reduced by `90 wei`.
- Each repayment results in User A's principal being decreased by `1 wei` more than the amount subtracted from `floatingBackupBorrowed` and `pool.borrowed`.
- This mismatch can lead to a growing discrepancy between the actual debt and the debt recorded by the protocol, potentially causing solvency issues.
- The `reduceProportionally` function should use the same `principalCovered` value to avoid this precision loss.

- The precision loss issue, although seemingly minor,it can be exploited by a malicious user who repeatedly repays a debt in increments of `1 wei`. By creating a debt once and then partially repaying it, the user's principal is reduced by `1 wei` more than what is subtracted from `floatingBackupBorrowed` and `pool.borrowed`. This can be repeated in a loop, with each `repayAtMaturity` call , to slowly but consistently deplete the protocol's assets. On Layer 2 networks, where gas fees are lower, such an attack becomes more feasible, especially with assets like `WBTC` that have fewer decimals and higher value, making the attack more impactful over time.

### POC :

add this test :

```js
  function test_precisionLoss() external {
   market.deposit(12 ether, address(this));
   // deposit
   market.borrowAtMaturity(FixedLib.INTERVAL, 1 ether, 1.5 ether, address(this), address(this));

   // get values before maturity  before :
   (uint256 principalBefore, uint256 feesBefore) = market.fixedBorrowPositions(FixedLib.INTERVAL, address(this));
   (uint borrowedBefore,,,) = market.fixedPools(FixedLib.INTERVAL);
   uint backUpBefore = market.floatingBackupBorrowed();
   // audit : repaying in a loop with 1 wei each time will decrease the position principal , but not borrowedbackup :
   for (uint256 i; i < 1000; i++) {
     market.repayAtMaturity(FixedLib.INTERVAL, 1, 1, address(this));
   }
   uint backUpAfter = market.floatingBackupBorrowed();
   ( uint borrowedAfter,,,) = market.fixedPools(FixedLib.INTERVAL);
   // get user maturity :
   (uint256 principalAfter, uint256 feesAfter) = market.fixedBorrowPositions(FixedLib.INTERVAL, address(this));
   assertEq(backUpAfter,backUpBefore);// floatingBackupBorrowed didn't change
   assertEq(borrowedAfter,borrowedBefore);// pool.borrowed didn't change
   assertEq(principalBefore - principalAfter , 1000);// fixedBorrowPositions[address(this)].principal reduced by 1000 (we loop 1000 time)

 }
```

## Impact
- the increase in  `floatingBackupBorrowed` will be locked forever in the contract  even though it was paid it's not possible to withdraw it. 
-   incorrect shareToAssets calculation.
- Understates `floatingBackupBorrowed`, risking protocol **insolvency**.
- Depositors may not withdraw funds during liquidity shortages.
- Compromises system integrity, affecting debt and rate calculations.

## Code Snippet

- [scaleProportionally Function](https://github.com/exactly-finance/exactly-v1/blob/main/contracts/FixedLib.sol#L123)
- [reduceProportionally Function](https://github.com/exactly-finance/exactly-v1/blob/main/contracts/FixedLib.sol#L134)
- [repayAtMaturity Function](https://github.com/exactly-finance/exactly-v1/blob/main/contracts/Market.sol#L535)

## Tool used

Manual Review

## Recommendation

- adjust the `reduceProportionally` function to use `mulDivUp()` for rounding up, ensuring consistency with the `scaleProportionally` function which rounds down.

```diff
 function reduceProportionally(Position memory position, uint256 amount) internal pure returns (Position memory) {
-   position.principal = newPositionAssets.mulDivDown(position.principal, positionAssets);
+   position.principal = newPositionAssets.mulDivUp(position.principal, positionAssets);
    position.fee = newPositionAssets - position.principal;
    return position;
}

```

