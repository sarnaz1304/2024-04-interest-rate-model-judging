Perfect Mint Chimpanzee

high

# The function `updateFloatingDebt` must be called before every update of the `floatingAssets`,  `floatingDebt` and `floatingBackupBorrowed` variables.

## Summary

The `floatingAssets`, `floatingDebt` and `floatingBackupBorrowed` variables track the amount of floating assets deposited to the pool, the amount of assets lent by the floating pool to accounts, the amount of assets lent by the floating pool to the fixed pools respectively. The interest rate of the floating pool is calculated based on these variables. So The function [Market.updateFloatingDebt()](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L888-L904) must be called before every update of the `floatingAssets`,  `floatingDebt` and `floatingBackupBorrowed` variables.

However, in the functions [depositAtMaturity()](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L232-L237), [borrowAtMaturity()](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L281-L287), [withdrawAtMaturity()](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L363-L369) and [noTransferRepayAtMaturity()](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L467-L472), the function `updateFloatingDebt()` is not called before the update of update of these variables. As a result, the variables track wrong values and the interest rates calculated for both the floating pool and the fixed pools are incorrect. Finally, the protocol fails to operate as expected.

## Vulnerability Detail

The interest rate of the floating pool is calculated by the function `InterestRateModel.floatingRate()` that uses the utilization rates of the floating pool and the market.

```solidity
File: InterestRateModel.sol
169:   function floatingRate(uint256 uFloating, uint256 uGlobal) public view returns (uint256) {
170:     return Math.min(baseRate(uFloating, uGlobal), maxRate);
171:   }
```
This function is called by `Market.updateFloatingDebt()`.
```solidity
File: Market.sol
888:   function updateFloatingDebt() internal returns (uint256 treasuryFee) {
889:     uint256 memFloatingDebt = floatingDebt;
890:     uint256 memFloatingAssets = floatingAssets;
891:     uint256 utilization = floatingUtilization(memFloatingAssets, memFloatingDebt);
892:     uint256 newDebt = memFloatingDebt.mulWadDown(
893:       interestRateModel
894:         .floatingRate(utilization, globalUtilization(memFloatingAssets, memFloatingDebt, floatingBackupBorrowed))
895:         .mulDivDown(block.timestamp - lastFloatingDebtUpdate, 365 days)
896:     );
897: 
898:     memFloatingDebt += newDebt;
899:     treasuryFee = newDebt.mulWadDown(treasuryFeeRate);
900:     floatingAssets = memFloatingAssets + newDebt - treasuryFee;
901:     floatingDebt = memFloatingDebt;
902:     lastFloatingDebtUpdate = uint32(block.timestamp);
903:     emit FloatingDebtUpdate(block.timestamp, utilization);
904:   }
```
Here, the utilization rates of the floating pool and the market are calculated from the `floatingAssets`, `floatingDebt` and `floatingBackupBorrowed` variables. The variables track the amount of floating assets deposited to the pool, the amount of assets lent by the floating pool to accounts, the amount of assets lent by the floating pool to the fixed pools respectively.
```solidity
889:     uint256 memFloatingDebt = floatingDebt;
890:     uint256 memFloatingAssets = floatingAssets;
891:     uint256 utilization = floatingUtilization(memFloatingAssets, memFloatingDebt);
         [...]
894:     globalUtilization(memFloatingAssets, memFloatingDebt, floatingBackupBorrowed)
```
As shown above, when one of the variables(`floatingAssets`, `floatingDebt`, `floatingBackupBorrowed`) changes, the interest rate of the floating pool changes either. So before the update of the variables, the interest accrued using the `old interest rate` should be accumulated by calling the function `updateFloatingDebt`.

However,  in the function [depositAtMaturity()](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L232-L237), despite of the update of the variable `floatingAssets` from L245 and the update of `floatingBackupBorrowed` from L251, the function `updateFloatingDebt` is not called.

```solidity
File: Market.sol
232:   function depositAtMaturity(
233:     uint256 maturity,
234:     uint256 assets,
235:     uint256 minAssetsRequired,
236:     address receiver
237:   ) external whenNotPaused whenNotFrozen returns (uint256 positionAssets) {
238:     if (assets == 0) revert ZeroDeposit(); 
239:     // reverts on failure
240:     FixedLib.checkPoolState(maturity, maxFuturePools, FixedLib.State.VALID, FixedLib.State.NONE);
241: 
242:     FixedLib.Pool storage pool = fixedPools[maturity];
243: 
244:     uint256 backupEarnings = pool.accrueEarnings(maturity);
245:     floatingAssets += backupEarnings; //@audit-info 
246: 
247:     (uint256 fee, uint256 backupFee) = pool.calculateDeposit(assets, backupFeeRate);
248:     positionAssets = assets + fee;
249:     if (positionAssets < minAssetsRequired) revert Disagreement();
250: 
251:     floatingBackupBorrowed -= pool.deposit(assets); //@audit-info 
252:     pool.unassignedEarnings -= fee + backupFee;
253:     earningsAccumulator += backupFee; 
254: 
255:     // update account's position
256:     FixedLib.Position storage position = fixedDepositPositions[maturity][receiver];
257: 
258:     // if account doesn't have a current position, add it to the list
259:     if (position.principal == 0) {
260:       Account storage account = accounts[receiver];
261:       account.fixedDeposits = account.fixedDeposits.setMaturity(maturity);
262:     }
263: 
264:     position.principal += assets;
265:     position.fee += fee;
266: 
267:     emit DepositAtMaturity(maturity, msg.sender, receiver, assets, fee);
268:     emitMarketUpdate();
269:     emitFixedEarningsUpdate(maturity);
270: 
271:     asset.safeTransferFrom(msg.sender, address(this), assets);
272:   }
```

From then, the variable `floatingAssets` begans to track wrong value and the interest rate of the floating pool becomes incorrect, the market and the protocol fails to operate as expected. 

The similar errors can be found in the functions [borrowAtMaturity()](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L281-L287), [withdrawAtMaturity()](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L363-L369) and [noTransferRepayAtMaturity()](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L467-L472) as shown below.

In the `withdrawAtMaturity` function, the `floatingAssets` variable is updated from L375, but the function `updateFloatingDebt` is not called before.

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L363-L437

```solidity
File: Market.sol
363:   function withdrawAtMaturity(
364:     uint256 maturity,
365:     uint256 positionAssets,
366:     uint256 minAssetsRequired,
367:     address receiver,
368:     address owner
369:   ) external whenNotPaused returns (uint256 assetsDiscounted) {
370:     if (positionAssets == 0) revert ZeroWithdraw();
371:     // reverts on failure
372:     FixedLib.checkPoolState(maturity, maxFuturePools, FixedLib.State.VALID, FixedLib.State.MATURED);
373: 
374:     FixedLib.Pool storage pool = fixedPools[maturity];
375:     floatingAssets += pool.accrueEarnings(maturity);  //@audit-info
         [...]
```

In the `noTransferRepayAtMaturity` function, the `floatingAssets` variable is updated from L479, but the function `updateFloatingDebt` is not called before.

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L467-L536
```solidity
File: Market.sol
467:   function noTransferRepayAtMaturity(
468:     uint256 maturity,
469:     uint256 positionAssets,
470:     uint256 maxAssets,
471:     address borrower,
472:     bool canDiscount
473:   ) internal returns (uint256 actualRepayAssets) {
474:     if (positionAssets == 0) revert ZeroRepay();
475: 
476:     FixedLib.Pool storage pool = fixedPools[maturity];
477: 
478:     uint256 backupEarnings = pool.accrueEarnings(maturity);
479:     floatingAssets += backupEarnings; //@audit-info
         [...]
```

In the `borrowAtMaturity` function, the `floatingAssets` variable is updated from L293, but the function `updateFloatingDebt` is not called before.

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L281-L354
```solidity 
File: Market.sol
281:   function borrowAtMaturity(
282:     uint256 maturity,
283:     uint256 assets,
284:     uint256 maxAssets,
285:     address receiver,
286:     address borrower
287:   ) external whenNotPaused whenNotFrozen returns (uint256 assetsOwed) {
288:     if (assets == 0) revert ZeroBorrow();
289:     // reverts on failure
290:     FixedLib.checkPoolState(maturity, maxFuturePools, FixedLib.State.VALID, FixedLib.State.NONE);
291: 
292:     FixedLib.Pool storage pool = fixedPools[maturity];
293:     floatingAssets += pool.accrueEarnings(maturity); //@audit-info 
294: 
295:     RewardsController memRewardsController = rewardsController;
296:     if (address(memRewardsController) != address(0)) memRewardsController.handleBorrow(borrower);
297: 
298:     {
299:       uint256 backupDebtAddition = pool.borrow(assets);
300:       if (backupDebtAddition != 0) {
301:         uint256 newFloatingBackupBorrowed = floatingBackupBorrowed + backupDebtAddition;
302:         depositToTreasury(updateFloatingDebt()); //@audit-info calling after the update of floatingAssets
303:         if (newFloatingBackupBorrowed + floatingDebt > floatingAssets.mulWadDown(1e18 - reserveFactor)) {
304:           revert InsufficientProtocolLiquidity();
305:         }
306:         floatingBackupBorrowed = newFloatingBackupBorrowed;
307:       }
308:     }

         [...]
```

## Impact

The `floatingAssets`, `floatingDebt` and `floatingBackupBorrowed` variables track wrong values.
As a result, the calculated interest rate of the floating pool becomes incorrect, the floating pool and the fixed pools does not work as expected. Finally the protocol fails to operate as expected. 

## Tool used

Manual Review

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L208-L209

## Recommendation

```diff
File: Market.sol
232:   function depositAtMaturity(
233:     uint256 maturity,
234:     uint256 assets,
235:     uint256 minAssetsRequired,
236:     address receiver
237:   ) external whenNotPaused whenNotFrozen returns (uint256 positionAssets) {
238:     if (assets == 0) revert ZeroDeposit(); 
239:     // reverts on failure
240:     FixedLib.checkPoolState(maturity, maxFuturePools, FixedLib.State.VALID, FixedLib.State.NONE);
241: 
242:     FixedLib.Pool storage pool = fixedPools[maturity];
243: 
244:     uint256 backupEarnings = pool.accrueEarnings(maturity);
+        depositToTreasury(updateFloatingDebt());
245:     floatingAssets += backupEarnings;
246:     [...]
```

```diff 
File: Market.sol
281:   function borrowAtMaturity(
282:     uint256 maturity,
283:     uint256 assets,
284:     uint256 maxAssets,
285:     address receiver,
286:     address borrower
287:   ) external whenNotPaused whenNotFrozen returns (uint256 assetsOwed) {
288:     if (assets == 0) revert ZeroBorrow();
289:     // reverts on failure
290:     FixedLib.checkPoolState(maturity, maxFuturePools, FixedLib.State.VALID, FixedLib.State.NONE);
291: 
292:     FixedLib.Pool storage pool = fixedPools[maturity];
+        depositToTreasury(updateFloatingDebt());
293:     floatingAssets += pool.accrueEarnings(maturity);
294: 
295:     RewardsController memRewardsController = rewardsController;
296:     if (address(memRewardsController) != address(0)) memRewardsController.handleBorrow(borrower);
297: 
298:     {
299:       uint256 backupDebtAddition = pool.borrow(assets);
300:       if (backupDebtAddition != 0) {
301:         uint256 newFloatingBackupBorrowed = floatingBackupBorrowed + backupDebtAddition;
-            depositToTreasury(updateFloatingDebt());
303:         if (newFloatingBackupBorrowed + floatingDebt > floatingAssets.mulWadDown(1e18 - reserveFactor)) {
304:           revert InsufficientProtocolLiquidity();
305:         }
306:         floatingBackupBorrowed = newFloatingBackupBorrowed;
307:       }
308:     }
         [...]
```

```diff
File: Market.sol
363:   function withdrawAtMaturity(
364:     uint256 maturity,
365:     uint256 positionAssets,
366:     uint256 minAssetsRequired,
367:     address receiver,
368:     address owner
369:   ) external whenNotPaused returns (uint256 assetsDiscounted) {
370:     if (positionAssets == 0) revert ZeroWithdraw();
371:     // reverts on failure
372:     FixedLib.checkPoolState(maturity, maxFuturePools, FixedLib.State.VALID, FixedLib.State.MATURED);
373: 
374:     FixedLib.Pool storage pool = fixedPools[maturity];
+        depositToTreasury(updateFloatingDebt());
375:     floatingAssets += pool.accrueEarnings(maturity);
         [...]
```

```diff
File: Market.sol
467:   function noTransferRepayAtMaturity(
468:     uint256 maturity,
469:     uint256 positionAssets,
470:     uint256 maxAssets,
471:     address borrower,
472:     bool canDiscount
473:   ) internal returns (uint256 actualRepayAssets) {
474:     if (positionAssets == 0) revert ZeroRepay();
475: 
476:     FixedLib.Pool storage pool = fixedPools[maturity];
477: 
478:     uint256 backupEarnings = pool.accrueEarnings(maturity);
+        depositToTreasury(updateFloatingDebt());
479:     floatingAssets += backupEarnings;
         [...]
```