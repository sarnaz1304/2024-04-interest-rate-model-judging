Trendy Cedar Wasp

medium

# When the borrower is liquidated, there will be some funds untracked in the market.

## Summary
In the `market`, all `funds` should be tracked accurately, whether they are currently held, `borrowed` by `borrowers`, or repaid in the future.
To ensure this, the `market` has a sophisticated tracking system that functions effectively.
However, there will be some funds left untracked in the `market` when the `borrower` of the `maturity pool` is liquidated before `maturity`.
## Vulnerability Detail
Users have the option to deposit into the `market` directly or into specific `fixed rate pools`.
When `borrowers` `borrow` funds from the `fixed rate pool`, they are backed by the `fixed deposits` first.
If there is a shortfall in funds, the remaining `debt` is supported by `floating assets`.
The movement of funds between `fixed borrowers` and `fixed depositors` is straightforward outside of the `tracking system`.
The `tracking system` within the `market` primarily monitors funds within the `variable pool` itself.
To simplify the scenario, let's assume there are no `fixed depositors` involved.

First, there are `extraordinary earnings`, including `variable backup fees`, `late fixed repayment penalties`, etc.
The `earnings accumulator` is responsible for collecting these earnings from `extraordinary` sources and subsequently distributing them gradually and smoothly.
For this purpose, there is a `earningsAccumulator` variable.
```solidity
function depositAtMaturity(
  uint256 maturity,
  uint256 assets,
  uint256 minAssetsRequired,
  address receiver
) external whenNotPaused whenNotFrozen returns (uint256 positionAssets) {
    earningsAccumulator += backupFee;
}
```
When users deposit funds into the `variable pool`, the `floatingAssets` increase by the deposited amounts as well as any additional earnings from the `earnings accumulator`.
```solidity
function afterDeposit(uint256 assets, uint256) internal override whenNotPaused whenNotFrozen {
  updateFloatingAssetsAverage();
  uint256 treasuryFee = updateFloatingDebt();
  uint256 earnings = accrueAccumulatedEarnings();  // @audit, here
  floatingAssets += earnings + assets;  // @audit, here
  depositToTreasury(treasuryFee);
  emitMarketUpdate();
}
```
Funds borrowed by `variable rate borrowers` are tracked using the `floatingDebt` variable, while funds borrowed by `fixed rate borrowers` are tracked using the `floatingBackupBorrowed` variable.
Additionally, there is an `unassignedEarnings` variable for each `maturity pool`, which represents upcoming `fees` from `borrowers`.
These earnings are added to the `floatingAssets` whenever there are changes in the `market`, such as `borrowers` repaying their `debt` , depositors withdrawing their funds etc.
```solidity
function depositAtMaturity(
  uint256 maturity,
  uint256 assets,
  uint256 minAssetsRequired,
  address receiver
) external whenNotPaused whenNotFrozen returns (uint256 positionAssets) {
  uint256 backupEarnings = pool.accrueEarnings(maturity); // @audit, here
  floatingAssets += backupEarnings;
}
```
While this variable is important, it is not directly involved in the `tracking system`.

Let's describe the vulnerability.

For each `maturity`, there is a specific `maturity pool` with its own states.
Consider a user who deposits 5 `DAI` into the `DAI market`.
At this point, `1 DAI` is equivalent to `5000 WETH`.
With this deposit, this user can `borrow` `2000 WETH` from the `WETH market` and `2 DAI` from the `DAI market`, as he has sufficient `collateral`.
At this stage, the `market`'s tracking values are as follows:
```solidity
floatingAssets before liquidate             ==>   100000000000000000000000
floatingDebt before liquidate               ==>   0
floatingBackupBorrowed before liquidate     ==>   2000000000000000000000
earningsAccumulator before liquidate        ==>   0
unassignedEarnings before liquidate         ==>   46027397260273972000
owed weth balance before liquidate          ==>   2000000000000000000000 46027397260273972000
calculated balance before liquidate         ==>   98000000000000000000000
weth balance before liquidate               ==>   98000000000000000000000
```
As we can observe, the current `WETH` balance of the `WETH market` equals `floatingAssets - floatingDebt - floatingBackupBorrowed + earningsAccumulator`.
Additionally, the `unassignedEarnings` for this `maturity pool` exactly matches the `fee` owed by this `borrower` and this will be added to the `floatingAssets` when this `borrower` repay `debt`.
In summary, everything is correct.

Now, imagine the price of `DAI` drops to `1000 WETH`, triggering `liquidation` of this `borrower` before `maturity`.
The `noTransferRepayAtMaturity` function is invoked and the last parameter is `false`.
```solidity
function liquidate(
  address borrower,
  uint256 maxAssets,
  Market seizeMarket
) external whenNotPaused returns (uint256 repaidAssets) {
   while (packedMaturities != 0 && maxAssets != 0) {
    if (packedMaturities & 1 != 0) {
      uint256 actualRepay;
      if (block.timestamp < maturity) {
        actualRepay = noTransferRepayAtMaturity(maturity, maxAssets, maxAssets, borrower, false);   // @audit, here
        maxAssets -= actualRepay;
      } else {
        ...
      }
      repaidAssets += actualRepay;
    }
    packedMaturities >>= 1;
    maturity += FixedLib.INTERVAL;
  }
}
```
In the `noTransferRepayAtMaturity` function, the `liquidator` repays the `full debt` of this `borrower` even before `maturity`.
It's important to note that the funds equivalent to the `unassignedEarnings` of this `maturity pool` are not added to the `tracking system`.
They should be included in the `floatingAssets` as usual, but in this liquidation, we skipped this.
```solidity
function noTransferRepayAtMaturity(
  uint256 maturity,
  uint256 positionAssets,
  uint256 maxAssets,
  address borrower,
  bool canDiscount  // @audit, here : this is false
) internal returns (uint256 actualRepayAssets) {
  uint256 debtCovered = Math.min(positionAssets, position.principal + position.fee);  // @audit, here
  uint256 principalCovered = FixedLib
    .Position(position.principal, position.fee)
    .scaleProportionally(debtCovered)
    .principal;

  RewardsController memRewardsController = rewardsController;
  if (address(memRewardsController) != address(0)) memRewardsController.handleBorrow(borrower);

  if (block.timestamp < maturity) {
    if (canDiscount) {
      (uint256 discountFee, uint256 backupFee) = pool.calculateDeposit(principalCovered, backupFeeRate);
      pool.unassignedEarnings -= discountFee + backupFee; // As we can see, the unassignedEarnings is not 0
      earningsAccumulator += backupFee;
      actualRepayAssets = debtCovered - discountFee;
    } else {
      actualRepayAssets = debtCovered;  // @audit, here  : we repay the full debt
    }
  } else {
    actualRepayAssets = debtCovered + debtCovered.mulWadDown((block.timestamp - maturity) * penaltyRate);

    earningsAccumulator += actualRepayAssets - debtCovered;
  }
}
```
If there are other `borrowers` or `depositors` in this `maturity pool`, the `unassignedEarnings` would be added to the `floatingAssets` when their state changes.
If this is the last user of this `maturity pool`, they won't be any chance to update this `pool`'s state.
Consequently, these funds remain untracked in the `market`, even though they are actually deposited in the `market`.
After `liquidation`, the tracking values of the `market` are as follows:
```solidity
floatingAssets after liquidate              ==>   100000000000000000000000
floatingDebt after liquidate                ==>   0
floatingBackupBorrowed after liquidate      ==>   0
earningsAccumulator after liquidate         ==>   20460273972602739720
unassignedEarnings after liquidate          ==>   46027397260273972000
owed weth balance after liquidate           ==>   0 0
calculated balance after liquidate          ==>   100020460273972602739720
weth balance after liquidate                ==>   100066487671232876711720
difference                                  ==>   46027397260273972000
```
The tracked balance and the actual balance are different and the difference is equal to the `unassignedEarnings` of that `maturity pool`.
If this user is the last `borrower` of that `maturity pool`, there is no way to convert these `unassignedEarnings` into `floatingAssets`.
Consequently, some funds become untracked in the `market`.
Or if this user is not the last user of this `maturity pool`, these untracked `unassignedEarnings` can be allocated to late `fixed depositors`.

Please add below test to the `Market.t.sol`.
```solidity
contract MarketTest is Test {
+  Market internal market;

  function testLiquidateLastBorrower() external {
    vm.prank(ALICE);
    marketWETH.approve(address(this), type(uint256).max);

    auditor.enterMarket(market);
    market.deposit(5 ether, address(this));

    marketWETH.deposit(100_000 ether, ALICE);
    
    daiPriceFeed.setPrice(5_000e18);

    uint256 maturity = 12 weeks;  
    uint256 borrowAmount = 2000 ether;
    market.borrowAtMaturity(maturity, 2 ether, 10 ether, address(this), address(this));
    marketWETH.borrowAtMaturity(maturity, borrowAmount, borrowAmount * 2, address(this), address(this));
    (, , uint256 unassignedEarnings_before, ) = marketWETH.fixedPools(maturity);
    (uint256 principal_before, uint256 fee_before) = marketWETH.fixedBorrowPositions(maturity, address(this));
    uint256 calculatedBalanceBefore = marketWETH.floatingAssets() - marketWETH.floatingDebt() - marketWETH.floatingBackupBorrowed() + marketWETH.earningsAccumulator();

    console2.log("floatingAssets before liquidate             ==>  ", marketWETH.floatingAssets());
    console2.log("floatingDebt before liquidate               ==>  ", marketWETH.floatingDebt());
    console2.log("floatingBackupBorrowed before liquidate     ==>  ", marketWETH.floatingBackupBorrowed());
    console2.log("earningsAccumulator before liquidate        ==>  ", marketWETH.earningsAccumulator());
    console2.log("unassignedEarnings before liquidate         ==>  ", unassignedEarnings_before);
    console2.log("owed weth balance before liquidate          ==>  ", principal_before, fee_before);
    console2.log("calculated balance before liquidate         ==>  ", calculatedBalanceBefore);
    console2.log("weth balance before liquidate               ==>  ", weth.balanceOf(address(marketWETH)));

    daiPriceFeed.setPrice(1_000 ether);
    weth.mint(ALICE, 1_000_000 ether);
    vm.prank(ALICE);
    weth.approve(address(marketWETH), type(uint256).max);
  
    vm.prank(ALICE);
    marketWETH.liquidate(address(this), type(uint256).max, market);
    (uint256 principal_after, uint256 fee_after) = marketWETH.fixedBorrowPositions(maturity, address(this));
    (, , uint256 unassignedEarnings_after, ) = marketWETH.fixedPools(maturity);
    uint256 calculatedBalanceAfter = marketWETH.floatingAssets() - marketWETH.floatingDebt() - marketWETH.floatingBackupBorrowed() + marketWETH.earningsAccumulator();

    console2.log("************");
    console2.log("floatingAssets after liquidate              ==>  ", marketWETH.floatingAssets());
    console2.log("floatingDebt after liquidate                ==>  ", marketWETH.floatingDebt());
    console2.log("floatingBackupBorrowed after liquidate      ==>  ", marketWETH.floatingBackupBorrowed());
    console2.log("earningsAccumulator after liquidate         ==>  ", marketWETH.earningsAccumulator());
    console2.log('unassignedEarnings after liquidate          ==>  ', unassignedEarnings_after);
    console2.log("owed weth balance after liquidate           ==>  ", principal_after, fee_after);
    console2.log("calculated balance after liquidate          ==>  ", calculatedBalanceAfter);
    console2.log("weth balance after liquidate                ==>  ", weth.balanceOf(address(marketWETH)));
    console2.log("difference                                  ==>  ", weth.balanceOf(address(marketWETH)) - calculatedBalanceAfter);
  }
}
```
## Impact
Although the description of this vulnerability may seem complex, it can occur under normal circumstances and its impact is significant.
Therefore, it's crucial to prevent this issue.

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L253
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L714
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L244-L245
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L565C27-L565C52
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L508
## Tool used

Manual Review

## Recommendation
```solidity
function noTransferRepayAtMaturity(
  uint256 maturity,
  uint256 positionAssets,
  uint256 maxAssets,
  address borrower,
  bool canDiscount  
) internal returns (uint256 actualRepayAssets) {
  uint256 debtCovered = Math.min(positionAssets, position.principal + position.fee);
  uint256 principalCovered = FixedLib
    .Position(position.principal, position.fee)
    .scaleProportionally(debtCovered)
    .principal;

  RewardsController memRewardsController = rewardsController;
  if (address(memRewardsController) != address(0)) memRewardsController.handleBorrow(borrower);

  if (block.timestamp < maturity) {
    if (canDiscount) {
      (uint256 discountFee, uint256 backupFee) = pool.calculateDeposit(principalCovered, backupFeeRate);
      pool.unassignedEarnings -= discountFee + backupFee; // As we can see, the unassignedEarnings is not 0
      earningsAccumulator += backupFee;
      actualRepayAssets = debtCovered - discountFee;
    } else {
      actualRepayAssets = debtCovered; 

+      if (principalCovered == pool.borrowed) {
+        earningsAccumulator += pool.unassignedEarnings;
+        pool.unassignedEarnings = 0;
+      }
    }
  } else {
    actualRepayAssets = debtCovered + debtCovered.mulWadDown((block.timestamp - maturity) * penaltyRate);

    earningsAccumulator += actualRepayAssets - debtCovered;
  }
}
```
Please run test again and we can observe that everything is correct.

Or we need more sophisticated solution.