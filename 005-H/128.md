Decent Lilac Ant

medium

# `unassignedEarnings` in a fixed pool may become inaccessible

## Summary
When the last borrower in a fixed pool is liquidated and no one deposits afterward, all the `unassignedEarnings` in the pool will be locked.

## Vulnerability Details
During the `liquidate` operation of a position in an immature pool, `pool.unassignedEarnings` are only reduced to a value corresponding to `block.timestamp`.
[accrueEarnings](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/utils/FixedLib.sol#L87-L92)
```solidity
    if (block.timestamp < maturity) {
      uint256 unassignedEarnings = pool.unassignedEarnings;
      pool.lastAccrual = block.timestamp;
      backupEarnings = unassignedEarnings.mulDivDown(block.timestamp - lastAccrual, maturity - lastAccrual);
      pool.unassignedEarnings = unassignedEarnings - backupEarnings;
    } else if (lastAccrual == maturity) {
```

Consider the following conditions:
- A liquidation of 100% of the assets (e.g., a 50% price change as in PoC).
- No deposits into the fixed pool (this can often be the case for less popular and profitable pools).
- It is the last position in the fixed pool (the protocol has many coins and pools, some of which will be unpopular).
- No one uses this fixed pool (`xxxAtMaturity` functions) before maturity after the liquidation.

After maturity, the `unassignedEarnings` cannot be changed:
- `depositAtMaturity`, `borrowAtMaturity` require `FixedLib.State.VALID`, but the state is `FixedLib.State.MATURED`.
- `withdrawAtMaturity`, `repayAtMaturity`, `liquidate` will revert because there are no positions in the pool.
- There are no other mechanisms to call `accrueEarnings` on a mature fixed pool.

## Impact
- Loss of funds that would otherwise go to the variable rate pool (`market.floatingAssets`).

## Code Snippet
[accrueEarnings](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/utils/FixedLib.sol#L87-L92)
```solidity
    if (block.timestamp < maturity) {
      uint256 unassignedEarnings = pool.unassignedEarnings;
      pool.lastAccrual = block.timestamp;
      backupEarnings = unassignedEarnings.mulDivDown(block.timestamp - lastAccrual, maturity - lastAccrual);
      pool.unassignedEarnings = unassignedEarnings - backupEarnings;
    } else if (lastAccrual == maturity) {
```

## Proof of Concept
`forge test  -vvv  --match-path test/AAH1.t.sol --match-test test5`

```solidity
// SPDX-License-Identifier: None
pragma solidity ^0.8.17; // solhint-disable-line one-contract-per-file

import "./Market.t.sol";
import {console} from "forge-std/console.sol";

contract AAH1 is MarketTest {

      function test5() external {
            irm.setRate(1e18);
            marketWETH.deposit(1.15 ether, address(this));
            market.deposit(50_000 ether, ALICE);
            market.setMaxFuturePools(12);
            market.setPenaltyRate(2e11);

            daiPriceFeed.setPrice(0.0002e18);
            _logThisAccountLiquidity("Snapshot before borrowing");
            uint MARKET_MATURITY = FixedLib.INTERVAL * 2;
            market.borrowAtMaturity(MARKET_MATURITY, 3000 ether, 4000 ether, address(this), address(this));
            _logThisAccountLiquidity("Snapshot after borrowing");

            daiPriceFeed.setPrice(0.0003025e18);
            _logThisAccountLiquidity("Snapshot after setting price");


            vm.warp(MARKET_MATURITY / 2);
            _logThisAccountLiquidity("Before liquidation");
            _logPoolAtMaturity(MARKET_MATURITY);

            vm.prank(BOB);
            market.liquidate(address(this), type(uint256).max, marketWETH);

            console.log("\n");
            _logThisAccountLiquidity("After liquidation");
            /* Expected output:
                borrowed: 1e0
                supplied: 0e0
                unassignedEarnings: 2.301369863013698625e20
                lastAccrual: 2419200
             */
            _logPoolAtMaturity(MARKET_MATURITY);

            address CHARLIE = _prepareCharlie();
            vm.warp(MARKET_MATURITY + 1);
            vm.startPrank(CHARLIE);

            vm.expectRevert();
            market.repayAtMaturity(MARKET_MATURITY, 1, type(uint).max, address(this));

            vm.expectRevert(ZeroRepay.selector);
            market.repayAtMaturity(MARKET_MATURITY, 0, type(uint).max, address(this));

            vm.stopPrank();
    }

    function _prepareCharlie() internal returns (address CHARLIE) {
        CHARLIE = address(0x45723895749abc);
        vm.label(CHARLIE, "CHARLIE");
        MockERC20(address(market.asset())).mint(CHARLIE, 5_000_000 ether);
        MockERC20(address(marketWETH.asset())).mint(CHARLIE, 50_000 ether);
        vm.startPrank(CHARLIE);
        market.asset().approve(address(market), type(uint).max);
        marketWETH.asset().approve(address(marketWETH), type(uint).max);
        auditor.enterMarket(marketWETH);
        auditor.enterMarket(market);
        vm.stopPrank();
    }

    function _logThisAccountSnapshot(string memory text) internal view {
        console.log(text);
        (uint256 collateral, uint256 debt) = market.accountSnapshot(address(this));
        console.log("collateral: %e", collateral);
        console.log("debt: %e", debt);
        console.log("\n");
    }

    function _logThisAccountLiquidity(string memory text) internal view {
        console.log(text);
        _logAccountLiquidity(address(this));
    }

    function _logAccountLiquidity(address acc) internal view {
        (uint256 collateral, uint256 debt) = auditor.accountLiquidity(acc, Market(address(0)), 0);
        console.log("collateral: %e", collateral);
        console.log("debt: %e", debt);
        console.log("\n");
    }

    function _logPoolAtMaturity(uint maturity) internal view {
        FixedLib.Pool memory pool;
        (pool.borrowed, pool.supplied, pool.unassignedEarnings, pool.lastAccrual) = market.fixedPools(maturity);
        _logPool(pool);

    }

    function _logPool(FixedLib.Pool memory pool) internal view {
        console.log("borrowed: %e", pool.borrowed);
        console.log("supplied: %e", pool.supplied);
        console.log("unassignedEarnings: %e", pool.unassignedEarnings);
        console.log("lastAccrual: ", pool.lastAccrual);
        console.log("\n");
    }
}

```
## Tool used
Manual review

## Recommendation
- Consider adding a function that will allow for manual accrual of earnings (`accrueEarnings`) after maturity.