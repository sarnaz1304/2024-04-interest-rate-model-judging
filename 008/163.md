Tiny Mulberry Tapir

medium

# A maturity with remaining earnings that have not yet been accrued may not be included in the `totalAsset()` function

## Summary
In the Market contract, the `totalAsset()` function accrues earnings from `maxFuturePools + 1` maturities. However, there may be a maturity not included in this list which has remaining `unassignedEarnings` that are not accrued. This could result in an unfair situation for the share price of the Market and allow a malicious holder to manipulate `totalAsset()` to acquire cheap shares. There is no way to ensure that this case never happens because there is no function in the Market contract to manually accrue the earnings of a maturity.
## Vulnerability Detail
In totalAsset() function of Market contract:
```solidity=
///https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L925-L949
function totalAssets() public view override returns (uint256) {
    unchecked {
      uint256 backupEarnings = 0;

      uint256 latestMaturity = block.timestamp - (block.timestamp % FixedLib.INTERVAL);
      uint256 maxMaturity = latestMaturity + maxFuturePools * FixedLib.INTERVAL;

      for (uint256 maturity = latestMaturity; maturity <= maxMaturity; maturity += FixedLib.INTERVAL) {
        FixedLib.Pool storage pool = fixedPools[maturity];
        uint256 lastAccrual = pool.lastAccrual;

        if (maturity > lastAccrual) {
          backupEarnings += block.timestamp < maturity
            ? pool.unassignedEarnings.mulDivDown(block.timestamp - lastAccrual, maturity - lastAccrual)
            : pool.unassignedEarnings;
        }
      }

      return
        floatingAssets +
        backupEarnings +
        accumulatedEarnings() +
        (totalFloatingBorrowAssets() - floatingDebt).mulWadDown(1e18 - treasuryFeeRate);
    }
  }
```
This function considers a total of `maxFuturePools + 1` maturities from `latestMaturity` to `maxMaturity`. However, there may be a maturity not included in this list that has remaining `unassignedEarnings` which have not been accrued. This can occur when there have been no operations for a time during the active period of this maturity.

For example, a maturity runs from May 1st to May 29th (4 weeks), but there have been no operations from May 20th to June 27th. On June 28th, the earnings from the last 9 days (May 20th to May 29th) of this maturity have not yet been accrued. However, `totalAsset()` will not consider this maturity for accruing its earnings into the total assets, resulting in `totalAsset()` being lower than the actual amount.

This vulnerability creates an unfair situation for users of the Market who deposit or withdraw from the floating pool to acquire or redeem shares. A malicious borrower can exploit it to manipulate the share price of the floating pool by using `repayAtMaturity()` to trigger `accrueEarnings` again.

Here is the test function for a PoC:
```solidity=
function testMaturityUnchangedManipulateTotalAsset() external {
    irm = MockInterestRateModel(address(new MockBorrowRate(0.1e18)));
    market.setInterestRateModel(InterestRateModel(address(irm)));
    market.setMaxFuturePools(5);
    market.deposit(100 ether, address(this));

    // borrow and create unassigned earnings for a maturity
    market.borrowAtMaturity(FixedLib.INTERVAL, 10 ether, 11 ether, address(this), address(this));
    
    // nearly one month after maturity ends
    // retrieve totalAsset() and share price at this moment
    vm.warp(FixedLib.INTERVAL * 2 - 1);
    uint256 fullyAsset = market.totalAssets();
    uint256 oldPrice = market.previewRedeem(1 ether);
    console.log(fullyAsset);

    // more than one month after maturity ends
    // totalAsset() has not accrued the earnings from maturity, so totalAssets() decreases
    // therefore, the share price will decrease and become cheaper
    vm.warp(FixedLib.INTERVAL * 2 + 1);
    (, , uint256 accruedEarnings, ) = market.fixedPools(FixedLib.INTERVAL);
    console.log(market.totalAssets());
    assertEq(market.totalAssets(), fullyAsset - accruedEarnings);
    uint256 newPrice = market.previewRedeem(1 ether);
    assertLt(newPrice, oldPrice);
    
    //After that, user can repay to manipulate the share price increase
    market.repayAtMaturity(FixedLib.INTERVAL, 1.1 ether, 2 ether, address(this));
    uint256 manipulatedPrice = market.previewRedeem(1 ether);
    assertGt(manipulatedPrice, newPrice);
}
```
Please put this function into `Market.t.sol` test file and run the command:
```bash=
forge test -vv --match-test testMaturityUnchangedManipulateTotalAsset
```
## Impact
This case result in an unfair situation for the share price of the Market and allow a malicious holder to manipulate `totalAsset()` to acquire cheap shares.
## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L925-L949
## Tool used

Manual Review

## Recommendation
Market contract should have a function to manually accrue earnings from maturities to prevent the scenario where a maturity still has unassigned earnings for an extended period after it ends.