Dapper Bronze Cricket

high

# In certain functions, the `floatingDebt` variable is used for calculations without being updated beforehand

## Summary

In functions like `borrowAtMaturity()` and `withdrawAtMaturity()`, the protocol fails to update the `floatingDebt` variable before its usage, resulting in inaccurate subsequent calculations.

## Vulnerability Detail

The  ` Market.updateFloatingDebt()` function is used to update the variables related to floating pool borrows, such as `floatingAssets` and `floatingDebt`.
```solidity
  function updateFloatingDebt() internal returns (uint256 treasuryFee) {
    uint256 memFloatingDebt = floatingDebt;
    uint256 memFloatingAssets = floatingAssets;
    uint256 utilization = floatingUtilization(memFloatingAssets, memFloatingDebt);
    uint256 newDebt = memFloatingDebt.mulWadDown(
      interestRateModel
        .floatingRate(utilization, globalUtilization(memFloatingAssets, memFloatingDebt, floatingBackupBorrowed))
        .mulDivDown(block.timestamp - lastFloatingDebtUpdate, 365 days)
    );

    memFloatingDebt += newDebt;
    treasuryFee = newDebt.mulWadDown(treasuryFeeRate);
    floatingAssets = memFloatingAssets + newDebt - treasuryFee;
    floatingDebt = memFloatingDebt;
    lastFloatingDebtUpdate = uint32(block.timestamp);
    emit FloatingDebtUpdate(block.timestamp, utilization);
  }

```
 In the `Market.borrowAtMaturity()` function, when `backupDebtAddition != 0`, the protocol calls `updateFloatingDebt()` to update these values. 
```solidity
     if (backupDebtAddition != 0) {
        uint256 newFloatingBackupBorrowed = floatingBackupBorrowed + backupDebtAddition;
        depositToTreasury(updateFloatingDebt());
        if (newFloatingBackupBorrowed + floatingDebt > floatingAssets.mulWadDown(1e18 - reserveFactor)) {
          revert InsufficientProtocolLiquidity();
        }
        floatingBackupBorrowed = newFloatingBackupBorrowed;
      }
    }

```

However, when `backupDebtAddition = 0`, the protocol does not invoke `updateFloatingDebt()`. Consequently, in subsequent calculations, if `floatingDebt` is used without being updated beforehand, it leads to the utilization of outdated values, thereby affecting the accuracy of subsequent computations. 

```solidity
uint256 fee;
    {
      uint256 memFloatingAssetsAverage = previewFloatingAssetsAverage();
      uint256 memFloatingDebt = floatingDebt;
      uint256 fixedRate = interestRateModel.fixedRate(
        maturity,
        maxFuturePools,
        fixedUtilization(pool.supplied, pool.borrowed, memFloatingAssetsAverage),
        floatingUtilization(memFloatingAssetsAverage, memFloatingDebt),
        globalUtilization(memFloatingAssetsAverage, memFloatingDebt, floatingBackupBorrowed)
      );
      fee = assets.mulWadDown(fixedRate.mulDivDown(maturity - block.timestamp, 365 days));
    }
    assetsOwed = assets + fee;

```

Similarly, this issue exists in the `withdrawAtMaturity()` function as well.
```solidity

 if (block.timestamp < maturity) {
      uint256 memFloatingAssetsAverage = previewFloatingAssetsAverage();
      uint256 memFloatingDebt = floatingDebt;
      uint256 memFloatingBackupBorrowed = floatingBackupBorrowed;

      uint256 fixedRate = interestRateModel.fixedRate(
        maturity,
        maxFuturePools,
        fixedUtilization(pool.supplied, pool.borrowed, memFloatingAssetsAverage),
        floatingUtilization(memFloatingAssetsAverage, memFloatingDebt),
        globalUtilization(memFloatingAssetsAverage, memFloatingDebt, memFloatingBackupBorrowed)
      );
      assetsDiscounted = positionAssets.divWadDown(1e18 + fixedRate.mulDivDown(maturity - block.timestamp, 365 days));
    } else {
      assetsDiscounted = positionAssets;
    }
```

## Impact
The protocol's computation results may be inaccurate.

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L281-L354

## Tool used

Manual Review

## Recommendation
Before using `floatingAssets` and `floatingDebt`, they need to be updated first.