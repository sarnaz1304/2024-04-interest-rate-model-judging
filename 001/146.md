Trendy Cedar Wasp

medium

# Updating maxFuturePools causes a significant shortage of rewards in the rewardsController.

## Summary

## Vulnerability Detail
As noted in the comments below, we may need to reduce the value of `maxFuturePools`.
```solidity
/// @notice Sets the protocol's max future pools for fixed borrowing and lending.
/// @dev If value is decreased, VALID maturities will become NOT_READY.
/// @param futurePools number of pools to be active at the same time.
function setMaxFuturePools(uint8 futurePools) public onlyRole(DEFAULT_ADMIN_ROLE) {
  maxFuturePools = futurePools;
  emit MaxFuturePoolsSet(futurePools);
}
```
Let's take an example.
Initially, the `maxFuturePools` is set to `6`.
`BOB` `borrows` `1 ether` from a `fixed rate pool` at `4 weeks maturity`.
`ALICE` `borrows` `1 ether` from the same `fixed rate pool`.
`ALICE` then `borrows` an additional `20 ether` from a `fixed rate pool` at `24 weeks maturity` and this is possible.

The `admin` then changes `maxFuturePools` to `3`.
At the current time, which is at `11 weeks`, `BOB` decides to `claim` his `rewards` so we need to calculate the `total borrow shares` for distributing the `rewards`.
However, due to the `maxFuturePools` is `3`, only `fixed rate pools` with `maturities` up to `20 weeks` can be considered. (`12 weeks`, `16 weeks`, `20 weeks`.)
```solidity
function previewAllocation(
  RewardData storage rewardData,
  Market market,
  uint256 deltaTime
) internal view returns (uint256 borrowIndex, uint256 depositIndex, uint256 newUndistributed) {
  TotalMarketBalance memory m;
  m.floatingDebt = market.floatingDebt();
  m.floatingAssets = market.floatingAssets();
  TimeVars memory t;
  t.start = rewardData.start;
  t.end = rewardData.end;
  {
    uint256 firstMaturity = t.start - (t.start % FixedLib.INTERVAL) + FixedLib.INTERVAL;
    uint256 maxMaturity = block.timestamp -
      (block.timestamp % FixedLib.INTERVAL) +
      (FixedLib.INTERVAL * market.maxFuturePools());  // @audit, 11 weeks - (11 weeks % 4 weeks) + (4 weeks * 3) = 20 weeks
    uint256 fixedDebt;
    for (uint256 maturity = firstMaturity; maturity <= maxMaturity; ) {
      (uint256 borrowed, ) = market.fixedPoolBalance(maturity);
      fixedDebt += borrowed;
      unchecked {
        maturity += FixedLib.INTERVAL;
      }
    }
    m.debt = m.floatingDebt + fixedDebt;
    m.fixedBorrowShares = market.previewRepay(fixedDebt);
  }
}
```
Consequently, the `fixed borrow shares` for the `pool` at `24 weeks maturity` are ignored since it's not ready yet.
The `rewardsIndex` is calculated based on the `fixed borrow shares` of `2 ether` (`1 ether` for `BOB` and `1 ether` for `ALICE` from the `fixed rate pool` at `4 weeks maturity`)

From `12 weeks`, the `fixed rate pool` at `24 weeks maturity` becomes ready, and `ALICE`'s `20 ether` `fixed borrow shares` are now eligible for `claiming rewards`.
Although these `fixed borrow shares` were initially skipped in the `rewardsIndex` calculation, they are now considered, resulting in a significantly larger available `rewards` for `ALICE` than expected.

Below log shows that this available `rewards` for `ALICE` become much larger than the actual `rewards balance`.
```solidity
Rewards balance for BOB after 11 weeks     ==>   916666636000000000000
Available rewards for ALICE after 11 weeks ==>   916666636000000000000
Available rewards for ALICE after 15 weeks ==>   15101659177000000000000
Available rewards token balance            ==>   3083333364000000000000
```
Please add below test to `RewardsController.t.sol`:
```solidity
function testSetMaxFuturePools() external {
  marketUSDC.setMaxFuturePools(6);

  vm.prank(ALICE);
  marketUSDC.approve(address(this), 100 ether);

  vm.prank(BOB);
  marketUSDC.approve(address(this), 100 ether);

  vm.prank(ALICE);
  auditor.enterMarket(marketUSDC);

  vm.prank(BOB);
  auditor.enterMarket(marketUSDC);

  marketUSDC.deposit(50 ether, ALICE);
  marketUSDC.deposit(50 ether, BOB);

  marketUSDC.borrowAtMaturity(4 weeks, 1 ether, 20 ether, ALICE, ALICE);
  marketUSDC.borrowAtMaturity(24 weeks, 20 ether, 40 ether, ALICE, ALICE);
  marketUSDC.borrowAtMaturity(4 weeks, 1 ether, 20 ether, BOB, BOB);

  marketUSDC.setMaxFuturePools(3);
  vm.warp(11 weeks);
  
  vm.prank(BOB);
  rewardsController.claimAll(BOB);
  console2.log("Rewards balance for BOB after 11 weeks     ==>  ", opRewardAsset.balanceOf(BOB));
  console2.log("Available rewards for ALICE after 11 weeks ==>  ", rewardsController.allClaimable(ALICE, opRewardAsset));

  vm.warp(15 weeks);
  console2.log("Available rewards for ALICE after 15 weeks ==>  ", rewardsController.allClaimable(ALICE, opRewardAsset));
  console2.log("Available rewards token balance            ==>  ", opRewardAsset.balanceOf(address(rewardsController)));
}
```
## Impact

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L1093-L1099
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/RewardsController.sol#L482-L492
## Tool used

Manual Review

## Recommendation
