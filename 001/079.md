Crazy Pickle Bee

medium

# Missing timely update floating debt interest in depositAtMaturity/withdrawAtMaturity

## Summary
Function depositAtMaturity()/withdrawAtMaturity() might change the floating rate, contract should update accured floating debt.

## Vulnerability Detail
Function updateFloatingDebt() aims to calculate the floating interest rate. The floating interest rate is one function related with `floatingDebt`, `floatingAssets`, `floatingBackupBorrowed`. This means that we should calculate the previous floating borrow debt interest before any mentioned variables changes.

```c
  function updateFloatingDebt() internal returns (uint256 treasuryFee) {
    uint256 memFloatingDebt = floatingDebt;
    uint256 memFloatingAssets = floatingAssets;
    uint256 utilization = floatingUtilization(memFloatingAssets, memFloatingDebt);
    uint256 newDebt = memFloatingDebt.mulWadDown(
      interestRateModel
        .floatingRate(utilization, globalUtilization(memFloatingAssets, memFloatingDebt, floatingBackupBorrowed))
        .mulDivDown(block.timestamp - lastFloatingDebtUpdate, 365 days)
    );

    memFloatingDebt += newDebt; // update floating total debt
    treasuryFee = newDebt.mulWadDown(treasuryFeeRate);
    //new generated debt will become part of floating asset
    floatingAssets = memFloatingAssets + newDebt - treasuryFee;
    floatingDebt = memFloatingDebt;
    lastFloatingDebtUpdate = uint32(block.timestamp);
    emit FloatingDebtUpdate(block.timestamp, utilization);
  }
```
In depositAtMaturity()/withdrawAtMaturity(), variable `floatingBackupBorrowed` might change, which means this action might change the current floating borrow rate. And contract does not update previous floating interest timely.

```c
  function depositAtMaturity(
    uint256 maturity,
    uint256 assets,
    uint256 minAssetsRequired,
    address receiver
  ) external whenNotPaused whenNotFrozen returns (uint256 positionAssets) {
    if (assets == 0) revert ZeroDeposit();
    // reverts on failure
    FixedLib.checkPoolState(maturity, maxFuturePools, FixedLib.State.VALID, FixedLib.State.NONE);

    FixedLib.Pool storage pool = fixedPools[maturity];
    // floating pool earning from fixed pool
    uint256 backupEarnings = pool.accrueEarnings(maturity);
    floatingAssets += backupEarnings;
    (uint256 fee, uint256 backupFee) = pool.calculateDeposit(assets, backupFeeRate);
    positionAssets = assets + fee;
    if (positionAssets < minAssetsRequired) revert Disagreement();
    //deposit at fix pool can decrease floating pool's contribution
    floatingBackupBorrowed -= pool.deposit(assets);
    pool.unassignedEarnings -= fee + backupFee;
    earningsAccumulator += backupFee; // part of deposit interest 

    // update account's position
    FixedLib.Position storage position = fixedDepositPositions[maturity][receiver];

    // if account doesn't have a current position, add it to the list
    if (position.principal == 0) {
      Account storage account = accounts[receiver];
      account.fixedDeposits = account.fixedDeposits.setMaturity(maturity);
    }

    position.principal += assets;
    position.fee += fee;
    asset.safeTransferFrom(msg.sender, address(this), assets);
  }
```

## Impact
The floating borrow interest is inaccurate.

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L888-L904
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L232-L272
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L363-L437

## Tool used

Manual Review

## Recommendation
Timely update floating borrow interest whenever `floatingBackupBorrowed` changes.
