Faint Mahogany Jellyfish

medium

# Bad Debt Socialization could be blocked by depositing dust amounts on behalf of unhealthy account

## Summary
Attackers could block the socialization of bad debt by depositing dust amounts on behalf of the user with the under-water position.
## Vulnerability Detail
The protocol relies on `Auditor.handleBadDebt()` to carry on the socialization of bad debt. The method will first check that the user has zero amount of adjusted collateral, if it is the case then handleBadDebt will return. 
```solidity
  function handleBadDebt(address account) external {
    uint256 memMarketMap = accountMarkets[account];
    uint256 marketMap = memMarketMap;
    for (uint256 i = 0; marketMap != 0; marketMap >>= 1) {
      if (marketMap & 1 != 0) {
        Market market = marketList[i];
        MarketData storage m = markets[market];
        uint256 assets = market.maxWithdraw(account);
        // @audit-issue possible avoidance of bad debt socialization by donation
        if (assets.mulDivDown(assetPrice(m.priceFeed), 10 ** m.decimals).mulWadDown(m.adjustFactor) > 0) return;
      }
```

The protocol however also allows any user to deposit collateral on behalf of other users (relies on solmate ERC4626) implementation.  
(`protocol/node_modules/solmate/src/mixins/ERC4626.sol`)
```solidity
function deposit(uint256 assets, address receiver) public virtual returns (uint256 shares) {
        // Check for rounding error since we round down in previewDeposit.
        require((shares = previewDeposit(assets)) != 0, "ZERO_SHARES");

        // Need to transfer before minting or ERC777s could reenter.
        asset.safeTransferFrom(msg.sender, address(this), assets);

        _mint(receiver, shares);

        emit Deposit(msg.sender, receiver, assets, shares);

        afterDeposit(assets, shares);
}
```
An Attacker could deposit a small amount (2~5 wei) on behalf of the user in a listed market in `Auditor.sol`, this will lead to the method `handleBadDebt()` to be skipped and the bad debt won't be socialized. This will lead to the creation of a "dust" position in the market, that liquidators wouldn't want to liquidate, as the cost of liquidating such a position (gas fees) overly exceeds the value of the collateral. 

## Impact
Breaking the socialization of bad debt by depositing dust amounts on behalf of the user could expose the protocol to risk of insolvancy. For e.g a large Position is going under (value collateral > value debt), an attacker would deposit 5 wei on behalf of the large position, blocking the socialization of bad debt for the user. This will lead to the position being still open and the bad debt unsocialized. An attacker could benefit from this in order to get more yield from the `earningsAccumulator`
## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Auditor.sol#L298-L320


```solidity
  function handleBadDebt(address account) external {
    uint256 memMarketMap = accountMarkets[account];
    uint256 marketMap = memMarketMap;
    for (uint256 i = 0; marketMap != 0; marketMap >>= 1) {
      if (marketMap & 1 != 0) {
        Market market = marketList[i];
        MarketData storage m = markets[market];
        uint256 assets = market.maxWithdraw(account);
        if (assets.mulDivDown(assetPrice(m.priceFeed), 10 ** m.decimals).mulWadDown(m.adjustFactor) > 0) return;
      }
      unchecked {
        ++i;
      }
    }

    marketMap = memMarketMap;
    for (uint256 i = 0; marketMap != 0; marketMap >>= 1) {
      if (marketMap & 1 != 0) marketList[i].clearBadDebt(account);
      unchecked {
        ++i;
      }
    }
  }
```

## Tool used

Manual Review

## Recommendation
To avoid griefing, we recommend either adding access control to `Market.deposit()` or enforcing a minimum amount of collateral to deposit. This way evern if attackers will try to grief the protocol, the costs of the attack will be substantially higher.