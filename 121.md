Faithful Felt Swift

medium

# DOMAIN_SEPARATOR Incorrect signature content

## Summary
`market.sol` use `name==""` to generate `INITIAL_DOMAIN_SEPARATOR` in `constructor()` and cached.
Changed `name=exactly` in `initialize()` 
but `INITIAL_DOMAIN_SEPARATOR` does not change.
`permit()` is using the wrong `INITIAL_DOMAIN_SEPARATOR` (name="") for sign
it will does not execute correctly.


## Vulnerability Detail
`market.sol` use `name==""` to generate `INITIAL_DOMAIN_SEPARATOR` and cache it

```solidity
contract Market is Initializable, AccessControlUpgradeable, PausableUpgradeable, ERC4626 {
...
@>constructor(ERC20 asset_, Auditor auditor_) ERC4626(asset_, "", "") {
    auditor = auditor_;

    _disableInitializers();
  }



  function setAssetSymbol(string calldata assetSymbol) public onlyRole(DEFAULT_ADMIN_ROLE) {
@>  name = string.concat("exactly ", assetSymbol);
    symbol = string.concat("exa", assetSymbol);
  }
```
```solidity
abstract contract ERC20 {
...
    constructor(
        string memory _name,
        string memory _symbol,
        uint8 _decimals
    ) {
        name = _name;
        symbol = _symbol;
        decimals = _decimals;

        INITIAL_CHAIN_ID = block.chainid;
@>      INITIAL_DOMAIN_SEPARATOR = computeDomainSeparator();
    }

    function computeDomainSeparator() internal view virtual returns (bytes32) {
        return
            keccak256(
                abi.encode(
                    keccak256("EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)"),
@>                  keccak256(bytes(name)),
                    keccak256("1"),
                    block.chainid,
                    address(this)
                )
            );
    }

    function permit(
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) public virtual {
        require(deadline >= block.timestamp, "PERMIT_DEADLINE_EXPIRED");

        // Unchecked because the only math done is incrementing
        // the owner's nonce which cannot realistically overflow.
        unchecked {
            address recoveredAddress = ecrecover(
                keccak256(
                    abi.encodePacked(
                        "\x19\x01",
@>>                     DOMAIN_SEPARATOR(),
                        keccak256(
                            abi.encode(
                                keccak256(
                                    "Permit(address owner,address spender,uint256 value,uint256 nonce,uint256 deadline)"
                                ),
                                owner,
                                spender,
                                value,
                                nonces[owner]++,
                                deadline
                            )
                        )
                    )
                ),
                v,
                r,
                s
            );

            require(recoveredAddress != address(0) && recoveredAddress == owner, "INVALID_SIGNER");

            allowance[recoveredAddress][spender] = value;
        }

        emit Approval(owner, spender, value);
    }

    function DOMAIN_SEPARATOR() public view virtual returns (bytes32) {
@>      return block.chainid == INITIAL_CHAIN_ID ? INITIAL_DOMAIN_SEPARATOR : computeDomainSeparator();
    }
```

This causes `permit()` to always use the `name=""` signature, and not `name=exactly`

## Impact

The user assembles the content of the signature through the EIP-712 standard, but cannot execute permit() properly.
Such as wallets

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L97
## Tool used

Manual Review

## Recommendation

Override `DOMAIN_SEPARATOR()` real-time calculations

```diff
contract Market is Initializable, AccessControlUpgradeable, PausableUpgradeable, ERC4626 {
...

+   function DOMAIN_SEPARATOR() public view override returns (bytes32) {
+       return computeDomainSeparator();
+   }

```