Tiny Mulberry Tapir

medium

# As time passes, the decrease in the value of `previewRepay(fixedDebt)` due to the floating interest results in borrowers being unable to claim their full rewards

## Summary
The passage of time will cause the value of `previewRepay(fixedDebt)` to decrease, consequently reducing the rewards that users can claim over time.

## Vulnerability Detail
The `RewardsController` contract features an allocation mechanism that determines the distribution of tokens among different user classes within the protocol, namely borrowers and depositors, for each distinct period. Depositors receive rewards based directly on their deposit share balance (`market.balanceOf(user)`). In contrast, calculating rewards for borrowers is more complex due to the presence of both fixed and floating debt options.

For borrowers, the contract calculates a user's debt share by summing up:
* `floatingBorrowShares`: the user's shares in floating debt.
* `accountFixedBorrowShares`: this represents the floating borrow shares equivalent to the userâ€™s total fixed debt across all maturities. Specifically, it is determined by using `previewRepay(fixedDebt)`, where `fixedDebt` is the aggregate of the user's fixed debts calculated as follows:
    ```solidity=
    function accountFixedBorrowShares(
        Market market,
        address account,
        uint32 start
    ) internal view returns (uint256 fixedDebt) {
        uint256 firstMaturity = start - (start % FixedLib.INTERVAL) + FixedLib.INTERVAL;
        uint256 maxMaturity = block.timestamp -
          (block.timestamp % FixedLib.INTERVAL) +
          (FixedLib.INTERVAL * market.maxFuturePools());

        for (uint256 maturity = firstMaturity; maturity <= maxMaturity; ) {
          (uint256 principal, ) = market.fixedBorrowPositions(maturity, account);

          fixedDebt += principal;
          unchecked {
            maturity += FixedLib.INTERVAL;
          }
        }

        fixedDebt = market.previewRepay(fixedDebt); 
      }
    ```

It's important to note that while the market's total floating debt increases over time due to accruing floating interest rates (as detailed in `Market.totalFloatingBorrowAssets()`), the fixed debt remains constant regardless of the time elapsed. 

```solidity=
function previewRepay(uint256 assets) public view returns (uint256) {
    uint256 supply = totalFloatingBorrowShares;

    return supply == 0 ? assets : assets.mulDivDown(supply, totalFloatingBorrowAssets());
}

function totalFloatingBorrowAssets() public view returns (uint256) {
    uint256 memFloatingDebt = floatingDebt;
    uint256 memFloatingAssets = floatingAssets;

    uint256 newDebt = memFloatingDebt.mulWadDown(
      interestRateModel
        .floatingRate(
          floatingUtilization(memFloatingAssets, memFloatingDebt),
          globalUtilization(memFloatingAssets, memFloatingDebt, floatingBackupBorrowed)
        )
        .mulDivDown(block.timestamp - lastFloatingDebtUpdate, 365 days)
    );
    return memFloatingDebt + newDebt;
}
```

In practical terms, as time progresses, the `previewRepay()` function will yield progressively lower values for the same amount of fixed debt due to increases in the total floating debt. This results in a reduction of the debt share attributed to a user, thereby decreasing the rewards they accrue over time.

## Impact

Consider the following scenario:

0. A reward distribution is initiated with OPs as the reward.
1. Alice borrows 100 USDC from a market, receiving 100 floating borrow shares:
    * `market.floatingDebt = 100`
    * `market.totalFloatingBorrowShares = 100`
2. Alice borrows 100 USDC at maturity `0`.
3. When the reward distribution concludes, we assume that 200 OPs were allocated for the borrowers class and no floating interest accrued by the end of the distribution due to only a short time passing.
4. Bob deposits 1 USDC into the market to trigger the `RewardControllers.handleDeposit() -> update() -> previewAllocation()` function:
    * Within the `previewAllocation()` function:
        * Lines 486 - 493: `fixedDebt = 100`
        * Line 495: `m.fixedBorrowShares = previewRepay(100) = 100 * 100 / 100 = 100`
        * Lines 582 - 584: `borrowIndex = 200 / (100 + 100) = 1`

5. Some time passes, and floating debt interest accrues. The function `updateFloatingDebt()` is invoked, which increases the `market.floatingDebt` value to `200`.

6. Alice calls `claim()` to claim the reward:
    * Within the function `accountBalanceOperations()`, Alice's borrow balance is calculated as:
    ```solidity=
          borrowBalance = floatingBorrowShares + accountFixedBorrowShares(market, Alice, distributionStart)
                        = 100 + 100 * 100 / 200
                        = 100 + 50
                        = 150
     ```
    * The accrued rewards for Alice are:
        * `borrowBalance * borrowIndex = 150 * 1 = 150`

The scenario above demonstrates that Alice is entitled to 200 OPs as a reward in step 4, but when she claims the rewards in step 6, 50 OPs are lost.

--> Some rewards will be lost in the contract.

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/RewardsController.sol#L481-L495
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/RewardsController.sol#L614
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L908-L920

## Tool used
Manual Review

## Recommendation
Consider adding a new mapping to the `RewardsController` contract for each account to store the latest borrow share balance. To determine the reward allocated to a user, use this stored value from the mapping to multiply with the `borrowIndex` instead of recalculating the borrow share balance each time.