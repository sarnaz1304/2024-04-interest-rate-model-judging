Real Mocha Hornet

high

# `EscrowedEXA` contract will mint users excess `esEXA` tokens due to double refunding on cancel stream

## Summary
`EscrowedEXA` contains functionality for vesting `EXA` tokens, and allows the recipient to cancel the `vesting stream`. When cancelling a `vesting steam`, `EscrowedEXA::cancel` erroneously doubly refunds the `esEXA` tokens, allowing callers to receive 2x the amount of tokens they are owed.

## Vulnerability Detail
Let's follow the flow when cancelling a vesting stream.

`EscrowedEXA::cancel`
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/periphery/EscrowedEXA.sol#L133
```javascript
  function cancel(uint256[] memory streamIds) external returns (uint256 streamsReserves) {
    uint128 refundableAmount;
    for (uint256 i = 0; i < streamIds.length; ++i) {
      uint256 streamId = streamIds[i];
      checkStream(streamId);
      assert(msg.sender == sablier.getRecipient(streamId));
      streamsReserves += reserves[streamId];
      delete reserves[streamId];
@>    refundableAmount += sablier.refundableAmountOf(streamId);
      withdrawMax(streamId);
@>    sablier.cancel(streamId);
    }
    emit Cancel(msg.sender, streamIds);
@>  _mint(msg.sender, refundableAmount);
    exa.safeTransfer(msg.sender, streamsReserves);
  }
```

Here, the `refundableAmount` is calculated via the external call to `Sablier::refundableAmountOf`. Let's take a look at this function:

`SablierV2LockupLinear::refundableAmountOf` 
https://etherscan.io/address/0xafb979d9afad1ad27c5eff4e27226e3ab9e5dcc9#code
```javascript
    function refundableAmountOf(uint256 streamId)
        external
        view
        override
        notNull(streamId)
        returns (uint128 refundableAmount)
    {
        if (_streams[streamId].isCancelable && !_streams[streamId].isDepleted) {
            refundableAmount = _streams[streamId].amounts.deposited - _calculateStreamedAmount(streamId);
        }
    }
```

We can then see that `refundableAmount` returned is then used to mint `msg.sender` `esEXA` tokens.

When `sablier.cancel(streamId)` is executed, the external `SablierV2LockupLinear` initiates a callback on cancelling streams, which the `EscrowedEXA` contract implements:

`EscrowedEXA::onStreamCanceled`
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/periphery/EscrowedEXA.sol#L188
```javascript
  /// @notice Hook called when a recipient cancels a stream.
  /// @notice Mints esEXA to the recipient with the remaining EXA received from the canceled stream.
  /// @param streamId streamId of the cancelled stream.
  /// @param recipient recipient of the cancelled stream.
  /// @param senderAmount amount of EXA received back from the stream cancelling.
  function onStreamCanceled(uint256 streamId, address recipient, uint128 senderAmount, uint128) external {
    assert(msg.sender == address(sablier));
    checkStream(streamId);
    // @audit we are refunding here again
@>  _mint(recipient, senderAmount);
    returnReserve(streamId, recipient);
  }
```

We can see here that the refunded amount is sent back as `senderAmount` and the same amount of `esEXA` tokens are minted to the caller (`recipient`) again. We can confirm this by viewing the implementation of the `Sablier` function that is called.

`SablierV2LockupLinear::_cancel` 
https://etherscan.io/address/0xafb979d9afad1ad27c5eff4e27226e3ab9e5dcc9#code
```javascript
    function _cancel(uint256 streamId) internal override {
        // @audit same calculation as `refundableAmountOf`
        uint128 streamedAmount = _calculateStreamedAmount(streamId);

        .
        .
        .

@>      uint128 senderAmount = amounts.deposited - streamedAmount;

        .
        .
        .

@>      asset.safeTransfer({ to: sender, value: senderAmount });

   
      if (recipient.code.length > 0) {
            try ISablierV2LockupRecipient(recipient).onStreamCanceled({
                streamId: streamId,
                sender: sender,
@>              senderAmount: senderAmount,
                recipientAmount: recipientAmount
            }) { } catch { }
        }
```

The calculation for `senderAmount` is the exact same calculation as `SablierV2LockupLinear::refundableAmountOf`. This is because `SablierV2LockupLinear::refundableAmountOf` is intended to just get a preview of the refundable amount, but the `EscrowedEXA` contract uses it for refunding, however it already handles that via `onStreamCanceled` callback. Therefore cancelling `vesting streams` erroneously doubly refunds the caller, minting them 2x the amount of `esEXA` tokens than actually owed to them.

## Impact
Excess funds refunded to the caller.

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/periphery/EscrowedEXA.sol#L133

## Tool used
Manual Review

## Recommendation
Since the callback already refunds these tokens to the caller, make the following changes:

`EscrowedEXA::cancel`
```diff
  function cancel(uint256[] memory streamIds) external returns (uint256 streamsReserves) {
-   uint128 refundableAmount;
    for (uint256 i = 0; i < streamIds.length; ++i) {
      uint256 streamId = streamIds[i];
      checkStream(streamId);
      assert(msg.sender == sablier.getRecipient(streamId));
      streamsReserves += reserves[streamId];
      delete reserves[streamId];
-     refundableAmount += sablier.refundableAmountOf(streamId);
      withdrawMax(streamId);
      sablier.cancel(streamId);
    }
    emit Cancel(msg.sender, streamIds);
-   _mint(msg.sender, refundableAmount);
    exa.safeTransfer(msg.sender, streamsReserves);
  }
```