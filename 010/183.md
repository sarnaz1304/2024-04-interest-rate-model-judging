Stable Fiery Pangolin

medium

# Recipient of vesting stream can withdraw all of deposited reserve EXA tokens much earlier than corresponding vesting stream is fully vested and withdrawn

## Summary
Although the `EscrowedEXA.withdrawMax` function only allows the recipient to withdraw the deposited reserve EXA tokens when the corresponding vesting stream is fully vested and withdrawn, the recipient can still call the `EscrowedEXA.cancel` function to prematurely receive all of the corresponding reserve much earlier than such vesting stream is fully vested and withdrawn. This allows the recipient to withdraw all of the reserve much earlier than she or he is entitled to, bypasses the intended vesting period, and breaks the agreement between the user, who starts the vesting stream, and the recipient, which requires the vesting stream to be fully vested and withdrawn before the reserve can be withdrawn.

## Vulnerability Detail
When a user calls the `vest` function shown in the Code Snippet section to create a vesting stream for a recipient, such user deposits and locks the reserve EXA tokens. Since the `withdrawMax` function shown in the Code Snippet section executes `if (sablier.isDepleted(streamId)) returnReserve(streamId, msg.sender)`, the recipient can only receive such reserve when the vesting stream is fully vested and withdrawn.

After the vesting stream is started, the recipient can call the `cancel` function shown in the Code Snippet section, which executes `_mint(msg.sender, refundableAmount)` and `exa.safeTransfer(msg.sender, streamsReserves)`. Executing `_mint(msg.sender, refundableAmount)` would mint the unvested EscrowedEXA tokens to the recipient in which the recipient still needs to vest these tokens for the corresponding duration in order to receive the associated EXA tokens. However, executing `exa.safeTransfer(msg.sender, streamsReserves)` would immediately transfer all of the deposited reserve EXA tokens to the recipient much earlier than the corresponding vesting stream is fully vested and withdrawn, which is inconsistent with the `withdrawMax` function.

For POC, please add the following test in `protocol\test\EscrowedEXA.t.sol`. This test will pass to demonstrate the described scenario.

```solidity
  function test_withdrawAllOfReserveMuchEarlierThanStreamIsFullyVestedAndWithdrawn() external {
    address CHARLIE = address(123);

    uint256 amount = 1_000 ether;
    uint256 ratio = esEXA.reserveRatio();
    uint256 reserve = amount.mulWadDown(ratio);
    esEXA.mint(amount, address(this));

    // start vesting stream for Charlie
    uint256 streamId = esEXA.vest(uint128(amount), CHARLIE, ratio, esEXA.vestingPeriod());

    // some reserve has been deposited after vest function call
    uint256 reserveBefore = esEXA.reserves(streamId);
    assertGt(reserveBefore, 0);

    // Charlie has no EXA at this moment
    uint256 EXABeforeCharlie = exa.balanceOf(CHARLIE);
    assertEq(EXABeforeCharlie, 0);

    vm.startPrank(CHARLIE);

    // Charlie calls cancel immediately after vest function call
    uint256[] memory streamIds = new uint256[](1);
    streamIds[0] = streamId;
    esEXA.cancel(streamIds);

    vm.stopPrank();

    // all of deposited reserve is withdrawn
    uint256 reserveAfter = esEXA.reserves(streamId);
    assertEq(reserveAfter, 0);

    // Charlie receives all of deposited reserve much earlier than vesting stream is fully vested and withdrawn
    uint256 EXAAfterCharlie = exa.balanceOf(CHARLIE);
    assertEq(EXAAfterCharlie, reserveBefore);
  }
```

## Impact
When the user, who starts the vesting stream, and the recipient agree that the corresponding vesting stream must be fully vested and withdrawn before the user's deposited reserve EXA tokens can be transferred to the recipient, the recipient can still call the `cancel` function, instead of calling the `withdrawMax` function, much earlier than such vesting stream is fully vested and withdrawn. As a result, the recipient receives all of the deposited reserve EXA tokens much earlier than she or he is entitled to, breaking the previously mentioned agreement.

## Code Snippet

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/periphery/EscrowedEXA.sol#L89-L110
```solidity
  function vest(uint128 amount, address to, uint256 maxRatio, uint256 maxPeriod) public returns (uint256 streamId) {
    ...
    _burn(msg.sender, amount);
    uint256 reserve = amount.mulWadUp(reserveRatio);
    exa.safeTransferFrom(msg.sender, address(this), reserve);
    streamId = sablier.createWithDurations(
      CreateWithDurations({
        asset: exa,
        sender: address(this),
        recipient: to,
        totalAmount: amount,
        cancelable: true,
        transferable: true,
        durations: Durations({ cliff: 0, total: vestingPeriod }),
        broker: Broker({ account: address(0), fee: 0 })
      })
    );
    reserves[streamId] = reserve;
    ...
  }
```

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/periphery/EscrowedEXA.sol#L163-L166
```solidity
  function withdrawMax(uint256 streamId) internal {
    if (sablier.withdrawableAmountOf(streamId) != 0) sablier.withdrawMax(streamId, msg.sender);
    if (sablier.isDepleted(streamId)) returnReserve(streamId, msg.sender);
  }
```

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/periphery/EscrowedEXA.sol#L133-L148
```solidity
  function cancel(uint256[] memory streamIds) external returns (uint256 streamsReserves) {
    uint128 refundableAmount;
    for (uint256 i = 0; i < streamIds.length; ++i) {
      uint256 streamId = streamIds[i];
      checkStream(streamId);
      assert(msg.sender == sablier.getRecipient(streamId));
      streamsReserves += reserves[streamId];
      delete reserves[streamId];
      refundableAmount += sablier.refundableAmountOf(streamId);
      withdrawMax(streamId);
      sablier.cancel(streamId);
    }
    emit Cancel(msg.sender, streamIds);
    _mint(msg.sender, refundableAmount);
    exa.safeTransfer(msg.sender, streamsReserves);
  }
```

## Tool used
Manual Review

## Recommendation
The `cancel` function can be updated to only allow the recipient to withdraw the part of the deposited reserve that is proportional to the time that has passed after the vesting stream is started comparing to the intended vesting period and return the remaining part of the deposited reserve to the user, who started the corresponding vesting stream.