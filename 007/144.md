Trendy Cedar Wasp

high

# The rewards are being distributed incorrectly because the lastConfig is updated inaccurately.

## Summary
`Rewards` are given to `borrowers` and `depositors` as `incentives`.
When `rewards` are available, the `admin` sets them, specifying the `total distribution amount` and `duration` for `distribution`.
The `rewards` are released per `second`, with the `release rate` determined by dividing the `total distribution amount` by the `duration`.
However, in some cases, this `release rate` can be significantly lower than the correct value, and this means that many `rewards` will not be distributed to users.
## Vulnerability Detail
I will describe the issue with an example.

Let's say the current `block.timestamp` is at `20 weeks`.
Initially, there are `4000 rewards` allocated for a `duration` from `40 weeks` to `50 weeks`.
The `lastConfig` is updated as the current `block.timestamp`, i.e.,`20 weeks`.
```solidity
function config(Config[] memory configs) external onlyRole(DEFAULT_ADMIN_ROLE) {
  for (uint256 i = 0; i < configs.length; ) {
    Distribution storage dist = distribution[configs[i].market];
    RewardData storage rewardData = dist.rewards[configs[i].reward];

    if (rewardData.lastUpdate == 0) {  // @audit, lastUpdate is 0
      dist.availableRewards[dist.availableRewardsCount++] = configs[i].reward;
      dist.baseUnit = 10 ** configs[i].market.decimals();
      rewardData.start = configs[i].start;
      rewardData.lastUpdate = configs[i].start;
      rewardData.releaseRate = configs[i].totalDistribution / configs[i].distributionPeriod; // @audit, rate = 4000 / 10 weeks
    } else {
      ...
    }
    rewardData.lastConfig = uint32(block.timestamp);  // @audit, here
    rewardData.end = rewardData.start + uint32(configs[i].distributionPeriod);
    ...
    unchecked {
      ++i;
    }
  }
}
```
Rewards start releasing at `40 weeks`, with a `release rate` equals to `4000 / 10 weeks`.
Now, suppose at `42 weeks`, an additional `5000 rewards` are added by the `admin` for this `duration`.
So the new `total distribution` will be changed to `9000`.
In the below calculation, we erroneously consider `rewards` released for `22 weeks` from the `lastConfig` with current `release rate`.
```solidity
function config(Config[] memory configs) external onlyRole(DEFAULT_ADMIN_ROLE) {
  for (uint256 i = 0; i < configs.length; ) {
    Distribution storage dist = distribution[configs[i].market];
    RewardData storage rewardData = dist.rewards[configs[i].reward];

    if (rewardData.lastUpdate == 0) {
      ...
    } else { // @audit, lastUpdate is not 0
      uint32 start = rewardData.start; // @audit, 40 weeks
      uint32 end = rewardData.end; // @audit, 50 weeks
      bool[] memory ops = new bool[](1);
      ops[0] = true;
      update(
        address(0),
        configs[i].market,
        configs[i].reward,
        accountBalanceOperations(configs[i].market, ops, address(0), start)
      );
      if (block.timestamp < end) { // @audit, 42 weeks < 50 weeks
        uint256 released = 0;
        uint256 elapsed = 0;
        if (block.timestamp > start) { // @audit, 42 weeks > 40 weeks
          released =
            rewardData.lastConfigReleased +
            rewardData.releaseRate *
            (block.timestamp - rewardData.lastConfig); // @audit, 0 + (4000 / 10 weeks) * (42 weeks - 20 weeks) = 8800
          elapsed = block.timestamp - start; // @audit, 42 weeks - 40 weeks = 2 weeks
          if (configs[i].totalDistribution <= released || configs[i].distributionPeriod <= elapsed) {
            revert InvalidConfig();
          }
          rewardData.lastConfigReleased = released;
        }

        rewardData.releaseRate =
          (configs[i].totalDistribution - released) /
          (configs[i].distributionPeriod - elapsed); // @audit, (9000 - 8800) / (10 weeks - 2 weeks) = 200 / 8 weeks
      } else if (rewardData.start != configs[i].start) {
        ...
      }
    }

    unchecked {
      ++i;
    }
  }
}
```
So we calculate: `22 weeks * (4000 / 10 weeks) = 8800` `rewards` already released.
However, in reality, only `800` `rewards` have been released among `9000` total `rewards`.
And new `200` `rewards` will be released in the next `8 weeks`.

In summary, due to the miscalculation, `8000` `rewards` will not be released to the users.

Please check below log:
```solidity
old release rate                            ==>   661375661375661
old release rate * old distribution period  ==>   3999999999999997728000  (4,000 ether)
********************
new release rate                            ==>   41335978835979
new release rate * new distribution period  ==>   200000000000000793600
already released rewards                    ==>   799999999999999545600
the total rewards which will be distributed ==>   1000000000000000339200 (1,000 ether)
```

Please add below test to the `RewardsController.t.sol`:
```solidity
function testLastConfigSet() external {
  vm.warp(20 weeks);

  opRewardAsset.mint(address(rewardsController), 4_000 ether);
  RewardsController.Config[] memory configs = new RewardsController.Config[](1);
  configs[0] = RewardsController.Config({
    market: marketUSDC,
    reward: opRewardAsset,
    priceFeed: MockPriceFeed(address(0)),
    targetDebt: 40_000e6,
    totalDistribution: 4_000 ether,
    start: uint32(40 weeks),
    distributionPeriod: 10 weeks,
    undistributedFactor: 0.5e18,
    flipSpeed: 2e18,
    compensationFactor: 0.85e18,
    transitionFactor: 0.64e18,
    borrowAllocationWeightFactor: 0,
    depositAllocationWeightAddend: 0.02e18,
    depositAllocationWeightFactor: 0.01e18
  });
  rewardsController.config(configs);

  uint256 oldReleaseRate = rewardsController.getReleaseRate(marketUSDC, opRewardAsset);
  console2.log("old release rate                            ==>  ", oldReleaseRate);
  console2.log("old release rate * old distribution period  ==>  ", oldReleaseRate * 10 weeks);

  vm.warp(42 weeks);
  
  opRewardAsset.mint(address(rewardsController), 5_000 ether);
  configs[0] = RewardsController.Config({
    market: marketUSDC,
    reward: opRewardAsset,
    priceFeed: MockPriceFeed(address(0)),
    targetDebt: 40_000e6,
    totalDistribution: 9_000 ether,
    start: uint32(40 weeks),
    distributionPeriod: 10 weeks,
    undistributedFactor: 0.5e18,
    flipSpeed: 2e18,
    compensationFactor: 0.85e18,
    transitionFactor: 0.64e18,
    borrowAllocationWeightFactor: 0,
    depositAllocationWeightAddend: 0.02e18,
    depositAllocationWeightFactor: 0.01e18
  });
  rewardsController.config(configs);

  uint256 newReleaseRate = rewardsController.getReleaseRate(marketUSDC, opRewardAsset);
  console2.log("********************");
  console2.log("new release rate                            ==>  ", newReleaseRate);
  console2.log("new release rate * new distribution period  ==>  ", newReleaseRate * 8 weeks);
  console2.log("already released rewards                    ==>  ", oldReleaseRate * 2 weeks);
  console2.log("the total rewards which will be distributed ==>  ", oldReleaseRate * 2 weeks + newReleaseRate * 8 weeks);
}
```
For test, also add below function to the `RewardsController.sol`:
```solidity
function getReleaseRate(Market market, ERC20 token) external view returns (uint256) {
  return distribution[market].rewards[token].releaseRate;
}
```
## Impact

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/RewardsController.sol#L699
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/RewardsController.sol#L678-L686
## Tool used

Manual Review

## Recommendation
```solidity
function config(Config[] memory configs) external onlyRole(DEFAULT_ADMIN_ROLE) {
  for (uint256 i = 0; i < configs.length; ) {
    if (rewardData.lastUpdate == 0) {
      dist.availableRewards[dist.availableRewardsCount++] = configs[i].reward;
      dist.baseUnit = 10 ** configs[i].market.decimals();
      rewardData.start = configs[i].start;
      rewardData.lastUpdate = configs[i].start;
      rewardData.releaseRate = configs[i].totalDistribution / configs[i].distributionPeriod;
+    rewardData.lastConfig = configs[i].start;
    } else {
      if (block.timestamp < end) {
        uint256 released = 0;
        uint256 elapsed = 0;
        if (block.timestamp > start) {
          released =
            rewardData.lastConfigReleased +
            rewardData.releaseRate *
            (block.timestamp - rewardData.lastConfig);
          elapsed = block.timestamp - start;
          if (configs[i].totalDistribution <= released || configs[i].distributionPeriod <= elapsed) {
            revert InvalidConfig();
          }
          rewardData.lastConfigReleased = released;
+        rewardData.lastConfig = uint32(block.timestamp);
        }

        rewardData.releaseRate =
          (configs[i].totalDistribution - released) /
          (configs[i].distributionPeriod - elapsed);
      } else if (rewardData.start != configs[i].start) {
        rewardData.start = configs[i].start;
        rewardData.lastUpdate = configs[i].start;
        rewardData.releaseRate = configs[i].totalDistribution / configs[i].distributionPeriod;
        rewardData.lastConfigReleased = 0;

+       rewardData.lastConfig = configs[i].start;
      }
    }
-    rewardData.lastConfig = uint32(block.timestamp);
    rewardData.end = rewardData.start + uint32(configs[i].distributionPeriod);
    rewardData.priceFeed = configs[i].priceFeed;
    // set emission and distribution parameters
    rewardData.totalDistribution = configs[i].totalDistribution;
    rewardData.targetDebt = configs[i].targetDebt;
    rewardData.undistributedFactor = configs[i].undistributedFactor;
    rewardData.flipSpeed = configs[i].flipSpeed;
    rewardData.compensationFactor = configs[i].compensationFactor;
    rewardData.borrowAllocationWeightFactor = configs[i].borrowAllocationWeightFactor;
    rewardData.depositAllocationWeightAddend = configs[i].depositAllocationWeightAddend;
    rewardData.transitionFactor = configs[i].transitionFactor;
    rewardData.depositAllocationWeightFactor = configs[i].depositAllocationWeightFactor;

    emit DistributionSet(configs[i].market, configs[i].reward, configs[i]);
    unchecked {
      ++i;
    }
  }
}
```