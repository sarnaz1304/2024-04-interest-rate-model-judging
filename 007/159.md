Tiny Mulberry Tapir

medium

# Modifying Reward Distribution Start Time Could Incur An Incorrect Reward Distribution

## Summary
When the function `config()` is invoked, the `start` of a distribution can be modified. This change will result in an inconsistency between the number of considered maturities before and after the function is invoked when computing the fixed debt.

This inconsistency will directly affect to the index and reward calculation for the borrowing side. 

## Vulnerability Detail
The function `RewardController.config()` allows for modifications to an existing reward redistribution program once it has concluded (`block.timestamp > rewardData.end`).

```solidity=
function config(Config[] memory configs) external onlyRole(DEFAULT_ADMIN_ROLE) {
    ... 
    
    } else {
        uint32 start = rewardData.start;
        uint32 end = rewardData.end;
        // update global indexes before updating distribution values
        bool[] memory ops = new bool[](1);
        ops[0] = true;
        update(
          address(0),
          configs[i].market,
          configs[i].reward,
          accountBalanceOperations(configs[i].market, ops, address(0), start)
        );
    
        ... 
        } else if (rewardData.start != configs[i].start) {
            rewardData.start = configs[i].start;
            rewardData.lastUpdate = configs[i].start;
            rewardData.releaseRate = configs[i].totalDistribution / configs[i].distributionPeriod;
            rewardData.lastConfigReleased = 0;
        }

    ...
}
```

For implementing these changes, the function operates in two main steps:
* **STEP 1**: It invokes the function `update()` to update the values of `borrowIndex` and `depositIndex` of the reward distribution. This ensures that users can claim their rightful share of the rewards from the concluded distribution when its configuration is modified.
* **STEP 2**: It modifies the state of the reward distribution according to the new `configs[i]`.

Delving deeper into **STEP 1**, we see that the `previewAllocation()` function is called within `update()` function to compute the updated values of `borrowIndex` and `depositIndex`.

```solidity=
function previewAllocation(
    RewardData storage rewardData,
    Market market,
    uint256 deltaTime
) internal view returns (uint256 borrowIndex, uint256 depositIndex, uint256 newUndistributed) {
    ... 
    
    {
        uint256 firstMaturity = t.start - (t.start % FixedLib.INTERVAL) + FixedLib.INTERVAL;
        uint256 maxMaturity = block.timestamp -
            (block.timestamp % FixedLib.INTERVAL) +
            (FixedLib.INTERVAL * market.maxFuturePools());
        uint256 fixedDebt = 0;
        for (uint256 maturity = firstMaturity; maturity <= maxMaturity; ) {
            (uint256 borrowed, ) = market.fixedPoolBalance(maturity);
            fixedDebt += borrowed;
            unchecked {
                maturity += FixedLib.INTERVAL;
            }
        }
        m.debt = m.floatingDebt + fixedDebt;
        m.fixedBorrowShares = market.previewRepay(fixedDebt);
    }
    
    ... 
    v.borrowAllocation = v.borrowRewardRule.divWadDown(v.borrowRewardRule + v.depositRewardRule);
    v.depositAllocation = 1e18 - v.borrowAllocation;
    {
        uint256 totalDepositSupply = market.totalSupply();
        uint256 totalBorrowSupply = market.totalFloatingBorrowShares() + m.fixedBorrowShares;
        uint256 baseUnit = distribution[market].baseUnit;
        borrowIndex =
            rewardData.borrowIndex +
            (totalBorrowSupply > 0 ? rewards.mulWadDown(v.borrowAllocation).mulDivDown(baseUnit, totalBorrowSupply) : 0);    
        
        ...
    }
}
```

The computation of `borrowIndex` is based on the division of allocated rewards for borrowing with the `totalBorrowSupply` variable. The `totalBorrowSupply` includes the total floating borrow shares (`market.totalFloatingBorrowShares`) and the corresponding floating borrow shares to the total fixed debt (`m.fixedBorrowShares`).

The `fixedDebt` is determined by summing the fixed debts from all available maturities beginning with the first maturity earlier than the start time of the reward distribution. An issue arises when the start time of the reward distribution is adjusted during **STEP 2** of the `config()` process. Consequently, the total fixed debt calculated after the execution of the `config()` function will differ from that computed before **STEP 2** because the altered start time can result in a different number of considered maturities.

## Impact
If `configs[i].start` is later than `rewardData.start`, the number of considered maturities after calling the `config()` function will be fewer than before it was invoked. Consequently, this will lead to less fixed debt being calculated for the user, resulting in fewer rewards received than expected.

For more detail consider the following scenario: 

0. A reward distribution is applied to the USDC market with 1000 OPs used as rewards for the borrowers, starting at time = 0.
1. Alice borrows 100 USDC from `maturity_1 = INTERVAL`.
2. Alice borrows another 100 USDC from `maturity_2 = 2 * INTERVAL`.
3. After the reward distribution concludes, the owner decides to implement a new configuration for the reward program starting at `INTERVAL + 1`. The process of the `config()` function is outlined as follows:
    * STEP 1: The `update()` function is invoked to update the global index.
        * Given the current start time is 0, the total fixed debt includes the sum of the fixed debts from `maturity_1` and `maturity_2`, resulting in `fixedDebt = 100 + 100 = 200`.
        * The corresponding `totalBorrowSupply` is calculated as:
            ```sh=
            totalBorrowSupply = market.totalFloatingBorrowShares() + m.fixedBorrowShares
                              = 0 + market.previewRepay(200)
                              = 200
            ```
        * Therefore, the `borrowIndex` will be `1000 / 200 = 5`.

    * STEP 2: The start time of the reward distribution is set to `INTERVAL + 1`.

4. Alice triggers the `claim()` function to claim the reward accrued from the previous reward distribution.
    * The borrow balance for Alice is determined by the function `accountFixedBorrowShares()`, as Alice has not taken on any floating debt.
        * Since the current start time is `INTERVAL + 1`, Alice's fixed debt only includes debt from `maturity_2` (because `maturity_1 < start - (start % INTERVAL) + INTERVAL = 2 * INTERVAL`). In other words, `fixedDebt = 100`.
        * The corresponding shares are calculated as `balance = market.previewRepay(100) = 100`.
    * The reward Alice receives is `balance * borrowIndex = 100 * 5 = 500 < 1000`.

From the scenario above, it is evident that Alice loses 500 OPs reward after the `config()` function modifies the start time of the reward distribution.

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/RewardsController.sol#L692-L697
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/RewardsController.sol#L580-L584
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/RewardsController.sol#L614

## Tool used
Manual Review

## Recommendation
Consider introducing a new mapping in the `RewardsController` contract to record each user's most recent borrow share balance. By doing this, the reward calculation for an account could be simplified to just multiplying the stored value from this mapping by the `borrowIndex`.