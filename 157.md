Cheerful Blonde Alligator

medium

# Fixed fees calculation rounds down allowing borrowers take debt with zero cost

## Summary
Fixed fees calculation on `Market::borrowAtMaturity` rounds down and allows borrowers to take debt without paying fees. Rounding direction favors the user and not the protocol.
## Vulnerability Detail
The calculation of fixed fees on `Market::borrowAtMaturity` is done the following way:

```solidity
    uint256 fee;
    {
      uint256 memFloatingAssetsAverage = previewFloatingAssetsAverage();
      uint256 memFloatingDebt = floatingDebt;
      uint256 fixedRate = interestRateModel.fixedRate(
        maturity,
        maxFuturePools,
        fixedUtilization(pool.supplied, pool.borrowed, memFloatingAssetsAverage),
        floatingUtilization(memFloatingAssetsAverage, memFloatingDebt),
        globalUtilization(memFloatingAssetsAverage, memFloatingDebt, floatingBackupBorrowed)
      );
      fee = assets.mulWadDown(fixedRate.mulDivDown(maturity - block.timestamp, 365 days));
    }
```

`assets` is the amount the user wants to borrow; `fixedRate` is the yearly fixed rate with 1e18 being 100%; `maturity` is the maturity date of a pool; `block.timestamp` is the moment that the user is taking the debt; `365 days` is 31536000; `WAD` is 1e18.

In order to round down the last division and get `assets` borrowed for free:
`assets <= WAD / [ (fixedRate . (maturity - block.timestamp)) / 365 days ]`

With that equation you can play around and give different values to see how that would behave:
For example:
`assets <= 1e18 / [ (2.5e16 . (2419200 - 1209600) / 31536000 ]` (two weeks borrow)
`assets <= 1042`

**Proof of Code/Concept:** 
To set this up, you just have to modify the value of the mock interest rate from 0.1e18 to 2.5e16: 
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/test/Market.t.sol#L56

Add the following test to `test/Market.t.sol`:
```solidity
function testBorrowAtMaturityWithZeroFees() public {
    // set up
    auditor.enterMarket(market);
  
    // so I can borrow something
    market.deposit(10 ether, address(this));

    vm.warp(FixedLib.INTERVAL / 2);
  
    uint256 asset = 1042;

    // doesnt matter how many times you execute it, it will cost always zero
    for (uint256 i = 0; i < 1000; i++) {
      market.borrowAtMaturity(FixedLib.INTERVAL, asset, asset, address(this), address(this));
    }

    (uint256 borrowed, uint256 fee) = market.fixedBorrowPositions(FixedLib.INTERVAL, address(this));
    assertEq(fee, 0);
    assertEq(borrowed, asset * 1000);
  }
```

Although this incorrect behavior will always occur, this issue to have impact needs the asset being borrowed have less than 18 decimals (such as USDC which is listed), and the fixed rate at the moment being low.  This is an edge case. However, rounding directions should always favor the protocol to avoid having issues with edge cases.
## Impact
Borrowers could take fixed debt with zero cost.
## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L309-L321
## Tool used

Manual Review
## Recommendation
Always chose rounding directions in favor of the protocol. Consider adding a minimum amount to be borrowed or a minimum fee amount.