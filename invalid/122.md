Faithful Felt Swift

medium

# before update earningsAccumulator , should execute accrueAccumulatedEarnings() first

## Summary
protocol will continuously accumulate the current `earningsAccumulator` according to the passage of time and record it into `floatingAssets`
But when adding `earningsAccumulator`, the accumulation `accrueAccumulatedEarnings()` is not performed first
Causes the accumulation to be incorrect

## Vulnerability Detail
The formula for `accumulatedEarnings ()` is as follows:
```solidity
  function accumulatedEarnings() internal view returns (uint256 earnings) {
    uint256 elapsed = block.timestamp - lastAccumulatorAccrual;
    if (elapsed == 0) return 0;
    return
      earningsAccumulator.mulDivDown(
        elapsed,
        elapsed + earningsAccumulatorSmoothFactor.mulWadDown(maxFuturePools * FixedLib.INTERVAL)
      );
  }
```

That is: over time, it is continuously accumulated and credited to `floatingAssets += accrueAccumulatedEarnings()`

However, the accumulation is not triggered before updating the `earningsAccumulator `
Take `depositAtMaturity()` as an example

```solidity
contract Market is Initializable, AccessControlUpgradeable, PausableUpgradeable, ERC4626 {
...
  function depositAtMaturity(
    uint256 maturity,
    uint256 assets,
    uint256 minAssetsRequired,
    address receiver
  ) external whenNotPaused whenNotFrozen returns (uint256 positionAssets) {
    if (assets == 0) revert ZeroDeposit();
    // reverts on failure
    FixedLib.checkPoolState(maturity, maxFuturePools, FixedLib.State.VALID, FixedLib.State.NONE);

    FixedLib.Pool storage pool = fixedPools[maturity];

    uint256 backupEarnings = pool.accrueEarnings(maturity);
    floatingAssets += backupEarnings;

    (uint256 fee, uint256 backupFee) = pool.calculateDeposit(assets, backupFeeRate);
    positionAssets = assets + fee;
    if (positionAssets < minAssetsRequired) revert Disagreement();

    floatingBackupBorrowed -= pool.deposit(assets);
    pool.unassignedEarnings -= fee + backupFee;
@> //@audit missing floatingAssets += accrueAccumulatedEarnings() first
    earningsAccumulator += backupFee;
```

This will cause the accumulation to be too fast. 
`backupFee` is also counted in the elapsed time `elapsed` and becomes:
`(old earningsAccumulator + backupFee) * elapsed/(elapsed + earningsAccumulatorSmoothFactor * maxFuturePools * FixedLib. INTERVAL) `

## Impact

`earningsAccumulator` accumulates too fast

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L253
## Tool used

Manual Review

## Recommendation

```diff
  function depositAtMaturity(
    uint256 maturity,
    uint256 assets,
    uint256 minAssetsRequired,
    address receiver
  ) external whenNotPaused whenNotFrozen returns (uint256 positionAssets) {
    if (assets == 0) revert ZeroDeposit();
    // reverts on failure
    FixedLib.checkPoolState(maturity, maxFuturePools, FixedLib.State.VALID, FixedLib.State.NONE);

    FixedLib.Pool storage pool = fixedPools[maturity];

    uint256 backupEarnings = pool.accrueEarnings(maturity);
    floatingAssets += backupEarnings;

    (uint256 fee, uint256 backupFee) = pool.calculateDeposit(assets, backupFeeRate);
    positionAssets = assets + fee;
    if (positionAssets < minAssetsRequired) revert Disagreement();

    floatingBackupBorrowed -= pool.deposit(assets);
    pool.unassignedEarnings -= fee + backupFee;
+   floatingAssets += accrueAccumulatedEarnings();
    earningsAccumulator += backupFee;
```
