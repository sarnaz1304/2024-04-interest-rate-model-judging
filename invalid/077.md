Crazy Pickle Bee

high

# Incorrect rounding direction might cause floating debt's interest to 0.

## Summary
Floating debt's interest calculation in function Market::updateFloatingDebt() is rounding down. It means the interest might be 0 if updating frequency is high enough.

## Vulnerability Detail
We will call function updateFloatingDebt() to calculate the whole market's floating debt from `lastFloatingDebtUpdate` to block.timestamp. The calculation of floating borrow interest is rounding down and the floating borrow interest is one function related with timeslot, which means this time slot's floating borrow interest could be zero when time slot is short enough, eg one block, especially for low decimal token market.
In current exactly WBTC market, the floating borrow rate is less than 2% per year. 
For example: we have one WBTC floating debt in WBTC market, and floating borrow rate is 2% per year, we update the floating debt via function borrow()/deposit()/etc every block in Op main net(2 seconds per block).
The floating debt interest for one block should be `1 * 10^8 * 2/100 * 2/365 days`. The result is 0, so if borrowers continue to update updateFloatingDebt() in every block. Borrowers' borrow interest is 0. 

```c
  function updateFloatingDebt() internal returns (uint256 treasuryFee) {
    uint256 memFloatingDebt = floatingDebt;
    uint256 memFloatingAssets = floatingAssets;
    uint256 utilization = floatingUtilization(memFloatingAssets, memFloatingDebt);
    //newDebt --> floating pool interest from lastFloatingDebtUpdate to block.timestamp
    uint256 newDebt = memFloatingDebt.mulWadDown(
      interestRateModel
        .floatingRate(utilization, globalUtilization(memFloatingAssets, memFloatingDebt, floatingBackupBorrowed))
        .mulDivDown(block.timestamp - lastFloatingDebtUpdate, 365 days)
    );

    memFloatingDebt += newDebt; // update floating total debt
    treasuryFee = newDebt.mulWadDown(treasuryFeeRate);
    //new generated debt will become part of floating asset
    floatingAssets = memFloatingAssets + newDebt - treasuryFee;
    floatingDebt = memFloatingDebt;
    lastFloatingDebtUpdate = uint32(block.timestamp);
    emit FloatingDebtUpdate(block.timestamp, utilization);
  }
```
### Poc
Setup one low decimal Token, eg, WBTC(decimal 8)
```c
  function setUp() external {
    vm.warp(0);

    MockERC20 asset = new MockERC20("WBTC", "WBTC", 8);
    weth = new MockERC20("WETH", "WETH", 18);

    auditor = Auditor(address(new ERC1967Proxy(address(new Auditor(18)), "")));
    auditor.initialize(Auditor.LiquidationIncentive(0.09e18, 0.01e18));
    vm.label(address(auditor), "Auditor");

    irm = new MockInterestRateModel(0.1e18);

    market = Market(address(new ERC1967Proxy(address(new Market(asset, auditor)), "")));
    market.initialize(
      "DAI",
      3,
      1e18,
      InterestRateModel(address(irm)),
      0.02e18 / uint256(1 days),
      1e17,
      0,
      0.0046e18,
      0.42e18
    );
    vm.label(address(market), "MarketDAI");
    daiPriceFeed = new MockPriceFeed(18, 1e18);

    marketWETH = Market(address(new ERC1967Proxy(address(new Market(weth, auditor)), "")));
    marketWETH.initialize(
      "WETH",
      12,
      1e18,
      InterestRateModel(address(irm)),
      0.02e18 / uint256(1 days),
      1e17,
      0,
      0.0046e18,
      0.42e18
    );
    vm.label(address(marketWETH), "MarketWETH");

    auditor.enableMarket(market, daiPriceFeed, 0.8e18);
    auditor.enableMarket(marketWETH, IPriceFeed(auditor.BASE_FEED()), 0.9e18);
    auditor.enterMarket(marketWETH);

    vm.label(BOB, "Bob");
    vm.label(ALICE, "Alice");
    asset.mint(BOB, 50_000 ether);
    asset.mint(ALICE, 50_000 ether);
    asset.mint(address(this), 1_000_000 ether);
    weth.mint(address(this), 1_000_000 ether);

    asset.approve(address(market), type(uint256).max);
    weth.approve(address(marketWETH), type(uint256).max);
    vm.prank(BOB);
    asset.approve(address(market), type(uint256).max);
    vm.prank(BOB);
    weth.approve(address(marketWETH), type(uint256).max);
    vm.prank(ALICE);
    asset.approve(address(market), type(uint256).max);
  }

```

Borrow some BTC and trigger function updateFloatingDebt() every 2 secs to check the `floatingDebt`'s change. There is no change in `floatingDebt`. It means that no debt interest is generated because of the incorrect rounding direction.

```c
  function testSingleFloatingBorrow() external {
    market.deposit(10e8, address(this));
    console.log('Initial float debt:', market.floatingDebt());
    market.borrow(1e8, address(this), address(this));
    console.log('float debt after borrow:', market.floatingDebt());
    vm.warp(2);
    market.deposit(0.1e8, address(this));
    console.log('update floating debt in 2 secs: ', market.floatingDebt());
    vm.warp(2);
    market.deposit(0.1e8, address(this));
    //market.setTreasury(BOB, 0.1e18);
    console.log('update floating debt in another 2secs: ', market.floatingDebt());
  }
```
```output
Logs:
  Initial float debt: 0
  float debt after borrow: 100000000
  update floating debt in 2 secs:  100000000
  update floating debt in another 2secs:  100000000
```
## Impact
Floating borrowers' interest is 0 and floating pool's lenders will lose the profit which they deserve.

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L888-L904

## Tool used

Manual Review

## Recommendation
Round up for floating debt calculation

