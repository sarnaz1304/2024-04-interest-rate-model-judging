Stable Fiery Pangolin

medium

# `RewardsController.permitSender` modifier is not compliant to EIP-712 standard

## Summary
In the `RewardsController` contract, the `claim(MarketOperation[] memory marketOps, ClaimPermit calldata permit)` function uses the `permitSender` modifier, which doesn't comply with the EIP-712 standard because the `encodeData` contains `nonces[permit.owner]` but the `encodeType` does not include the `permit.owner`'s nonce variable type and name in the `permitSender` modifier. Claiming the `permit.owner`'s rewards would always fail when using the EIP-712 compliant signature of the `permit.owner` since such signature's `encodeType` does include the `permit.owner`'s nonce variable type and name.

## Vulnerability Detail
The `claim` function calls the `permitSender` modifier as shown in the Code Snippet section. In the `permitSender` modifier, the `encodeData` contains `nonces[permit.owner]` but the `encodeType` is `ClaimPermit(address owner,address spender,address[] assets,uint256 deadline)` that does not include the `permit.owner`'s nonce variable type and name. According to https://eips.ethereum.org/EIPS/eip-712#definition-of-encodedata, `encodeData` `is ``enc(value₁) ‖ enc(value₂) ‖ … ‖ enc(valueₙ)``, i.e. the concatenation of the encoded member values in the order that they appear in the type`; since the `encodeData` includes `nonces[permit.owner]`, the `encodeType` needs to include the `permit.owner`'s nonce variable type and name as well to be compliant to the EIP-712 standard but that is not the case. In contrast to the `permitSender` modifier that is not compliant to the EIP-712 standard, the `encodeType` that is a part of the `permit.owner`'s signature can contain the `permit.owner`'s nonce variable type and name to be compliant to the EIP-712 standard. In this case, calling the `claim` function with the `permit` that includes such EIP-712 compliant signature of the `permit.owner` would always revert because `recoveredAddress == permit.owner` would be false in the `permitSender` modifier.

For POC, please add the following test in `protocol\test\RewardsController.t.sol`. This test will pass to demonstrate the described scenario.

```solidity
  function test_failToClaimRewardsOfPermitOwnerUsingEIP712CompliantSignature() external {
    uint256 accountKey = 0xb0b;
    address account = vm.addr(accountKey);
    marketUSDC.deposit(100e6, account);
    vm.prank(account);
    marketUSDC.borrow(10e6, account, account);

    vm.warp(4 days + 20 minutes);
    uint256 opClaimableRewards = rewardsController.allClaimable(account, opRewardAsset);
    bool[] memory ops = new bool[](2);
    ops[0] = false;
    ops[1] = true;
    RewardsController.MarketOperation[] memory marketOps = new RewardsController.MarketOperation[](1);
    marketOps[0] = RewardsController.MarketOperation({ market: marketUSDC, operations: ops });
    ERC20[] memory assets = new ERC20[](2);
    assets[0] = opRewardAsset;
    assets[1] = exaRewardAsset;

    ClaimPermit memory permit;
    permit.owner = account;
    permit.assets = assets;
    permit.deadline = block.timestamp;

    // encodeType of permit.owner's signature includes permit.owner's nonce variable type and name to be compliant to EIP-712 standard
    (permit.v, permit.r, permit.s) = vm.sign(
      accountKey,
      keccak256(
        abi.encodePacked(
          "\x19\x01",
          rewardsController.DOMAIN_SEPARATOR(),
          keccak256(
            abi.encode(
              keccak256("ClaimPermit(address owner,address spender,address[] assets,uint256 nonce,uint256 deadline)"),
              permit.owner,
              address(this),
              permit.assets,
              rewardsController.nonces(permit.owner),
              permit.deadline
            )
          )
        )
      )
    );

    // claiming permit.owner's rewards fails even though permit.owner does allow her or his rewards to be claimed by this contract
    vm.expectRevert();
    rewardsController.claim(marketOps, permit);
  }
```

## Impact
Users who use this protocol and other protocols that integrate with this protocol are DOS'ed from claiming the `permit.owner`'s rewards when using such EIP-712 compliant signature of the `permit.owner` even though such `permit.owner` does allow these parties to claim her or his rewards.

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/RewardsController.sol#L165-L170
```solidity
  function claim(
    MarketOperation[] memory marketOps,
    ClaimPermit calldata permit
  ) external permitSender(permit) returns (ERC20[] memory, uint256[] memory claimedAmounts) {
    return claim(marketOps, msg.sender, permit.assets);
  }
```

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/RewardsController.sol#L743-L773
```solidity
  modifier permitSender(ClaimPermit calldata permit) {
    assert(_claimSender == address(0));
    assert(permit.deadline >= block.timestamp);
    unchecked {
      address recoveredAddress = ecrecover(
        keccak256(
          abi.encodePacked(
            "\x19\x01",
            DOMAIN_SEPARATOR(),
            keccak256(
              abi.encode(
                keccak256("ClaimPermit(address owner,address spender,address[] assets,uint256 deadline)"),
                permit.owner,
                msg.sender,
                permit.assets,
                nonces[permit.owner]++,
                permit.deadline
              )
            )
          )
        ),
        permit.v,
        permit.r,
        permit.s
      );
      assert(recoveredAddress != address(0) && recoveredAddress == permit.owner);
      _claimSender = permit.owner;
    }
    _;
    assert(_claimSender == address(0));
  }
```

## Tool used
Manual Review

## Recommendation
The `encodeType` in the `permitSender` modifier can be updated to include the `permit.owner`'s nonce variable type and name.