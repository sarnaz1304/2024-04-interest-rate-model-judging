Shaggy Mint Liger

medium

# Lack of calling the Market#`afterDeposit()` inside the Market#`depositAtMaturity()`

## Summary

To update the `floatingAssetsAverage` and deposit the `treasuryFee` to the treasury address when a user would deposit to the fixed (Maturity) pool, the Market#`afterDeposit()` is supposed to be called inside the Market#`depositAtMaturity()`. 

However, within the Market#`depositAtMaturity()`, the Market#`afterDeposit()` would **not** to be called. 

This lead to the unexpected situation that the `floatingAssetsAverage` is **not** updated and the `treasuryFee` is **not** deposited into the treasury address when a user would call the Market#`depositAtMaturity()`. 


## Vulnerability Detail

When a user would deposit a certain amount of the assets into a fixed (maturity) pool, the user would call the Market#`depositAtMaturity()` like this:
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L232-L272
```solidity
  /// @notice Deposits a certain amount to a maturity.
  /// @param maturity maturity date where the assets will be deposited.
  /// @param assets amount to receive from the msg.sender.
  /// @param minAssetsRequired minimum amount of assets required by the depositor for the transaction to be accepted.
  /// @param receiver address that will be able to withdraw the deposited assets.
  /// @return positionAssets total amount of assets (principal + fee) to be withdrawn at maturity.
  function depositAtMaturity(
    uint256 maturity,
    uint256 assets,
    uint256 minAssetsRequired,
    address receiver
  ) external whenNotPaused whenNotFrozen returns (uint256 positionAssets) {
    if (assets == 0) revert ZeroDeposit();
    // reverts on failure
    FixedLib.checkPoolState(maturity, maxFuturePools, FixedLib.State.VALID, FixedLib.State.NONE);

    FixedLib.Pool storage pool = fixedPools[maturity];

    uint256 backupEarnings = pool.accrueEarnings(maturity);
    floatingAssets += backupEarnings;

    (uint256 fee, uint256 backupFee) = pool.calculateDeposit(assets, backupFeeRate);
    positionAssets = assets + fee;
    if (positionAssets < minAssetsRequired) revert Disagreement();

    floatingBackupBorrowed -= pool.deposit(assets); 
    pool.unassignedEarnings -= fee + backupFee;
    earningsAccumulator += backupFee;

    // update account's position
    FixedLib.Position storage position = fixedDepositPositions[maturity][receiver];

    // if account doesn't have a current position, add it to the list
    if (position.principal == 0) {
      Account storage account = accounts[receiver];
      account.fixedDeposits = account.fixedDeposits.setMaturity(maturity);
    }

    position.principal += assets;
    position.fee += fee;

    emit DepositAtMaturity(maturity, msg.sender, receiver, assets, fee);
    emitMarketUpdate();
    emitFixedEarningsUpdate(maturity);

    asset.safeTransferFrom(msg.sender, address(this), assets);
  }
```

Within the Market#`afterDeposit()`, the following function would be called like this:
- Market#`updateFloatingAssetsAverage()`
- Market#`depositToTreasury()`
   https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L711
   https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L715
```solidity
  /// @notice Hook to update the floating pool average, floating pool balance and distribute earnings from accumulator.
  /// @param assets amount of assets to be deposited to the floating pool.
  function afterDeposit(uint256 assets, uint256) internal override whenNotPaused whenNotFrozen {
    updateFloatingAssetsAverage(); ///<------------------ @audit
    uint256 treasuryFee = updateFloatingDebt();
    uint256 earnings = accrueAccumulatedEarnings();
    floatingAssets += earnings + assets;
    depositToTreasury(treasuryFee); ///<------------------ @audit
    ...
  }
```

To update the `floatingAssetsAverage` and deposit the `treasuryFee` to the treasury address when a user would deposit to the fixed (Maturity) pool, the Market#`afterDeposit()` is supposed to be called inside the Market#`depositAtMaturity()`. 

However, within the Market#`depositAtMaturity()`, the Market#`afterDeposit()` would **not** to be called. 


## Impact

This lead to the unexpected situation that the `floatingAssetsAverage` would **not** be updated and the `treasuryFee` would **not** be deposited into the treasury address when a user would call the Market#`depositAtMaturity()`. 


## Code Snippet
- https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L232-L272
- https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L710-L717

## Tool used
- Manual Review


## Recommendation
Within the Market#`depositAtMaturity()`, consider invoking the Market#`afterDeposit()` like this:
```diff
  function depositAtMaturity(
    uint256 maturity,
    uint256 assets,
    uint256 minAssetsRequired,
    address receiver
  ) external whenNotPaused whenNotFrozen returns (uint256 positionAssets) {
    ...
    asset.safeTransferFrom(msg.sender, address(this), assets);

+   afterDeposit(assets);
  }
```
