Melted Sapphire Iguana

medium

# Cross Market Liquidation will be impossible when the debt market != repay market

## Summary

Cross-market liquidations is problematic, unlikely and possibly disincentivized when the debt market to be liquidated is not the repay market. And when the borrower who is in default entered multiple markets, liquidation becomes really tedious. Without prompt liquidation, the risk of all parties, most importantly, the borrowers and the lenders, isn't properly managed.

## Vulnerability Detail 

Exactly is a lending market that aggregates the surplus in the defi economy and supplies loans to borrowers at a floating or fixed rate. For supplying liquidity, the LPs earn a fixed or floating rate yield. Put together, this creates a floating and fixed rate market on Exactly.

Borrowers are allowed to borrow up to a certain portion of the collateral they deposited but there are incidences such as a sharp drop in the price of the collateral causing a healthy loan position to be unhealthy and eligible for partial or complete liquidation. To regulate the market when this happens, Exactly provided for liquidations that allow anyone to help repay a loan position to health for a little incentive in [`Market::liquidate()`](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L545C3-L614C4).

Exactly's formula for handling liquidations and bad debt according to their docs is as follows:

> Then it proceeds to iterate over all fixed loans where the borrower borrowed, first repaying the oldest ones. If extra repayment is needed, it continues with the variable debt. After the liquidator seizes what corresponds, a call to the Auditor's handleBadDebt function is made. This function checks if the borrower doesn't have any more collateral and in case it doesn't, then it forwards the execution to the clearBadDebt function in each Market. This one finally deletes the debt and spreads the losses subtracting them from some general accumulated earnings.

Unfortunately, this is not the case in `Market::liquidate()`.

```solidity
function liquidate(
    address borrower,
    uint256 maxAssets,
    Market seizeMarket
  ) external whenNotPaused returns (uint256 repaidAssets) {
    ...........................
    Account storage account = accounts[borrower];

    {
      uint256 packedMaturities = account.fixedBorrows;
      uint256 maturity = packedMaturities & ((1 << 32) - 1);
      packedMaturities = packedMaturities >> 32;
      while (packedMaturities != 0 && maxAssets != 0) {
        if (packedMaturities & 1 != 0) {
          uint256 actualRepay;
          if (block.timestamp < maturity) {
            actualRepay = noTransferRepayAtMaturity(maturity, maxAssets, maxAssets, borrower, false);
            maxAssets -= actualRepay;
          } else {
            uint256 position;
            {
              FixedLib.Position storage p = fixedBorrowPositions[maturity][borrower];
              position = p.principal + p.fee;
            }
            uint256 debt = position + position.mulWadDown((block.timestamp - maturity) * penaltyRate);
            actualRepay = debt > maxAssets ? maxAssets.mulDivDown(position, debt) : maxAssets;

            if (actualRepay == 0) maxAssets = 0;
            else {
              actualRepay = noTransferRepayAtMaturity(maturity, actualRepay, maxAssets, borrower, false);
              maxAssets -= actualRepay;
            }
          }
          repaidAssets += actualRepay;
        }
        packedMaturities >>= 1;
        maturity += FixedLib.INTERVAL;
      }
    }

    if (maxAssets != 0 && account.floatingBorrowShares != 0) {
      uint256 borrowShares = previewRepay(maxAssets);
      if (borrowShares != 0) {
        (uint256 actualRepayAssets, ) = noTransferRefund(borrowShares, borrower);
        repaidAssets += actualRepayAssets;
      }
    }

    .......................
  }
```
Instead of aggregating the fixed and flexible loan values of all the debt markets that the borrower is in, a system similar to the one employed in [`Auditor::accountLiquidity()`](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Auditor.sol#L107C3-L148C4), `Market::liquidate()` only looks at that of the repay market. This means if the repay market != debt market, cross-market liquidation is unlikely.

Assume a user, BOB, has ETH in his wallet but needs a USDC loan, he deposits his 1000 ETH into marketETH and then borrows 500 USDC from marketUSDC. Due to the price drop in ETH, the adjusted collateral value < adjusted debt value opening BOB up to liquidation. If a liquidator, ALICE, has enough rETH to bring BOB back to health, she won't be able to call liquidate on BOB from marketrETH because since the repay market is marketrETH which is not the debt market (marketUSDC), the debt position (fixed and floating) will come back empty.

### Proof of Concept

<details>

```solidity
  function test__liquidationCannotHappenOutsideRepayMarket() public {
    MockERC20 asset = MockERC20(address(market.asset()));

    market.deposit(asset.balanceOf(address(this)), address(this));
    marketWETH.deposit(weth.balanceOf(address(this)), address(this));

    vm.startPrank(BOB);
    //remove whatever balance has been given before
    asset.burn(BOB, asset.balanceOf(BOB));
    weth.burn(BOB, weth.balanceOf(BOB));

    //grant BOB new balances for both tokens
    asset.mint(BOB, 50_000 ether);
    weth.mint(BOB, 50_000 ether);

    asset.approve(address(market), type(uint256).max);
    weth.approve(address(marketWETH), type(uint256).max);

    auditor.enterMarket(market);
    auditor.enterMarket(marketWETH);

    daiPriceFeed.setPrice(50e18);

    uint depositShares = marketWETH.deposit(asset.balanceOf(BOB), BOB);

    emit log_named_uint("BOB's ERC4626 balance after deposit is ", depositShares);

    (uint adjustedCollateral, uint adjustedDebtPlusEffects) = auditor.accountLiquidity(BOB, Market(address(0)), 0);

    emit log_named_uint("BOB's Adjusted collateral is ", adjustedCollateral);
    emit log_named_uint("BOB's Adjusted debt + Effects is ", adjustedDebtPlusEffects);

    emit log_named_uint("Borrow share gotten is ", market.borrow(720 ether, BOB, BOB));

    daiPriceFeed.setPrice(75e18);

    (adjustedCollateral, adjustedDebtPlusEffects) = auditor.accountLiquidity(BOB, Market(address(0)), 0);

    vm.stopPrank();

    assertLt(adjustedCollateral, adjustedDebtPlusEffects, "Borrower is not in liquidatable state yet");

    MockERC20 rETH;
    Market marketrETH;
    MockPriceFeed rETHPriceFeed;

    rETH = new MockERC20("rETH", "reETH", 18);

    marketrETH = Market(address(new ERC1967Proxy(address(new Market(rETH, auditor)), "")));
    marketrETH.initialize(
      "rETH",
      3,
      1e18,
      InterestRateModel(address(irm)),
      0.02e18 / uint256(1 days),
      1e17,
      0,
      0.0046e18,
      0.42e18
    );
    vm.label(address(marketrETH), "marketrETH");
    rETHPriceFeed = new MockPriceFeed(18, 1e18);
    auditor.enableMarket(marketrETH, rETHPriceFeed, 0.9e18);
    auditor.setAdjustFactor(marketWETH, 0.5e18);

    (adjustedCollateral, adjustedDebtPlusEffects) = auditor.accountLiquidity(BOB, Market(address(0)), 0);

    console.log("Bob's adjusted collateral is %s and adjusted debt is %s", adjustedCollateral, adjustedDebtPlusEffects);

    console.log("MaxRepayAsset from check Liquidation call is %s", auditor.checkLiquidation(market, marketWETH, BOB, type(uint256).max));

    vm.startPrank(ALICE); //ALICE HERE IS THE LIQUIDATOR

    //remove whatever balance has been given before
    asset.burn(ALICE, asset.balanceOf(ALICE));
    weth.burn(ALICE, weth.balanceOf(ALICE));

    //grant ALICE new balances for both tokens
    rETH.mint(ALICE, 1_000_000 ether);

    rETH.approve(address(marketrETH), type(uint256).max);

    auditor.enterMarket(marketrETH);

    vm.expectRevert(ZeroWithdraw.selector);
    marketrETH.liquidate(BOB, type(uint256).max, marketWETH);
  }
```

</details>

### **What if the liquidator does the same thing as the Exactly Liquidation Bot, use Uniswap to swap to the debt market token and then performs the liquidation?**

Well, with this, liquidation just became a strenuous process which is not exactly looking good for Exactly. There is a swap cost involved on the part of the liquidator which for some small-scale liquidators, might disincentivize them. Also, in cases where the borrower to be liquidated borrowed from more markets on the collateral he supplied, then the liquidator has to swap multiple times to be able to liquidate the borrower. As established by Exactly, the bot cannot be solely relied on to get the job done because it takes a _few events_ before the bot triggers the liquidation process. They also rely on these liquidators to help restore balance to the markets and the entire protocol.

## Impact

1. Cross-market liquidations become problematic and unlikely.
2. Swift liquidations help mitigate the risk of losses for both borrowers and lenders. If a borrower's collateral value drops below a certain threshold, prompt liquidation can prevent further losses. If the promptness of liquidations is taken out because of a cross-market borrow operation, this benefit is also stripped away.

## Code Snippet

```solidity
  function liquidate(
    address borrower,
    uint256 maxAssets,
    Market seizeMarket
  ) external whenNotPaused returns (uint256 repaidAssets) {
    if (msg.sender == borrower) revert SelfLiquidation();

    maxAssets = auditor.checkLiquidation(this, seizeMarket, borrower, maxAssets);
    if (maxAssets == 0) revert ZeroRepay();

    Account storage account = accounts[borrower];

    {
      uint256 packedMaturities = account.fixedBorrows;
      uint256 maturity = packedMaturities & ((1 << 32) - 1);
      packedMaturities = packedMaturities >> 32;
      while (packedMaturities != 0 && maxAssets != 0) {
        if (packedMaturities & 1 != 0) {
          uint256 actualRepay;
          if (block.timestamp < maturity) {
            actualRepay = noTransferRepayAtMaturity(maturity, maxAssets, maxAssets, borrower, false);
            maxAssets -= actualRepay;
          } else {
            uint256 position;
            {
              FixedLib.Position storage p = fixedBorrowPositions[maturity][borrower];
              position = p.principal + p.fee;
            }
            uint256 debt = position + position.mulWadDown((block.timestamp - maturity) * penaltyRate);
            
            actualRepay = debt > maxAssets ? maxAssets.mulDivDown(position, debt) : maxAssets;

            if (actualRepay == 0) maxAssets = 0;
            else {
              actualRepay = noTransferRepayAtMaturity(maturity, actualRepay, maxAssets, borrower, false);
              maxAssets -= actualRepay;
            }
          }
          repaidAssets += actualRepay;
        }
        packedMaturities >>= 1;
        maturity += FixedLib.INTERVAL;
      }
    }

    if (maxAssets != 0 && account.floatingBorrowShares != 0) {
      uint256 borrowShares = previewRepay(maxAssets);
      if (borrowShares != 0) {
        (uint256 actualRepayAssets, ) = noTransferRefund(borrowShares, borrower);
        repaidAssets += actualRepayAssets;
      }
    }

    (uint256 lendersAssets, uint256 seizeAssets) = auditor.calculateSeize(this, seizeMarket, borrower, repaidAssets);
    earningsAccumulator += lendersAssets;

    if (address(seizeMarket) == address(this)) {
      internalSeize(this, msg.sender, borrower, seizeAssets);
    } else {
      seizeMarket.seize(msg.sender, borrower, seizeAssets);

      emitMarketUpdate();
    }

    emit Liquidate(msg.sender, borrower, repaidAssets, lendersAssets, seizeMarket, seizeAssets);

    auditor.handleBadDebt(borrower);

    asset.safeTransferFrom(msg.sender, address(this), repaidAssets + lendersAssets);
  }
```

## Tool used

Manual Review & Foundry Testing

## Recommendation

Consider looping through all markets, aggregate the fixed and floating rate loans and resolve them from the oldest as mentioned in the docs. Better still, from most likely liquidatable to least likely.

## Recommendation
