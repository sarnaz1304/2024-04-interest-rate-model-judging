Trendy Cedar Wasp

medium

# Borrowers can profit by borrowing and repaying before the maturity date.

## Summary
When there are no `fixed depositors`, the funds `borrowed` through `fixed borrowing` are backed by `floating assets`.
`Borrowers` are required to repay the `principal` amount along with a `fee`, which is determined based on the `maturity` time.
If a `borrower` repays the `debt` before the `maturity`, the actual repaid amount is calculated based on the `unassignedEarnings` value of that `maturity pool`.
However, there is a chance for a `borrower` to generate `profit` from these operations.
In any `protocol`, `borrowers` are not permitted to repay less than the `principal` amount.
## Vulnerability Detail
When users `borrow` with a `maturity` specified, the `borrowed` amounts are backed by `floating assets` if there are no fixed depositors.
```solidity
function borrowAtMaturity(
  uint256 maturity,
  uint256 assets,
  uint256 maxAssets,
  address receiver,
  address borrower
) external whenNotPaused whenNotFrozen returns (uint256 assetsOwed) {
  {
    uint256 backupDebtAddition = pool.borrow(assets);  // @audit, here
    if (backupDebtAddition != 0) {
      uint256 newFloatingBackupBorrowed = floatingBackupBorrowed + backupDebtAddition;
      depositToTreasury(updateFloatingDebt());
      if (newFloatingBackupBorrowed + floatingDebt > floatingAssets.mulWadDown(1e18 - reserveFactor)) {
        revert InsufficientProtocolLiquidity();
      }
      floatingBackupBorrowed = newFloatingBackupBorrowed;
    }
  }
}
```
The total `borrowed amount` is backed by `floating assets` because the `supplied` amount is `0`.
```solidity
function borrow(Pool storage pool, uint256 amount) internal returns (uint256 backupDebtAddition) {
  uint256 borrowed = pool.borrowed;
  uint256 newBorrowed = borrowed + amount;

  backupDebtAddition = newBorrowed - Math.min(Math.max(borrowed, pool.supplied), newBorrowed); // @audit, here
  pool.borrowed = newBorrowed;
}
```
Then the `fees` are distributed based on the amount backed by `floating assets`.
```solidity
function borrowAtMaturity(
  uint256 maturity,
  uint256 assets,
  uint256 maxAssets,
  address receiver,
  address borrower
) external whenNotPaused whenNotFrozen returns (uint256 assetsOwed) {
  (uint256 newUnassignedEarnings, uint256 newBackupEarnings) = pool.distributeEarnings(  // @audit, here
    chargeTreasuryFee(fee),
    assets
  );
  if (newUnassignedEarnings != 0) pool.unassignedEarnings += newUnassignedEarnings;
}
```
As we seen earlier, since all `borrow` amounts are backed by `floating assets`, the total `fee` (excluding the portion for the `treasury`) is added to the `unassignedEarnings` for that `maturity pool`.
```solidity
function distributeEarnings(
  Pool memory pool,
  uint256 earnings,
  uint256 borrowAmount
) internal pure returns (uint256 unassignedEarnings, uint256 backupEarnings) {
  backupEarnings = borrowAmount == 0
    ? 0
    : earnings.mulDivDown(borrowAmount - Math.min(backupSupplied(pool), borrowAmount), borrowAmount);  // @audit, here
  unassignedEarnings = earnings - backupEarnings;
}
```
When `borrowers` `repay` their `debt` before the `maturity date`, the actual repaid amount is reduced based on the current `unassignedEarnings` of that `maturity pool`.
```solidity
function noTransferRepayAtMaturity(
  uint256 maturity,
  uint256 positionAssets,
  uint256 maxAssets,
  address borrower,
  bool canDiscount
) internal returns (uint256 actualRepayAssets) {
  if (block.timestamp < maturity) {
    if (canDiscount) {
      // calculate the deposit fee considering the amount of debt the account'll pay
      (uint256 discountFee, uint256 backupFee) = pool.calculateDeposit(principalCovered, backupFeeRate);  // @audit, here

      // remove the fee from unassigned earnings
      pool.unassignedEarnings -= discountFee + backupFee;

      // the fee charged to the fixed pool supplier goes to the earnings accumulator
      earningsAccumulator += backupFee;

      // the fee gets discounted from the account through `actualRepayAssets`
      actualRepayAssets = debtCovered - discountFee;  // @audit, here
    } else {
      actualRepayAssets = debtCovered;
    }
  }
}
```
This reduction is propotional to the `borrower`'s `borrow` amount.
```solidity
function calculateDeposit(
  Pool memory pool,
  uint256 amount,
  uint256 backupFeeRate
) internal pure returns (uint256 yield, uint256 backupFee) {
  uint256 memBackupSupplied = backupSupplied(pool);
  if (memBackupSupplied != 0) {
    yield = pool.unassignedEarnings.mulDivDown(Math.min(amount, memBackupSupplied), memBackupSupplied);  // @audit, here
    backupFee = yield.mulWadDown(backupFeeRate);
    yield -= backupFee;
  }
}
```
The `interest rate` increases when new `borrows` occur, meaning that later `borrowers` pay more `fees` than earlier `borrowers`, consequently increasing the `unassignedEarnings` in proportion to that.
As a result, earlier `borrowers` can take more funds from `unassignedEarnings` than their original `fee`, potentially resulting in a net `profit` from these operations.

Let's consider an example.
`ALICE` `borrows` at a `maturity` of `12 weeks`, and `BOB` is also going to `borrow` at the same `maturity`.
At that time, the `interest rate` is larger than one for `ALICE`. (for testing purpose, set the `interest rate` manually in the test)
Consequently, the `fee` added to the `unassignedEarnings` of that `maturity pool` from `BOB` is larger than that of `ALICE` per unit of `borrowing funds`.
As a result, if `ALICE` repays the `debt` immediately, she can take `funds` from `unassignedEarnings` in proportion to her `borrowed amount`, obtaining more `funds` than her `fee`.
Consequently, she repays an amount less than the `principal`, and this difference represents a `net profit`.

Please check below log.
```solidity
borrowed amount       ==>   2000000000000000000
owed amount           ==>   2046027397260273972
actual repaid amount  ==>   1992653319283456270
net profit            ==>   7346680716543730
```

Please add below test to the `Market.t.sol`.
```solidity
function testRepayBeforeMaturity() external {
  market.deposit(1000 ether, address(this));
  market.deposit(100 ether, ALICE);
  market.deposit(100 ether, BOB);

  irm.setRate(0.1e18);
  uint256 borrowedAssets = 2 ether;
  vm.prank(ALICE);
  uint256 positionAssets = market.borrowAtMaturity(12 weeks, borrowedAssets, 2 * borrowedAssets, ALICE, ALICE);

  irm.setRate(0.13e18);
  vm.prank(BOB);
  market.borrowAtMaturity(12 weeks, 50 ether, 100 ether, BOB, BOB);

  vm.prank(ALICE);
  uint256 actualRepayAssets = market.repayAtMaturity(12 weeks, 100 ether, 100 ether, ALICE);

  console2.log("borrowed amount       ==>  ", borrowedAssets);
  console2.log("owed amount           ==>  ", positionAssets);
  console2.log("actual repaid amount  ==>  ", actualRepayAssets);
  console2.log("net profit            ==>  ", borrowedAssets - actualRepayAssets);
}
```
## Impact

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L299-L306
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/utils/FixedLib.sol#L59-L65
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L338-L341
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/utils/FixedLib.sol#L145-L148
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L497-L506
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/utils/FixedLib.sol#L24-L28
## Tool used

Manual Review

## Recommendation
We can prevent this by modifying the below function.
```solidity
function noTransferRepayAtMaturity(
  uint256 maturity,
  uint256 positionAssets,
  uint256 maxAssets,
  address borrower,
  bool canDiscount
) internal returns (uint256 actualRepayAssets) {
  FixedLib.Position memory position = fixedBorrowPositions[maturity][borrower];

  if (block.timestamp < maturity) {
    if (canDiscount) {
      // calculate the deposit fee considering the amount of debt the account'll pay
      (uint256 discountFee, uint256 backupFee) = pool.calculateDeposit(principalCovered, backupFeeRate);  

      // remove the fee from unassigned earnings
      pool.unassignedEarnings -= discountFee + backupFee;

      // the fee charged to the fixed pool supplier goes to the earnings accumulator
      earningsAccumulator += backupFee;

      // the fee gets discounted from the account through `actualRepayAssets`
      actualRepayAssets = debtCovered - discountFee;  
+      if (actualRepayAssets < position.principal) {
+          earningsAccumulator += position.principal - actualRepayAssets ;
+          actualRepayAssets = position.principal;
+      }
    } else {
      actualRepayAssets = debtCovered;
    }
  }
}
```
Or we can apply `default penalty fees` to the `borrower`.