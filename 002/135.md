Trendy Cedar Wasp

medium

# Calculate earnings incorrectly when fixed rate depositors withdraw before maturity

## Summary

## Vulnerability Detail
Here is the `Pool struct` representing a specific `maturity pool`.
```solidity
struct Pool {
  uint256 borrowed;
  uint256 supplied;
  uint256 unassignedEarnings;
  uint256 lastAccrual;
}
```
The `borrowed` refers to the amount borrowed by `fixed rate borrowers`, while `supplied` refers to the amount provided by `fixed rate depositors`.
`Fees` are not included in these `variables`.

When users deposit into the `fixed rate pool`, they will receive some `fees` at `maturity`.
```solidity
function depositAtMaturity(
  uint256 maturity,
  uint256 assets,
  uint256 minAssetsRequired,
  address receiver
) external whenNotPaused whenNotFrozen returns (uint256 positionAssets) {
  (uint256 fee, uint256 backupFee) = pool.calculateDeposit(assets, backupFeeRate);
  positionAssets = assets + fee;  // @audit, here
}
```
If `depositors` decide to `withdraw` before `maturity`, a `penalty` is applied, reducing the `actual withdrawn amount`.
This `penalty amount` is then distributed into `free lunch fees` and `backup fees`.
```solidity
function withdrawAtMaturity(
  uint256 maturity,
  uint256 positionAssets,
  uint256 minAssetsRequired,
  address receiver,
  address owner
) external whenNotPaused returns (uint256 assetsDiscounted) {
  if (block.timestamp < maturity) {
    uint256 memFloatingAssetsAverage = previewFloatingAssetsAverage();
    uint256 memFloatingDebt = floatingDebt;
    uint256 memFloatingBackupBorrowed = floatingBackupBorrowed;

    uint256 fixedRate = interestRateModel.fixedRate(
      maturity,
      maxFuturePools,
      fixedUtilization(pool.supplied, pool.borrowed, memFloatingAssetsAverage),
      floatingUtilization(memFloatingAssetsAverage, memFloatingDebt),
      globalUtilization(memFloatingAssetsAverage, memFloatingDebt, memFloatingBackupBorrowed)
    );
    assetsDiscounted = positionAssets.divWadDown(1e18 + fixedRate.mulDivDown(maturity - block.timestamp, 365 days));  // @audit, here
  } else {
    assetsDiscounted = positionAssets;
  }

  if (assetsDiscounted < minAssetsRequired) revert Disagreement();

  spendAllowance(owner, assetsDiscounted);

  // all the fees go to unassigned or to the floating pool
  (uint256 unassignedEarnings, uint256 newBackupEarnings) = pool.distributeEarnings(  // @audit, here
    chargeTreasuryFee(positionAssets - assetsDiscounted),
    assetsDiscounted // @audit, here
  );
  pool.unassignedEarnings += unassignedEarnings;
  collectFreeLunch(newBackupEarnings);
}
```
In the `distributeEarnings` function, the second parameter `borrowAmount` is meant to represent the amount reduced from the `supplied` in this case.
```solidity
function distributeEarnings(
  Pool memory pool,
  uint256 earnings,
  uint256 borrowAmount  // @audit, here
) internal pure returns (uint256 unassignedEarnings, uint256 backupEarnings) {
  backupEarnings = borrowAmount == 0
    ? 0
    : earnings.mulDivDown(borrowAmount - Math.min(backupSupplied(pool), borrowAmount), borrowAmount);
  unassignedEarnings = earnings - backupEarnings;
}
```
However, we are using the wrong value.
The actual removed supply amount should be as follows:
```solidity
function withdrawAtMaturity(
  uint256 maturity,
  uint256 positionAssets,
  uint256 minAssetsRequired,
  address receiver,
  address owner
) external whenNotPaused returns (uint256 assetsDiscounted) {
  {
    // remove the supply from the fixed rate pool
    uint256 newFloatingBackupBorrowed = floatingBackupBorrowed +
      pool.withdraw(
        FixedLib.Position(position.principal, position.fee).scaleProportionally(positionAssets).principal  // @audit, here
      );
    if (newFloatingBackupBorrowed + floatingDebt > floatingAssets) revert InsufficientProtocolLiquidity();
    floatingBackupBorrowed = newFloatingBackupBorrowed;
  }
}
```
This is what is removed from the `supplied` of that `maturity pool`.
## Impact

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/utils/FixedLib.sol#L231-L236
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L247-L248
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L392-L419
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/utils/FixedLib.sol#L140-L149
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/8f6ef1b0868d3ea3a98a5ab7e8b3a164857681d7/protocol/contracts/Market.sol#L385
## Tool used

Manual Review

## Recommendation
```solidity
```solidity
function withdrawAtMaturity(
  uint256 maturity,
  uint256 positionAssets,
  uint256 minAssetsRequired,
  address receiver,
  address owner
) external whenNotPaused returns (uint256 assetsDiscounted) {

  // all the fees go to unassigned or to the floating pool
  (uint256 unassignedEarnings, uint256 newBackupEarnings) = pool.distributeEarnings(  // @audit, here
    chargeTreasuryFee(positionAssets - assetsDiscounted),
-    assetsDiscounted 
+   FixedLib.Position(position.principal, position.fee).scaleProportionally(positionAssets).principal
  );
  pool.unassignedEarnings += unassignedEarnings;
  collectFreeLunch(newBackupEarnings);
}
```
```