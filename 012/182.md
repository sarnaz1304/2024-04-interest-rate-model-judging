Stable Fiery Pangolin

medium

# EscrowedEXA token can still be transferable even though protocol's design and intention are intended to make EscrowedEXA token untransferable

## Summary
Although the protocol's design and intention are intended to disallow transfers of the EscrowedEXA token, the EscrowedEXA token owner can call the `EscrowedEXA.vest` function for the receiving user and such receiving user can then call the `EscrowedEXA.cancel` function immediately to transfer the corresponding EscrowedEXA tokens.

## Vulnerability Detail
The `initialize` function shown in the Code Snippet section shows that `address(0)` has the `TRANSFERRER_ROLE`. Since the `_beforeTokenTransfer` function shown in the Code Snippet section executes `if (!hasRole(TRANSFERRER_ROLE, from) && !hasRole(TRANSFERRER_ROLE, to)) revert Untransferable()`, where `address(0)` has the `TRANSFERRER_ROLE`, the EscrowedEXA token is untransferable.

However, after some EscrowedEXA tokens are minted, the owner of these tokens can call the `vest` function shown in the Code Snippet section to start a vesting stream for another receiving user with the number of these minted EscrowedEXA tokens as the `amount` input. Immediately after such `vest` call, the receiving user would call the `cancel` function shown in the Code Snippet section to cancel the corresponding vesting stream, which executes `_mint(msg.sender, refundableAmount)`, where `refundableAmount` equals the minted EscrowedEXA token amount. As a result, the original owner of these minted EscrowedEXA tokens successfully transfers these tokens to the receiving user even though the EscrowedEXA token should be untransferable.

For POC, please add the following test in `protocol\test\EscrowedEXA.t.sol`. This test will pass to demonstrate the described scenario.

```solidity
  function test_callVestAndCancelToTransferEsEXA() external {
    address CHARLIE = address(123);

    uint256 amount = 1_000 ether;
    uint256 ratio = esEXA.reserveRatio();
    uint256 reserve = amount.mulWadDown(ratio);
    esEXA.mint(amount, address(this));

    // transferring 1_000 ether esEXA reverts because esEXA should be untransferable
    vm.expectRevert(Untransferable.selector);
    esEXA.transfer(CHARLIE, amount);

    // at this moment, Charlie has 0 esEXA
    uint256 esEXABeforeCharlie = esEXA.balanceOf(CHARLIE);
    assertEq(esEXABeforeCharlie, 0);

    // start a vesting stream for Charlie to vest 1_000 ether EXA
    uint256 streamId = esEXA.vest(uint128(amount), CHARLIE, ratio, esEXA.vestingPeriod());

    vm.startPrank(CHARLIE);

    // Charlie immediately cancels the corresponding vesting stream
    uint256[] memory streamIds = new uint256[](1);
    streamIds[0] = streamId;
    esEXA.cancel(streamIds);

    vm.stopPrank();

    // Charlie now has 1_000 ether esEXA,
    //   which means that the 1_000 ether esEXA has been transferred to Charlie
    //   even though esEXA should be untransferable
    uint256 esEXAAfterCharlie = esEXA.balanceOf(CHARLIE);
    assertEq(esEXAAfterCharlie, amount);
    assertGt(esEXAAfterCharlie, esEXABeforeCharlie);
  }
```

## Impact
The protocol's design and intention, which should prevent the EscrowedEXA token from being transferable, are broken.

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/periphery/EscrowedEXA.sol#L43-L56
```solidity
  function initialize(uint40 vestingPeriod_, uint256 reserveRatio_) external initializer {
    ...
    /// @dev address(0) holds the TRANSFERRER_ROLE so the token can be minted or burnt.
    _grantRole(TRANSFERRER_ROLE, address(0));
    ...
  }
```

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/periphery/EscrowedEXA.sol#L59-L62
```solidity
  function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual override {
    if (!hasRole(TRANSFERRER_ROLE, from) && !hasRole(TRANSFERRER_ROLE, to)) revert Untransferable();
    super._beforeTokenTransfer(from, to, amount);
  }
```

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/periphery/EscrowedEXA.sol#L89-L110
```solidity
  function vest(uint128 amount, address to, uint256 maxRatio, uint256 maxPeriod) public returns (uint256 streamId) {
    ...
    _burn(msg.sender, amount);
    uint256 reserve = amount.mulWadUp(reserveRatio);
    exa.safeTransferFrom(msg.sender, address(this), reserve);
    streamId = sablier.createWithDurations(
      CreateWithDurations({
        asset: exa,
        sender: address(this),
        recipient: to,
        totalAmount: amount,
        cancelable: true,
        transferable: true,
        durations: Durations({ cliff: 0, total: vestingPeriod }),
        broker: Broker({ account: address(0), fee: 0 })
      })
    );
    reserves[streamId] = reserve;
    ...
  }
```

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/periphery/EscrowedEXA.sol#L133-L148
```solidity
  function cancel(uint256[] memory streamIds) external returns (uint256 streamsReserves) {
    uint128 refundableAmount;
    for (uint256 i = 0; i < streamIds.length; ++i) {
      uint256 streamId = streamIds[i];
      checkStream(streamId);
      assert(msg.sender == sablier.getRecipient(streamId));
      streamsReserves += reserves[streamId];
      delete reserves[streamId];
      refundableAmount += sablier.refundableAmountOf(streamId);
      withdrawMax(streamId);
      sablier.cancel(streamId);
    }
    emit Cancel(msg.sender, streamIds);
    _mint(msg.sender, refundableAmount);
    exa.safeTransfer(msg.sender, streamsReserves);
  }
```

## Tool used
Manual Review

## Recommendation
The `cancel` function can be updated to revert until a reasonable time has passed after the corresponding vesting stream was created.