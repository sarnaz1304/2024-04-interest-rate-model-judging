Brilliant Sable Bison

medium

# Reward indexes can be wrong

## Summary
The floating debt amount is used without acquiring interest inside the handleBorrow and handleDeposit functions, which can cause wrong reward allocations.
## Vulnerability Detail
When the user calls the withdraw function to withdraw his collaterals, the handleDeposit function is called to update the user's reward index. 

```solidity
function handleDeposit(address account) external {
    Market market = Market(msg.sender);
    AccountOperation[] memory ops = new AccountOperation[](1);
    ops[0] = AccountOperation({ operation: false, balance: market.balanceOf(account) });

    Distribution storage dist = distribution[market];
    uint256 available = dist.availableRewardsCount;
    for (uint128 r = 0; r < available; ) {
      update(account, market, dist.availableRewards[r], ops);
      unchecked {
        ++r;
      }
    }
  }
```

The newUndistributed reward amount is calculated using the floating debt amount inside the previewAllocation function. 

```solidity
function previewAllocation(
    RewardData storage rewardData,
    Market market,
    uint256 deltaTime
  ) internal view returns (uint256 borrowIndex, uint256 depositIndex, uint256 newUndistributed) {
    TotalMarketBalance memory m;
    m.floatingDebt = market.floatingDebt(); 
    m.floatingAssets = market.floatingAssets(); 
    //........
    }
    uint256 target;
    {
      uint256 targetDebt = rewardData.targetDebt;
      target = m.debt < targetDebt ? m.debt.divWadDown(targetDebt) : 1e18;
    }
    uint256 rewards;
    {
      uint256 releaseRate = rewardData.releaseRate;
      uint256 lastUndistributed = rewardData.lastUndistributed;
      t.period = t.end - t.start;
      uint256 distributionFactor = t.period > 0
        ? rewardData.undistributedFactor.mulDivDown(target, t.period * 1e18)
        : 0;
      if (block.timestamp <= t.end) {
        if (distributionFactor > 0) {
          uint256 exponential = uint256((-int256(distributionFactor * deltaTime)).expWad());
          newUndistributed =
            lastUndistributed.mulWadDown(exponential) +
            releaseRate.mulDivDown(1e18 - target, distributionFactor).mulWadUp(1e18 - exponential);
        } else {
          newUndistributed = lastUndistributed + releaseRate.mulWadDown(1e18 - target) * deltaTime;
        }
        rewards = uint256(int256(releaseRate * deltaTime) - (int256(newUndistributed) - int256(lastUndistributed)));
```
The problem is that the floating debt amount is not updated during the function call and can be outdated due to not accruing any interest. This can cause all reward and index calculations to be wrong. 

If the user withdraws all his collateral with the wrong index, he loses some of his rewards. Even if the user calls the claim function after the floating debt is updated, he canâ€™t claim any rewards because his operation balance is 0 now.

## Impact
Users can lose their rewards.
## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/RewardsController.sol#L470-L594
## Tool used

Manual Review

## Recommendation
Use the updated version of the floating debt like in the previewRefund function.

```solidity
function previewRefund(uint256 shares) public view returns (uint256) {
    uint256 supply = totalFloatingBorrowShares; // Saves an extra SLOAD if totalFloatingBorrowShares is non-zero.

    return supply == 0 ? shares : shares.mulDivUp(totalFloatingBorrowAssets(), supply);
  }
```
The previewRefund function does not update the floating debt but uses the totalFloatingBorrowAssets, which returns the floating debt with interest.

```solidity
/// @notice Calculates the total floating debt, considering elapsed time since last update and current interest rate.
  /// @return actual floating debt plus projected interest.
  function totalFloatingBorrowAssets() public view returns (uint256) {
    uint256 memFloatingDebt = floatingDebt;
    uint256 memFloatingAssets = floatingAssets;
    uint256 newDebt = memFloatingDebt.mulWadDown(
      interestRateModel
        .floatingRate(
          floatingUtilization(memFloatingAssets, memFloatingDebt),
          globalUtilization(memFloatingAssets, memFloatingDebt, floatingBackupBorrowed)
        )
        .mulDivDown(block.timestamp - lastFloatingDebtUpdate, 365 days)
    );
    return memFloatingDebt + newDebt;
  }
```