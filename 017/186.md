Stable Fiery Pangolin

medium

# Slippage controls for `deposit`, `mint`, `withdraw`, and `redeem` functions of `Market` contract are missing

## Summary
Because the `Market` contract's `deposit`, `mint`, `withdraw`, and `redeem` functions have no slippage controls, EOA accounts that call these functions can mint/burn `shares` and deposit/withdraw `assets` that are different from the expected values returned by the `previewDeposit`/`previewWithdraw` and `previewMint`/`previewRedeem` functions. With these `deposit`, `mint`, `withdraw`, and `redeem` functions alone, the `Market` contract is insufficient to be compliant to the EIP-4626 standard.

## Vulnerability Detail
The `Market` contract uses Solmate's `ERC4626` contract's `mint` and `deposit` functions while overrides the `withdraw` and `redeem` functions as shown in the Code Snippet section. Yet, none of these functions have input parameters for controlling slippages.

When EOA accounts interact with the `Market` contract, they can call the `previewDeposit`, `previewMint`, `previewWithdraw`, and `previewRedeem` functions to preview `shares` that will be minted when calling the `deposit` function, `assets` that will be deposited when calling the `mint` function, `shares` that will be burned when calling the `withdraw` function, and `assets` that will be withdrawn when calling the `redeem` function. However, after these EOA accounts' `previewDeposit`, `previewMint`, `previewWithdraw`, and `previewRedeem` transactions are completed and while their `deposit`, `mint`, `withdraw`, and `redeem` transactions are pending in the mempool, other transactions that update the value returned by the `totalAssets` function shown in the Code Snippet section can occur and be completed; when these `deposit`, `mint`, `withdraw`, and `redeem` transactions are completed, the `shares` that are minted, `assets` that are deposited, `shares` that are burned, and `assets` that are withdrawn can be different from the expected values returned by these `previewDeposit`, `previewMint`, `previewWithdraw`, and `previewRedeem` transactions.

Also, according to https://eips.ethereum.org/EIPS/eip-4626#security-considerations, `If implementors intend to support EOA account access directly, they should consider adding an additional function call for ``deposit``/``mint``/``withdraw``/``redeem`` with the means to accommodate slippage loss or unexpected deposit/withdrawal limits, since they have no other means to revert the transaction if the exact output amount is not achieved`. Thus, having no slippage controls in the `deposit`, `mint`, `withdraw`, and `redeem` functions also violates the EIP-4626 standard.

For POC, please add the following test in `protocol\test\Market.t.sol`. This test will pass to demonstrate the described scenario for the `mint` function. The cases for the `deposit`, `withdraw`, and `redeem` functions can be similar.

```solidity
  function test_depositMoreAssetsThanExpectedWhenCallingMintFunction() external {
    uint256 assets = 10_000 ether;
    uint256 maturity = FixedLib.INTERVAL * 2;
    uint256 anotherMaturity = FixedLib.INTERVAL * 3;
    market.deposit(assets, address(this));

    vm.warp(FixedLib.INTERVAL);
    market.borrowAtMaturity(maturity, 10 ether, 20 ether, address(this), address(this));

    vm.startPrank(BOB);

    market.deposit(10_000 ether, BOB);
    market.borrowAtMaturity(anotherMaturity, 1_000 ether, 1_100 ether, BOB, BOB);

    vm.stopPrank();

    vm.warp(maturity + 1 days);

    uint256 sharesToMintAlice = 1 ether;

    // Alice calls previewMint function to determine how much assets need to deposited for minting 1 ether shares,
    //   and then call mint function to mint these 1 ether shares
    vm.prank(ALICE);
    uint256 assetsFromAliceExpected = market.previewMint(sharesToMintAlice);

    // while Alice's mint transaction is in mempool, Bob's repayAtMaturity transaction is executed in same block
    vm.prank(BOB);
    market.repayAtMaturity(maturity, 100 ether, 100 ether, address(this));

    // after Alice's mint transaction is executed, she has deposited more assets than expected for minting same 1 ether shares
    vm.prank(ALICE);
    uint256 assetsFromAliceActual = market.mint(sharesToMintAlice, ALICE);
    assertGt(assetsFromAliceActual, assetsFromAliceExpected);
  }
```

## Impact
These EOA accounts can mint less `shares` than expected, deposit more `assets` than expected, burn more `shares` than expected, and withdraw less `assets` than expected. Moreover, the `Market` contract's current `deposit`, `mint`, `withdraw`, and `redeem` functions alone are not sufficient to be compliant to the EIP-4626 standard.

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L9
```solidity
import { ERC4626, ERC20, SafeTransferLib } from "solmate/src/mixins/ERC4626.sol";
```

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L15
```solidity
contract Market is Initializable, AccessControlUpgradeable, PausableUpgradeable, ERC4626 {
```

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L725-L731
```solidity
  function withdraw(uint256 assets, address receiver, address owner) public override returns (uint256 shares) {
    auditor.checkShortfall(this, owner, assets);
    RewardsController memRewardsController = rewardsController;
    if (address(memRewardsController) != address(0)) memRewardsController.handleDeposit(owner);
    shares = super.withdraw(assets, receiver, owner);
    emitMarketUpdate();
  }
```

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L739-L745
```solidity
  function redeem(uint256 shares, address receiver, address owner) public override returns (uint256 assets) {
    auditor.checkShortfall(this, owner, previewRedeem(shares));
    RewardsController memRewardsController = rewardsController;
    if (address(memRewardsController) != address(0)) memRewardsController.handleDeposit(owner);
    assets = super.redeem(shares, receiver, owner);
    emitMarketUpdate();
  }
```

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L925-L949
```solidity
  function totalAssets() public view override returns (uint256) {
    unchecked {
      uint256 backupEarnings = 0;

      uint256 latestMaturity = block.timestamp - (block.timestamp % FixedLib.INTERVAL);
      uint256 maxMaturity = latestMaturity + maxFuturePools * FixedLib.INTERVAL;

      for (uint256 maturity = latestMaturity; maturity <= maxMaturity; maturity += FixedLib.INTERVAL) {
        FixedLib.Pool storage pool = fixedPools[maturity];
        uint256 lastAccrual = pool.lastAccrual;

        if (maturity > lastAccrual) {
          backupEarnings += block.timestamp < maturity
            ? pool.unassignedEarnings.mulDivDown(block.timestamp - lastAccrual, maturity - lastAccrual)
            : pool.unassignedEarnings;
        }
      }

      return
        floatingAssets +
        backupEarnings +
        accumulatedEarnings() +
        (totalFloatingBorrowAssets() - floatingDebt).mulWadDown(1e18 - treasuryFeeRate);
    }
  }
```

## Tool used
Manual Review

## Recommendation
The `Market` contract can be updated to add four new functions that respectively call the `deposit`, `mint`, `withdraw`, and `redeem` functions and allow the user to input the corresponding slippage control; then, when the corresponding slippage control is violated, such as when `assets` returned by the `mint` function is more than specified, calling such function should revert.