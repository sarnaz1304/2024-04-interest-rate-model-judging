Generous Denim Perch

medium

# interestRateModule::constructor() doesn't assert that fixedAllocation is greater than 0

## Summary

`interestRateModule::constructor()` doesn't assert that `p.fixedAllocation > 0` when checking all other important parameters. This can lead to a divide by 0 revert in certain calculations.

## Vulnerability Detail

The constructor ensures the parameters of the `InterestRateModel` are within appropriate bounds even if they are set by trusted admins, esepcially ensuring they are greater than 0 if they are ever utilised for division:
[InterestRateModel::constructor()](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/InterestRateModel.sol#L48-L86)
```solidity
  constructor(Parameters memory p, Market market_) {
    assert(
      p.minRate > 0 &&
        p.naturalRate > 0 &&
        p.maxUtilization > 1e18 &&
        p.naturalUtilization > 0 &&
        p.naturalUtilization < 1e18 &&
        p.growthSpeed > 0 &&
        p.sigmoidSpeed > 0 &&
        p.spreadFactor > 0 &&
        p.maturitySpeed > 0 &&
        p.maxRate > 0 &&
        p.maxRate <= 15_000e16
    );

    _parameters = p;
    growthSpeed = p.growthSpeed.toInt256();
    sigmoidSpeed = p.sigmoidSpeed.toInt256();
    spreadFactor = p.spreadFactor.toInt256();
    maturitySpeed = p.maturitySpeed.toInt256();
    floatingMaxUtilization = p.maxUtilization;
    naturalUtilization = p.naturalUtilization;

    floatingCurveA =
      ((p.naturalRate.mulWadUp(
        uint256(((growthSpeed * (1e18 - int256(p.naturalUtilization / 2)).lnWad()) / 1e18).expWad())
      ) - p.minRate) *
        (p.maxUtilization - p.naturalUtilization) *
        (p.maxUtilization)) /
      (p.naturalUtilization * 1e18);
    floatingCurveB = int256(p.minRate) - int256(floatingCurveA.divWadDown(p.maxUtilization));

    market = market_;
    fixedCurveA = address(market_) != address(0) ? floatingCurveA : 0;
    fixedCurveB = address(market_) != address(0) ? floatingCurveB : int256(0);
    fixedMaxUtilization = address(market_) != address(0) ? p.maxUtilization : 0;

    timePreference = p.timePreference;
    fixedAllocation = p.fixedAllocation;
```

`fixedAllocation` is used as the denominator during division in:
[InterestRateModel::fixedRate](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/InterestRateModel.sol#L103-L117)
```solidity
  function fixedRate(
    uint256 maturity,
    uint256 maxPools,
    uint256 uFixed,
    uint256 uFloating,
    uint256 uGlobal
  ) public view returns (uint256) {
    if (block.timestamp >= maturity) revert AlreadyMatured();
    if (uFixed > uGlobal) revert UtilizationExceeded();
    if (uGlobal == 0) return baseRate(uFloating, 0);

    FixedVars memory v;
>    v.sqFNatPools = (maxPools * 1e18).divWadDown(fixedAllocation);
```

## Impact

Due to no check that `fixedAllocation ` is greater than 0, any calls to `fixedRate` will revert when `fixedAllocation` is `0`. Other values are ensured to be within appropriate bound within the codebase, however `fixedAllocation` was missed.

## Code Snippet

[InterestRateModel::constructor()](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/InterestRateModel.sol#L48-L86)
[InterestRateModel::fixedRate](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/InterestRateModel.sol#L103-L117)

## Tool used

Manual Review

## Recommendation

Check that `p.fixedAllocation` is greater than 0 within the`assert` check in the constructor to ensure the model creation will fail if an incorrect parameter is utilised.

```diff
  constructor(Parameters memory p, Market market_) {
    assert(
      p.minRate > 0 &&
        p.naturalRate > 0 &&
        p.maxUtilization > 1e18 &&
        p.naturalUtilization > 0 &&
        p.naturalUtilization < 1e18 &&
        p.growthSpeed > 0 &&
        p.sigmoidSpeed > 0 &&
        p.spreadFactor > 0 &&
        p.maturitySpeed > 0 &&
+       p.fixedAllocation > 0 &&
        p.maxRate > 0 &&
        p.maxRate <= 15_000e16
    );
```