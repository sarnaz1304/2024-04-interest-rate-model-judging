Generous Denim Perch

medium

# InterestRateModel::fixedRate() base and maxRate check rounds incorrectly, which causes return if statement to be off by 1

## Summary

When `InterestRateModel::fixedRate()` has finished calculating the `base` and `spread`, it returns either `base * spread` or `maxRate`. However the if check does not follow the comment and exact intended behaviour, leading to a rounding issue.

## Vulnerability Detail

[InterestRateModel::fixedRate()](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/InterestRateModel.sol#L102-L140)
```solidity
  /// @return the minimum between `base * spread` and `maxRate` with given conditions.
  function fixedRate(
    uint256 maturity,
    uint256 maxPools,
    uint256 uFixed,
    uint256 uFloating,
    uint256 uGlobal
  ) public view returns (uint256) {
...SKIP!...
    if (base >= maxRate.divWadDown(spread)) return maxRate;
    return base.mulWadUp(spread);
```

The comment states that the minimum between `base * spread` and `maxRate` is returned, however the current code's implementation of the logic differes from the comment and exact intended logic.

The code is aiming to return the minimum out of (as can be seen from the return values):

$$base.mulWadUp(spread) >= maxRate$$

This can be expanded as (in cases where there is a remainder left after division):

$$(base\cdot{spread/WAD}).roundUp >= maxRate$$

$$(base\cdot{spread/WAD}) + (1) >= maxRate$$

When rearranging the actual implementation:

$$base >= maxRate.divWadDown(spread)$$

$$base >= (maxRate\cdot{WAD/spread}).roundDown$$

$$base >= (maxRate\cdot{WAD/spread})$$

Which is almost the same, however the rounding from the first equation is not being subtracted. The correct formula would be:

$$base >= (maxRate\cdot{WAD/spread}) -  (1)$$

Therefore:

`if (base >= maxRate.divWadDown(spread))` != `if (base.mulWadUp(spread) >= maxRate)`

## Impact

The implemented calculation `(base >= maxRate.divWadDown(spread))` has the right hand side be 1 larger than it should be based on the code comments as shown in the above expansions. Meaning that the returns value from `InterestRateModel::fixedRate()` will be return `base.mulWadUp(spread)` rather than `maxRate` in some edge cases.

This can cause users who call `Market::withdrawAtMaturity()` to get a slightly smaller discount, as the return value from `fixedRate()` is used to divide position assets.

## Code Snippet

[FixedPointMathLib.sol#L20-L26](https://github.com/transmissions11/solmate/blob/main/src/utils/FixedPointMathLib.sol#L20-L26)
```solidity
    function mulWadUp(uint256 x, uint256 y) internal pure returns (uint256) {
        return mulDivUp(x, y, WAD); // Equivalent to (x * y) / WAD rounded up.
    }

    function divWadDown(uint256 x, uint256 y) internal pure returns (uint256) {
        return mulDivDown(x, WAD, y); // Equivalent to (x * WAD) / y rounded down.
    }
```

[FixedPointMathLib::mulDivUp()](https://github.com/transmissions11/solmate/blob/main/src/utils/FixedPointMathLib.sol#L53-L69)
```solidity
    function mulDivUp(
        uint256 x,
        uint256 y,
        uint256 denominator
    ) internal pure returns (uint256 z) {
        /// @solidity memory-safe-assembly
        assembly {
            // Equivalent to require(denominator != 0 && (y == 0 || x <= type(uint256).max / y))
            if iszero(mul(denominator, iszero(mul(y, gt(x, div(MAX_UINT256, y)))))) {
                revert(0, 0)
            }

            // If x * y modulo the denominator is strictly greater than 0,
            // 1 is added to round up the division of x * y by the denominator.
            z := add(gt(mod(mul(x, y), denominator), 0), div(mul(x, y), denominator))
        }
    }
```

As can be seen above and below, `mulDivUp` and `mulDivDown` return a different calculation. The difference is that `mulDivUp` adds 1 if `mod(mul(x, y), denominator)` is greater than 0 (any remainer after division).

[FixedPointMathLib::mulDivDown()](https://github.com/transmissions11/solmate/blob/main/src/utils/FixedPointMathLib.sol#L36-L51)
```solidity
    function mulDivDown(
        uint256 x,
        uint256 y,
        uint256 denominator
    ) internal pure returns (uint256 z) {
        /// @solidity memory-safe-assembly
        assembly {
            // Equivalent to require(denominator != 0 && (y == 0 || x <= type(uint256).max / y))
            if iszero(mul(denominator, iszero(mul(y, gt(x, div(MAX_UINT256, y)))))) {
                revert(0, 0)
            }

            // Divide x * y by the denominator.
            z := div(mul(x, y), denominator)
        }
    }
```

## Tool used

Manual Review

## Recommendation

Change the if statement to match the values that will be returned, ensuring no rounding issues affect the return value:

[InterestRateModel.sol#L138-L139](https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/InterestRateModel.sol#L138-L139)
```diff
-    if (base >= maxRate.divWadDown(spread)) return maxRate;
+    if (base.mulWadUp(spread) >= maxRate) return maxRate;
    return base.mulWadUp(spread);
```

or specify in the code comments about the edge case due to rounding.