Formal Pickle Goldfish

high

# `accountRewards` is calculated by incorrect way, so it will be result in unfair accrued rewards.

## Summary
When first deposit/borrow operation is performed by an account, accrued rewards can be greater than next deposited/borrowed operation.

## Vulnerability Detail
If an account makes a rewarded deposit or borrow, protocol updates all the rewards for this account in this market.
At that time, accrued rewards is calculated by following function.
```solidity
function accountRewards(
    uint256 balance,
    uint256 globalIndex,
    uint256 accountIndex,
    uint256 baseUnit
  ) internal pure returns (uint256) {
    return balance.mulDivDown(globalIndex - accountIndex, baseUnit);
  }
``` 
`accountRewards` is proportional to `balance` and `globalIndex - accountIndex`, and `accountIndex` is zero for new account that doesn't have any operation before.

```solidity
function update(address account, Market market, ERC20 reward, AccountOperation[] memory ops) internal {
    uint256 baseUnit = distribution[market].baseUnit;
    RewardData storage rewardData = distribution[market].rewards[reward];
    {
      uint256 lastUpdate = rewardData.lastUpdate;
      // `lastUpdate` can be greater than `block.timestamp` if distribution is set to start on a future date
      if (block.timestamp > lastUpdate) {
        (uint256 borrowIndex, uint256 depositIndex, uint256 newUndistributed) = previewAllocation(
          rewardData,
          market,
          block.timestamp - lastUpdate
        );
        if (borrowIndex > type(uint128).max || depositIndex > type(uint128).max) revert IndexOverflow();
        rewardData.borrowIndex = uint128(borrowIndex);
        rewardData.depositIndex = uint128(depositIndex);
        rewardData.lastUpdate = uint32(block.timestamp);
        rewardData.lastUndistributed = newUndistributed;
        emit IndexUpdate(market, reward, borrowIndex, depositIndex, newUndistributed, block.timestamp);
      }
    }

    mapping(bool => Account) storage operationAccount = rewardData.accounts[account];
    for (uint256 i = 0; i < ops.length; ) {
      AccountOperation memory op = ops[i];
@>    Account storage accountData = operationAccount[op.operation];
@>    uint256 accountIndex = accountData.index;
      uint256 newAccountIndex;
      if (op.operation) {
        newAccountIndex = rewardData.borrowIndex;
      } else {
        newAccountIndex = rewardData.depositIndex;
      }
      if (accountIndex != newAccountIndex) {
        accountData.index = uint128(newAccountIndex);
        if (op.balance != 0) {
          uint256 rewardsAccrued = accountRewards(op.balance, newAccountIndex, accountIndex, baseUnit);
          accountData.accrued += uint128(rewardsAccrued);
          emit Accrue(market, reward, account, op.operation, accountIndex, newAccountIndex, rewardsAccrued);
        }
      }
      unchecked {
        ++i;
      }
    }
  }
```
For new account that doesn't have any operations before, `accountData` is empty, so `accountIndex` is zero in above code.
```solidity
rewardsAccrued = balance * (globalIndex - 0) / baseUnit;
```
Accrued rewards for first operation should be zero because this is never used yet.

## Impact
Rewards accrued for first operation of an account will be much greater than next operation.
In result, inconsistent rewards distribution will be occurred, malicious user can use new account for each reward operation(deposit and borrow).

## Code Snippet
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/RewardsController.sol#L336-L337
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/RewardsController.sol#L344-L350
https://github.com/sherlock-audit/2024-04-interest-rate-model/blob/main/protocol/contracts/RewardsController.sol#L439-L446

## Tool used

Manual Review

## Recommendation
Add accrued rewards for non-zero `accountIndex`. 
```solidity
function update(address account, Market market, ERC20 reward, AccountOperation[] memory ops) internal {
    
    __SNIP__
L344:
--    if (accountIndex != newAccountIndex) {
++    if (accountIndex != 0 && accountIndex != newAccountIndex) {
        accountData.index = uint128(newAccountIndex);
        if (op.balance != 0) {
          uint256 rewardsAccrued = accountRewards(op.balance, newAccountIndex, accountIndex, baseUnit);
          accountData.accrued += uint128(rewardsAccrued);
          emit Accrue(market, reward, account, op.operation, accountIndex, newAccountIndex, rewardsAccrued);
        }
      }
      unchecked {
        ++i;
      }
    }
  }
```